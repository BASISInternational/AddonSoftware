rem arr - Customer Name Listing
rem Program arr_namelist v8.0.0 05May2006 (arr_rb)
rem Created by adx_progconv.bbx v1.0.2 (05/05/2006 03:13:35)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Document initializations

            OutVect!=bbjAPI().getSysGui().makeVector()
            HdrVect!=bbjAPI().getSysGui().makeVector()
            BrkVect!=bbjAPI().getSysGui().makeVector()
            rep_date$=date(0:"%Mz/%Dz/%Yd")
            rep_date_stamp$=date(0:"%Yd%Mz%Dz")
            rep_time$=date(0:"%hz:%mz %p")
            rep_time_stamp$=date(0:"%Hz%mz%sz")

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Open/Lock files

    files=6,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="arm-01",ids$[1]="ARM_CUSTMAST"
    files$[2]="arm-02",ids$[2]="ARM_CUSTDET"
    files$[3]="arc_termcode",ids$[3]="ARC_TERMCODE"
    files$[4]="arc_salecode",ids$[4]="ARC_SALECODE"
    files$[5]="arc_terrcode",ids$[5]="ARC_TERRCODE"
    files$[6]="arc_custtype",ids$[6]="ARC_CUSTTYPE"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    arm01_dev=channels[1]
    arm02_dev=channels[2]
    arc_termcode_dev=channels[3]
    arc_salecode_dev=channels[4]
    arc_terrcode_dev=channels[5]
    arc_custtype_dev=channels[6]

rem --- Dimension string templates

    dim arm01a$:templates$[1],arm02a$:templates$[2],arm10a$:templates$[3]
    dim arm10f$:templates$[4],arm10h$:templates$[5],arm10l$:templates$[6]

rem --- Assign form input values to local variables

    sequence$=option!.getOptionData("REPORT_SEQ_MSTY")
    begterms$=option!.getOptionData("AR_TERMS_CODE_1")
    endterms$=option!.getOptionData("AR_TERMS_CODE_2")
    begsales$=option!.getOptionData("SALESPERSON_1")
    endsales$=option!.getOptionData("SALESPERSON_2")
    begterr$=option!.getOptionData("TERRITORY_1")
    endterr$=option!.getOptionData("TERRITORY_2")
    begtype$=option!.getOptionData("CUSTOMER_TYPE_1")
    endtype$=option!.getOptionData("CUSTOMER_TYPE_2")

rem --- Retrieve parameter records

    dim p[5]
    call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",m0$,0,cust_size
    m9$="(###)-000-0000"
    m0=len(m0$)
    m9=len(m9$)

rem --- Column Headings

    dim columns$[3,10]
    columns=3
        columns$[0,0]="Customer",  columns$[0,1]="C",columns$[0,2]="30"
        columns$[1,0]="Address",   columns$[1,1]="C",columns$[1,2]="60"
        columns$[2,0]="Zip/Postal",columns$[2,1]="C",columns$[2,2]="15"
        columns$[3,0]="Phone",     columns$[3,1]="C",columns$[3,2]="20"

rem --- Initializations

    headings=5
    dim headings$[headings],o[4]
    more=1
    page=0
    code=pos(sequence$="MSTY")
    width=132
    total=0
    maxlines=59
    lines=maxlines+1
    nf$="(Not on file)"
    lastvalue$=""
    when$=sysinfo.system_date$
    clock$=""
    terms=1
    salesperson=2
    territory=3
    type=4
    length=2
    keynum=3
    begvalue$=begterms$
    endvalue$=endterms$
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$+" "
    if sequence$="M" headings$[1]=headings$[1]+"By Terms Code"
    if sequence$="S"
       length=3
       keynum=1
       begvalue$=begsales$
       endvalue$=endsales$
       headings$[1]=headings$[1]+"By Salesperson"
    endif
    if sequence$="T"
       length=3
       keynum=2
       begvalue$=begterr$
       endvalue$=endterr$
       headings$[1]=headings$[1]+"By Territory"
    endif
    if sequence$="Y"
       length=3
       keynum=8
       begvalue$=begtype$
       endvalue$=endtype$
       headings$[1]=headings$[1]+"By Customer Type"
    endif
    if begvalue$<>"" begvalue$=begvalue$(1,len(begvalue$)-1)+$FF$
    endvalue=len(endvalue$)
    headings$[5]="[HEADER]"

rem --- Initialize print positions

    o[0]=0
    o[1]=o[0]+m0+1
    o[2]=o[1]+31
    o[3]=18+m0
    o[4]=width-m9
    o[3]=o[4]-12
    w=o[3]-o[2]-2

rem --- Open printer, start progress meter and position header file

    call pgmdir$+"adc_progress.aon","NC","","","","",0,arm02_dev,1,meter_num,status

rem --- Initial Read

   read (arm02_dev,knum=keynum,key=firm_id$+begvalue$,dom=*next)

rem --- Read next Customer Detail (arm-02) record

    while more
          k$=key(arm02_dev,knum=keynum,end=*break)
          if pos(firm_id$=k$)<>1 break
          if endvalue$<>"" if k$(3,endvalue)>endvalue$ break
          call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
          read record (arm02_dev) arm02a$

          rem --- Level break?

          value$=k$(3,length)
          if value$<>lastvalue$ 
             gosub code_break
             gosub subheading
          endif

          rem --- Retrieve the Customer Master (arm-01) record

          dim arm01a$:fattr(arm01a$)
          arm01a.customer_name$=nf$
          k$=firm_id$+arm02a.customer_id$
          find record (arm01_dev,key=k$,dom=*next) arm01a$

          rem --- Prepare line

          address$=""
          x$=arm01a.addr_line_1$+arm01a.addr_line_2$+arm01a.addr_line_3$+arm01a.addr_line_4$+
:            arm01a.city$+arm01a.state_code$
          for x=1 to 120 step 24
              l$=cvs(x$(x,24),1+2+32)
              if l$<>"" then if address$<>"" address$=address$+", "
              address$=address$+l$
          next x
          if len(address$)>w address$=address$(1,w)

          rem --- Print the detail line

          OutVect!.addItem(fnmask$(arm01a.customer_id$(1,cust_size),m0$)+"  "+arm01a.customer_name$)
          OutVect!.addItem(address$)
          OutVect!.addItem(fnzipcode$(arm01a.zip_code$))
          OutVect!.addItem(fnmask$(arm01a.phone_no$,m9$))
          total=total+1
    wend

rem --- All done

    xwk=fnblank(4)
    outVect!.addItem(str(total)+" Customer(s) Listed")
    goto std_exit
                    
subheading: rem --- Subheading by code

    BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
    HdrVect!.addItem(buffer$)
    xwk=fnblank(4)
    return

code_break: rem --- Code sequence break

    dim buffer$(m0+31)
    switch code
           case terms
                dim arm10a$:fattr(arm10a$)
                arm10a.code_desc$=nf$
                k$=firm_id$+"A"+value$
                find record (arc_termcode_dev,key=k$,dom=*next) arm10a$  
                buffer$(1)="Terms "+arm10a.ar_terms_code$+" - "+arm10a.code_desc$
           break
           case salesperson
                dim arm10f$:fattr(arm10f$)
                arm10f.code_desc$=nf$
                k$=firm_id$+"F"+value$
                find record (arc_salecode_dev,key=k$,dom=*next) arm10f$
                buffer$(1)="Salesperson "+arm10f.slspsn_code$+" - "+arm10f.code_desc$
           break
           case territory
                dim arm10h$:fattr(arm10h$)
                arm10h.code_desc$=nf$
                k$=firm_id$+"H"+value$
                find record (arc_terrcode_dev,key=k$,dom=*next) arm10h$  
                buffer$(1)="Territory "+arm10h.territory$+" - "+arm10h.code_desc$
           break
           case type
                dim arm10l$:fattr(arm10l$)
                arm10l.code_desc$=nf$
                k$=firm_id$+"L"+value$
                find record (arc_custtype_dev,key=k$,dom=*next) arm10l$  
                buffer$(1)="Customer Type "+arm10l.customer_type$+" - "+arm10l.code_desc$
           break
           case default
           break
    swend
    lastvalue$=value$
    lines=maxlines+1
    return

rem --- Functions

rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)

rem --- Format zip/postal code

    def fnzipcode$(q$)=fill(10,cvs(q$(1,3)+fill(pos("9"<q$(1,5))>0)+q$(4,2)+
:                      fill(ABS(pos("A">=q$(1,6))>0)-pos("0">q$(6,4))>0,"-")+
:                      q$(6,4)+fill(10),16))

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        if pos("E"=cvs(q1$,4)) goto alpha_mask
:          else return str(-num(q1$,err=alpha_mask):q2$,err=alpha_mask)
        alpha_mask:
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend
rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend
rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),
:                                   str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_missing_params_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

rem #endinclude std_end.src

    end
