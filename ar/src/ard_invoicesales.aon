rem Invoice Sales Drilldown Link
rem Proprietary Software. BASIS International Ltd.
rem Program ID: ard_invoicesales.aon  <Apr 13, 2011>
rem --- Copyright BASIS International Ltd.  All Rights Reserved.

    setesc std_error
    seterr std_error

    enter rec_tpl$,table_chans$[all],status$

rem rec_tpl$       - Current inquiry record
rem table_chans$[] - Barista opened channels array
rem status$        - Returned status: "SKIP" = Skip current record

    art_invdet=fnget_dev("ART_INVDET")
    dim art_invdet$:fnget_tpl$("ART_INVDET")

rem --- Main process

    status$=""

    hdr_key$=rec_tpl.firm_id$+rec_tpl.ar_type$+rec_tpl.customer_id$+rec_tpl.ar_inv_no$
    read(art_invdet,key=hdr_key$,dom=*next)
    while 1
        readrecord(art_invdet,end=*break)art_invdet$
        if art_invdet.firm_id$+art_invdet.ar_type$+art_invdet.customer_id$+art_invdet.ar_inv_no$<>hdr_key$ then break
        if art_invdet.trans_type$="C" then
            rec_tpl.invoice_amt=rec_tpl.invoice_amt+(art_invdet.trans_amt+art_invdet.adjdisc_amt)
        endif
    wend

    if rec_tpl.invoice_amt=0 then status$="SKIP"

    goto exit_prog

rem --- Functions

    def fnget_dev(tmp0$)=num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3))
    def fnget_tpl$(tmp0$)=table_chans$[num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3)),0]

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry

std_error_exit:
    
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
    
std_error_release:

    status=999
    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_error.src
    
exit_prog:rem --- Exit Program

    exit
