rem Callpoint program for SAR_TERRITORY - Sales Analysis by Territory
rem Program sar_territory_cpt.bbj

rem Barista Version 9.0
rem Copyright (c) 2007-2009 BASIS International Ltd.
rem All Rights Reserved

	seterr std_error
	setesc std_error

	use ::sys/prog/bao_callpoint.bbj::Callpoint
	declare Callpoint Callpoint!

	enter
:		Callpoint!,
:		bar_tpl$,
:		BarObj!,
:		user_tpl$,
:		UserObj!,
:		gui_dev,
:		SysGUI!,
:		Form!,
:		rec_data$,
:		table_chans$[all],
:		dtlg_param$[all],
:		GridVect!,
:		OrigVect!,
:		hrec_data$[all],
:		hattr_col$[all],
:		err=*next

rem --- Directory STBLs

	dir_pgm$=stbl("+DIR_SYP")
	dir_frm$=stbl("+DIR_FRM")
	dir_def$=stbl("+DIR_DEF")
	dir_img$=stbl("+DIR_IMG")
	dir_wrk$=stbl("+DIR_WRK")

rem --- Retrieve sysinfo Data

	sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
	dim sysinfo$:sysinfo_template$
	sysinfo$=stbl("+SYSINFO",err=*next)
	firm_id$=sysinfo.firm_id$

rem --- Callpoints and tables handled by this program

	callpoint_event$=Callpoint!.getCallpointEvent()

	callpoint_event_str$=
:		pad("SAR_TERRITORY.ARAR",40)+
:		pad("SAR_TERRITORY.ASVA",40)+
:		pad("SAR_TERRITORY.BFMC",40)+
:		pad("SAR_TERRITORY.BSHO",40)+
:		pad("SAR_TERRITORY.TWLVE_PER_REPORT.AVAL",40)

	switch fnstr_pos(callpoint_event$,callpoint_event_str$,40)
		case fnstr_pos(pad("SAR_TERRITORY.ARAR",40),callpoint_event_str$,40)			
			num_files=2
			dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
			open_tables$[1]="ARS_PARAMS",open_opts$[1]="OTA"
			open_tables$[2]="SAS_PARAMS",open_opts$[2]="OTA"
			gosub open_tables
			ars_params_chn=num(open_chans$[1]),ars_params_tpl$=open_tpls$[1]
			sas_params_chn=num(open_chans$[2]),sas_params_tpl$=open_tpls$[2]
			dim ars_params$:ars_params_tpl$
			readrecord(ars_params_chn,key=firm_id$+"AR00")ars_params$
			dim sas_params$:sas_params_tpl$
			readrecord(sas_params_chn,key=firm_id$+"SA00")sas_params$
			if sas_params.by_territory$<>"Y"
				msg_id$="INVALID_SA"
				dim msg_tokens$[1]
				msg_tokens$[1]="Territory"
				gosub disp_message
				bbjAPI!=bbjAPI()
				rdFuncSpace!=bbjAPI!.getGroupNamespace()
				rdFuncSpace!.setValue("+build_task","OFF")
				release
			endif
			callpoint!.setColumnData("SAR_TERRITORY.CURRENT_PER",ars_params.current_per$)
			callpoint!.setColumnData("SAR_TERRITORY.CURRENT_YEAR",ars_params.current_year$)
			callpoint!.setColumnData("SAR_TERRITORY.SA_LEVEL",sas_params.terrcode_lev$)
			callpoint!.setStatus("REFRESH")
			dim user_tpl$:"sa_levels:c(3),high_level:c(1)"
			user_tpl.sa_levels$="TPI"
			user_tpl.high_level$=sas_params.terrcode_lev$
		break
		case fnstr_pos(pad("SAR_TERRITORY.ASVA",40),callpoint_event_str$,40)			
			rem --- Check selected level against allowable level
				allow=pos(user_tpl.high_level$=user_tpl.sa_levels$)
				if pos(callpoint!.getColumnData("SAR_TERRITORY.SA_LEVEL")=user_tpl.sa_levels$)>allow or
:				   pos(callpoint!.getColumnData("SAR_TERRITORY.SA_LEVEL")=user_tpl.sa_levels$)=0
					msg_id$="INVALID_SA_LEVEL"
					gosub disp_message
					callpoint!.setStatus("ABORT")
				endif
		break
		case fnstr_pos(pad("SAR_TERRITORY.BFMC",40),callpoint_event_str$,40)			
			rem --- open files
				num_files=2
				dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
				open_tables$[1]="SAM_TERRITRY",open_opts$[1]="OTA"
				open_tables$[2]="SAS_PARAMS",open_opts$[2]="OTA"
				gosub open_tables
				sas01_dev=num(open_chans$[2]),sas01a$=open_tpls$[2]
				dim sas01a$:sas01a$
				read record (sas01_dev,key=firm_id$+"SA00")sas01a$
			
			rem --- create list for available levels
			
				ldat_list$=pad("Territory",20)+"~"+"T;"
				if pos(sas01a.terrcode_lev$="PI") ldat_list$=ldat_list$+pad("Product",20)+"~"+"P;"
				if pos(sas01a.terrcode_lev$="I") ldat_list$=ldat_list$+pad("Item",20)+"~"+"I;"
			
				callpoint!.setTableColumnAttribute("SAR_TERRITORY.SA_LEVEL","LDAT",ldat_list$)
		break
		case fnstr_pos(pad("SAR_TERRITORY.BSHO",40),callpoint_event_str$,40)			
				sas01_dev=fnget_dev("SAS_PARAMS")
				sas01a$=fnget_tpl$("SAS_PARAMS")
				dim sas01a$:sas01a$
				read record (sas01_dev,key=firm_id$+"SA00")sas01a$
				if sas01a.by_territory$<>"Y"
					msg_id$="INVALID_SA"
					dim msg_tokens$[1]
					msg_tokens$[1]="Territory"
					gosub disp_message
					bbjAPI!=bbjAPI()
					rdFuncSpace!=bbjAPI!.getGroupNamespace()
					rdFuncSpace!.setValue("+build_task","OFF")
					release
				endif
		break
		case fnstr_pos(pad("SAR_TERRITORY.TWLVE_PER_REPORT.AVAL",40),callpoint_event_str$,40)			
			x$=callpoint!.getUserInput()
			if x$="N" then
				callpoint!.setColumnData("SAR_TERRITORY.MTD","Y")
				callpoint!.setColumnData("SAR_TERRITORY.YTD","Y")
				callpoint!.setColumnData("SAR_TERRITORY.PRIOR","Y")
				callpoint!.setColumnData("SAR_TERRITORY.SALES_UNITS","")
			else
				callpoint!.setColumnData("SAR_TERRITORY.MTD","N")
				callpoint!.setColumnData("SAR_TERRITORY.YTD","N")
				callpoint!.setColumnData("SAR_TERRITORY.PRIOR","N")
				callpoint!.setColumnData("SAR_TERRITORY.SALES_UNITS","S")
			endif
			callpoint!.setStatus("REFRESH")
			
		break

		case default
		break
	swend

rem --- All done

	goto std_exit

rem [[SAR_TERRITORY.<CUSTOM>]]


open_tables:rem --- Open Tables

	call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:		open_beg,
:		open_end,
:		open_tables$[all],
:		open_opts$[all],
:		open_chans$[all],
:		open_tpls$[all],
:		table_chans$[all],
:		open_batch,
:		open_status$

	if open_status$<>"" then
		msg_id$="ENTRY_OPEN_ERROR"
		dim msg_tokens$[1]
		msg_tokens$[1]=open_status$
		gosub disp_message
		rdFuncSpace!=bbjAPI().getGroupNamespace().setValue("+build_task","OFF")
		release
	endif

	return

disp_message:rem --- Display Message Dialog

	call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

	return

disp_msg:rem --- Display Message Dialog

	call stbl("+DIR_SYP")+"bam_message.bbj",gui_dev,msg_title$,msg_text$,msg_defs$,msg_opt$

	return

disp_meter:rem --- Display Progress Meter

	call dir_pgm$+"bam_prog_bar.bbj",SysGUI!,Form!,MeterWin!,meter_title$,meter_total_recs,meter_proc_recs,meter_data$,meter_action$

	return

rem --- Functions

	def fnstr_pos(str_pos_att0$,str_pos_att1$,str_pos_att1)=int((pos(str_pos_att0$=str_pos_att1$,str_pos_att1)+str_pos_att1-1)/str_pos_att1)
	def fngett_attr$(gett_attr_att0$)=attr_tbl$[fnstr_pos(gett_attr_att0$,attr_def_tbl$[0,0],5)]
	def fngetc_attr$(getc_attr_att0,getc_attr_att0$)=attr_col$[getc_attr_att0,fnstr_pos(getc_attr_att0$,attr_def_col$[0,0],5)]
	def fngetv_attr$(getv_attr_att0$,getv_attr_att1$)=attr_col$[fnstr_pos(getv_attr_att0$,attr_col$[0,1],40),fnstr_pos(getv_attr_att1$,attr_def_col$[0,0],5)]
	def fnget_rec$(get_rec_att0$)=rec_data$[fnstr_pos(get_rec_att0$,rec_data$[0,0],40),0]
	def fnget_rec_orig$(get_rec_att0$)=rec_data$[fnstr_pos(get_rec_att0$,rec_data$[0,0],40),1]
	def fnget_hrec$(hrec_att0$)=hrec_data$[fnstr_pos(hrec_att0$,hrec_data$[0,0],40),0]
	def fndate$(date_att0$)=date(jul(num(date_att0$(1,4)),num(date_att0$(5,2)),num(date_att0$(7,2))):stbl("+DATE_MASK"))
	def fnget_dev(tmp0$)=num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3))
	def fnget_tpl$(tmp0$)=table_chans$[num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3)),0]
	def fnget_path$(tmp0$)=table_chans$[num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3)),1]
	def fnmdy$(tmp0$)=tmp0$(5,4)+tmp0$(1,4)

std_error: rem --- Standard error handler (01Apr2006)

	err_text$=""
	if tcb(5) and pgm(-1)=pgm(-2) then err_text$=pgm(tcb(5),err=*next)
	pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
	call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),str(err:"000"),err_text$,err_act$
	if pos("EXIT"=err_act$) then goto std_error_exit
	if pos("ESCAPE"=err_act$) then seterr 0;setesc 0
	if pos("RETRY"=err_act$) then retry

std_error_exit:

	master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
	sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
	dim sysinfo$:sysinfo_template$
	sysinfo$=stbl("+SYSINFO",err=std_error_release)
	if cvs(sysinfo.user_id$,2)=master_user$ then escape

std_error_release:

	status=999
	if pgm(-1)<>pgm(-2) then exit
	release

std_exit: rem --- Standard called program exit (01Mar2006)

	exit

