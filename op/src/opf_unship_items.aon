rem --- Unshipped Items query filter program
rem --- Program ID: opf_unship_items.aon
rem --- Copyright BASIS International Ltd.  All Rights Reserved.

rem --- Run by Barista as part of the OP_UNSHIP_ITEMS query.
rem --- For each row in the result set, apply various filters to show non-stock item description, qty backordered and total available.

    use ::bbtranslator.bbj::BBTranslator

    setesc std_error
    seterr std_error

    enter inq_mode$,rec_tpl$,calc_tpl$,table_chans$[all],status$

rem inq_mode$      - not used until recently - current use not yet documented
rem rec_tpl$       - Current row database columns
rem calc_tpl$      - Current row calculated columns
rem table_chans$[] - Barista opened channels array
rem status$        - Returned status: "SKIP" = Skip current record

rem --- Initialize data
    status$=""

rem --- Get BBTranslator from Group Namespace
    Translate!=cast(BBTranslator,BBjAPI().getGroupNamespace().getValue(rdTransSpaceKey$,err=*next))
    if Translate!=null()
        Translate!=BBTranslator.getInstance(stbl("+PROPS_NAME"),stbl("+USER_LOCALE"),null(),stbl("+PROPS_PATH"))
        BBjAPI().getGroupNamespace().setValue(rdTransSpaceKey$,Translate!)
    endif

rem --- Open tables
    num_files=4
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    open_tables$[1]="OPC_LINECODE",open_opts$[1]="OTA"
    open_tables$[2]="OPT_INVHDR",open_opts$[2]="OTA@"
    open_tables$[3]="OPT_INVDET",open_opts$[3]="OTA@"
    open_tables$[4]="IVM_ITEMWHSE",open_opts$[4]="OTA"
            
    gosub open_tables        

    opcLineCode_dev=num(open_chans$[1])
    optInvHdr_dev=num(open_chans$[2])
    optInvDet_dev=num(open_chans$[3])
    ivmItemWhse_dev=num(open_chans$[4])
    dim opcLineCode$:open_tpls$[1]
    dim optInvHdr$:open_tpls$[2]
    dim optInvDet$:open_tpls$[3]
    dim ivmItemWhse$:open_tpls$[4]

rem --- Exclude Dropship, Memo and Other detail lines
    readrecord(opcLineCode_dev,key=rec_tpl.firm_id$+rec_tpl.line_code$)opcLineCode$
    if opcLineCode.dropship$="Y" or pos(opcLineCode.line_type$="MO") then
        status$="SKIP"
        goto exit_prog
    endif
    
rem --- Set Qty Backordered when the Order is backordered
    optInvHdr_key$=rec_tpl.firm_id$+rec_tpl.ar_type$+rec_tpl.customer_id$+rec_tpl.order_no$+rec_tpl.ar_inv_no$
    findrecord(optInvHdr_dev,key=optInvHdr_key$)optInvHdr$
    if optInvHdr.backord_flag$="B" then
        rec_tpl.qty_backord$=rec_tpl.qty_shipped$
    endif

rem --- Set Item Description and inventory quantities for non-stock items
    if cvs(rec_tpl.item_id$,2)="" then
        rec_tpl.item_desc$=rec_tpl.order_memo$
        rec_tpl.qty_available=0
        rec_tpl.total_available=0
        rec_tpl.qty_on_hand=0
        rec_tpl.qty_commit=0
        rec_tpl_qty_on_hand=0
        goto exit_prog
    endif

rem --- Get all open backorders for this stocked item
    item_BOs!=BBjAPI().makeVector()
    optInvDet_key$=rec_tpl.firm_id$+rec_tpl.trans_status$
    read(optInvDet_dev,key=optInvDet_key$,knum="STAT_WH_ITEM_ORD",dom=*next)
    while 1
        thisKey$=key(optInvDet_dev,end=*break)
        if pos(optInvDet_key$=thisKey$)<>1 then break
        readrecord(optInvDet_dev)optInvDet$
        if optInvDet.qty_backord<>0 then
            item_BOs!.addItem(thisKey$)
            continue
        endif
        optInvHdr_key$=optInvDet.firm_id$+optInvDet.ar_type$+optInvDet.customer_id$+optInvDet.order_no$+optInvDet.ar_inv_no$
        findrecord(optInvHdr_dev,key=optInvHdr_key$)optInvHdr$
        if optInvHdr.backord_flag$="B" then item_BOs!.addItem(thisKey$)
    wend
    call stbl("+DIR_SYP")+"bac_key_template.bbj","OPT_INVDET","STAT_WH_ITEM_ORD",key_tpl$,table_chans$[all],status$
    dim backorderKey$:key_tpl$

rem --- Set stocked item's warehouse available
    whseOnHand=0
    whseCommitted=0
    ivmItemWhse_key$=rec_tpl.firm_id$+rec_tpl.warehouse_id$+rec_tpl.item_id$
    findrecord(ivmItemWhse_dev,key=ivmItemWhse_key$,knum="PRIMARY",dom=*next)ivmItemWhse$
    whseOnHand=ivmItemWhse.qty_on_hand
    whseCommitted=ivmItemWhse.qty_commit

    rem --- Don't count committed backorders
    committedBOs=0
    backorderIter!=item_BOs!.iterator()
    while backorderIter!.hasNext()
        backorderKey!=backorderIter!.next()
        backorderKey$=backorderKey!
        if backorderKey.warehouse_id$<>rec_tpl.warehouse_id$ or backorderKey$.item_id$<>rec_tpl.item_id$ then continue
        findrecord(optInvDet_dev,key=backorderKey$,dom=*continue)optInvDet$
        if optInvDet.commit_flag$="Y" then committedBOs=committedBOs+num(rec_tpl.qty_backord$)
    wend
    rec_tpl.qty_available=whseOnHand-(whseCommitted-committedBOs)

rem --- Set stocked item's total available for all warehouses
    ttlOnHand=0
    ttlCommitted=0
    ivmItemWhse_key$=rec_tpl.firm_id$+rec_tpl.item_id$
    read(ivmItemWhse_dev,key=ivmItemWhse_key$,knum="AO_ITEM_WH",dom=*next)
    while 1
        thisKey$=key(ivmItemWhse_dev,end=*break)
        if pos(ivmItemWhse_key$=thisKey$)<>1 then break
        readrecord(ivmItemWhse_dev)ivmItemWhse$
        ttlOnHand=ttlOnHand+ivmItemWhse.qty_on_hand
        ttlCommitted=ttlCommitted+ivmItemWhse.qty_commit
    wend

    rem --- Don't count committed backorders
    committedBOs=0
    backorderIter!=item_BOs!.iterator()
    while backorderIter!.hasNext()
        backorderKey!=backorderIter!.next()
        backorderKey$=backorderKey!
        if backorderKey$.item_id$<>rec_tpl.item_id$ then continue
        findrecord(optInvDet_dev,key=backorderKey$,dom=*continue)optInvDet$
        if optInvDet.commit_flag$="Y" then committedBOs=committedBOs+num(rec_tpl.qty_backord$)
    wend
    rec_tpl.total_available=ttlOnHand-(ttlCommitted-committedBOs)
    
rem --- Done
    goto exit_prog

open_tables:rem --- Open Tables

    call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:       open_beg,
:       open_end,
:       open_tables$[all],
:       open_opts$[all],
:       open_chans$[all],
:       open_tpls$[all],
:       table_chans$[all],
:       open_batch,
:       open_status$

    if open_status$<>"" then
        msg_id$="ENTRY_OPEN_ERROR"
        dim msg_tokens$[1]
        msg_tokens$[1]=open_status$
        gosub disp_message
        rdFuncSpace!=bbjAPI().getGroupNamespace().setValue("+build_task","OFF")
        release
    endif

    return
    
disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

    return

rem --- Functions

    def fnget_dev(tmp0$)=num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3))
    def fnget_tpl$(tmp0$)=table_chans$[num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3)),0]

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src
    
exit_prog:rem --- Exit Program

    exit
