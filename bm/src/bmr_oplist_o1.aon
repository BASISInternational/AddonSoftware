rem --- Operations Where Used Listing
rem --- Program bmr_oplist_o1 v8.0.0 07Jan2008 (bmr_db)
rem --- Created by c:\codeport_bbj\adx_codeport.bbj v1.1.5 (01/07/2008 08:58:47)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved

        setesc std_error
        seterr std_error

rem --- Retrieve the program path

        pgmdir$=stbl("+DIR_PGM",err=*next)
        precision num(ivs01a.precision$)

rem --- Retrieve sysinfo data

        sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
        dim sysinfo$:sysinfo_template$
        sysinfo$=stbl("+SYSINFO",err=*next)
        milestone=num(stbl("+MILESTONE",err=*next),err=*next)
        firm_id$=sysinfo.firm_id$

rem --- Init Data
        more=1
        h0=132
        h=0,headings=2,when$=sysinfo.system_date$,clock$=""
        dim d1$(60),headings$[headings]
        headings$[0]=sysinfo.firm_name$
        headings$[1]=sysinfo.task_desc$
        call pgmdir$+"adc_printer.aon",printer_dev,1,"","",status
        if status then goto std_exit
        call stbl("+DIR_SYP")+"bac_key_template.bbj","BMM_BILLOPER","ALT_KEY_01",bmm03a_key_tpl$,rd_table_chans$[all],status$
        dim bmm03a_key$:bmm03a_key_tpl$

rem --- Background

        call pgmdir$+"adc_progress.aon","N","","","","",0,bmm03a_dev,1,meter_num,status

rem --- Initial Read

        extractrecord (bmm03a_dev,key=firm_id$+opfrom$,knum=1,dom=*next)
        
rem --- Read Where Used File

        while more         
            bmm03a_key$=key(bmm03a_dev,knum=1,end=done)
            readrecord (bmm03a_dev)bmm03a$
            
rem --- Breaks

            if bmm03a_key.firm_id$<>firm_id$ then break
            if cvs(bmm03a_key.op_code$,3)="" then continue
            if opthru$<>"" then if bmm03a_key.op_code$>opthru$ then break
            read record (bmm01a_dev,key=bmm03a_key.firm_id$+bmm03a_key.bill_no$,dom=*continue) bmm01a$
            if bmm03a_key.firm_id$+bmm03a_key.op_code$<>t0$ then gosub item_break
            call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
            let ivm01a.item_desc$="*** Not On File ***"
            find record (ivm01a_dev,key=bmm01a.firm_id$+bmm01a.bill_no$,dom=printing) ivm01a$
            ivm01a.item_desc$=fnitem$(ivm01a.item_desc$,desc[1],desc[2],desc[3])+fill(60)

printing: rem --- Print

            if l+1>l9 then gosub sub_heading
            print (printer_dev)
:                       @(26),bmm01a.bill_no$,
:                       @(27+m0),ivm01a.item_desc$(1,30),
:                       @(110-p1*3),fndate$(bmm03a.effect_date$)," ",fndate$(bmm03a.obsolt_date$),bmm03a.hrs_per_pce:p1$,bmm03a.pcs_per_hour:p1$,fnop(bmm03a.hrs_per_pce,bmm03a.pcs_per_hour,bmm01a.est_yield,bmm03a.setup_time,bmm01a.std_lot_size):p1$,
:                       @(128),bmm03a.op_seq$
            t0=t0+1
            l=l+1
        wend

done: rem --- Totals

        t9$="E"
        gosub item_break
        if h=0 then gosub report_heading
        print (printer_dev)'lf',@(10),"Total Records:",t0
        goto std_exit

report_heading: rem --- Report Heading

        l=headings+1
        call pgmdir$+"adc_rpthead.aon",printer_dev,headings$[all],headings,h,h0,when$,clock$,status
        if status then exitto std_exit
        print (printer_dev)
:                   @(0),"Operation",
:                   @(26),"Bill",
:                   @(111-p1*3),"Effect",
:                   @(119-p1*3),"Obsolete",
:                   @(127-(p1*2+6)),"Hours",
:                   @(127-(p1+7)),"Pieces",
:                   @(127-4),"Net"
        print (printer_dev)
:                   @(0),"Code",
:                   @(5),"Description",
:                   @(26),"No.",
:                   @(27+m0),"Description",
:                   @(112-p1*3),"Date",
:                   @(121-p1*3),"Date",
:                   @(127-(p1*2+7)),"/Piece",
:                   @(127-(p1+6)),"/Hour",
:                   @(127-6),"Hours",
:                   @(128),"Seq",'lf'
        l=l+3
        w9$=""
return

sub_heading: rem --- Sub Heading

        if l+1>l9 then gosub report_heading
        print (printer_dev)@(0),bmm08a.op_code$,@(5),h3$,
return

item_break: rem --- Item Break
            
        if t0$<>"" then 
            print (printer_dev)'lf'
            l=l+2
        endif
        if t9$<>"" then return
        t0$=bmm03a_key.firm_id$+bmm03a_key.op_code$
        h3$="***Not On File***"
        find record (bmm08a_dev,key=t0$,dom=*return) bmm08a$
        h3$=bmm08a.code_desc$
        gosub sub_heading
return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$
    
rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)
    def fnop(q1,q2,q3,q4,q5)=100*(q1/q2)/q3+q4/q5

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Format zip/postal code

    def fnzipcode$(q$)=fill(10,cvs(q$(1,3)+fill(pos("9"<q$(1,5))>0)+q$(4,2)+
:                      fill(ABS(pos("A">=q$(1,6))>0)-pos("0">q$(6,4))>0,"-")+
:                      q$(6,4)+fill(10),16))

rem --- Date/time handling functions

    def fndate$(q$)=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2))))
    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem #endinclude std_functions.src

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src
rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)
    
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status

    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

    end
