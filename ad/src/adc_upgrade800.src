rem adc - General Upgrade Pre-Processing Checks and Dialog
rem Program adc_upgrade800 v8.0.0 05Nov2006 (syc70a)
rem Created by adx_codeport.bbx v1.1.2 (11/05/2006 11:33:38)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- This program checks to make sure that all entry files have been cleared
rem --- and that the application is installed. User approval is required before
rem --- proceeding.

rem --- application$  : Calling application ID (Input)
rem --- ads01_dev     : ads-01 channel number (Input)
rem --- adm04_dev     : adm-04 channel number (Input)
rem --- adm04a$       : adm-04 template (Input)
rem --- description$  : Application description (Returned)
rem --- version$      : Application version (Returned)
rem --- status        : 0=No error (Returned)

    seterr std_error
    setesc std_error
    enter application$,ads01_dev,adm04_dev,adm04a$,description$,version$,status

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    title$=sysinfo.task_desc$

rem --- Initializations

    dim descriptions$[12]
    icon=64
    status=0
    version$="8.00"

rem --- Application descriptions

    descriptions$[1]="Accounts Payable"
    descriptions$[2]="Accounts Receivable"
    descriptions$[3]="Bill Of Materials"
    descriptions$[4]="General Ledger"
    descriptions$[5]="Inventory"
    descriptions$[6]="Material Requirements Planning"
    descriptions$[7]="Order Processing"
    descriptions$[8]="Payroll"
    descriptions$[9]="Purchase Orders"
    descriptions$[10]="Sales Analysis"
    descriptions$[11]="Shop Floor Control"
    descriptions$[12]="GENERAL Report Writer"
    applications$="AP AR BM GL IV MP OP PR PO SA SF GE "
    x=pos(application$=applications$,3)
    x=int(x/3)+1
    description$=descriptions$[x]

rem --- Have entry files been cleared?

    find (ads01_dev,key="FOOBAR",dom=installed)
    message$="The system has determined that there are registers and updates which "+
:            "have not been run. This conversion program can not be run until all "+
:            "entry files are empty."
    icon=16
    status=99
    goto display_message

installed: rem --- Is this application installed?

    adm04a.system_id$=application$
    find record (adm04_dev,key=adm04a.system_id$,dom=not_installed) adm04a$
    if adm04a.sys_install$="Y" goto warning

not_installed: rem --- Application is not installed

    message$=description$+" is required to run this task."
    icon=16
    status=99
    goto display_message

warning: rem --- Display Warning

    message$="This task should be run once and ONLY once or data integrity "+
:            "may be lost."

display_message: rem --- Display message and get response

    button=1
    default=0
    modal=262144
    msgmode=button+icon+default+modal
    answer=msgbox(message$,msgmode,title$,err=std_exit)
    if answer=2 status=99

rem --- OK to continue?

    if status goto std_exit
    message$="Do you wish to upgrade "+description$+" to Version "+version$
    call pgmdir$+"adc_yesno.aon",0,message$,0,answer$,fkey
    if answer$<>"YES" status=99
    goto std_exit

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    exit

rem #endinclude std_exit.src

    end
