rem Change Password
rem Proprietary Software. AddonSoftware
rem Program ID: ads_password.aon  <Apr 19, 2006>

	if pgm(-1)<>pgm(-2)
		enter rd_gui_dev,rdSysGUI!,rd_table_chans$[all]
		else
		rd_gui_dev=unt
		open(unt,mode="ANTIALIASED=OFF")"X0"
		rdBBjAPI!=BBjAPI()
		rdSysGUI!=rdBBjAPI!.getSysGui()
		rdSysGUI!.setLookAndFeel("WindowsXPLookAndFeel")
	endif

rem --- Functions

	def fnstr_pos(rd_tmp0$,rd_tmp1$,rd_tmp0)=int((pos(rd_tmp0$=rd_tmp1$,rd_tmp0)+rd_tmp0-1)/rd_tmp0)
	def fnstrip$(rd_tmp0$,rd_tmp1$)=rd_tmp0$(1,pos(fill(50,rd_tmp1$)=rd_tmp0$+fill(50,rd_tmp1$))-1)
	def fngetc_attr$(rd_att0,rd_att0$)=rd_attr_dcol$[rd_att0,fnstr_pos(rd_att0$,rd_attr_def_col$,5)]
	def fngetv_attr$(rd_att0$,rd_att1$)=rd_attr_dcol$[fnstr_pos(rd_att0$,rd_attr_dcol$[0,1],16),fnstr_pos(rd_att1$,rd_attr_def_col$,5)]

rem --- Directory STBLs

	rd_dir_pgm$=stbl("+DIR_PGM")
	rd_dir_img$=stbl("+DIR_IMG")
	rd_dir_sys$=stbl("+DIR_SYS")
	rd_file_usr$=stbl("+FILE_USR")
	rd_file_set$=stbl("+FILE_SET")
	rd_user_id$=stbl("+USER_ID")

file_opens:rem --- Open File(s)

	rd_num_files=1
	dim rd_open_tables$[1:rd_num_files],rd_open_opts$[1:rd_num_files],rd_open_chans$[1:rd_num_files],rd_open_tpls$[1:rd_num_files]

	rd_open_tables$[1]="ADM_USER",rd_open_opts$[1]="OTA"

	gosub open_tables

	rd_adm_user=num(rd_open_chans$[1]);dim rd_adm_user$:rd_open_tpls$[1]

	readrecord(rd_adm_user,key=pad(rd_user_id$,16))rd_adm_user$
	rd_password_user$=rd_adm_user.password$

rem --- Misc Setup

	rdAPI!=BBjAPI()
    rdColsGUI!=rdAPI!.getSysGui()
    rdSysGUI!=rdAPI!.getSysGui()

rem --- Main Process

	gosub disp_win

	rdPwdCurr!.focus()
	rdWindow!.setVisible(1)

event_ctl:rem --- Event Control

	rdWindow!.setCallback(rdWindow!.ON_CLOSE,"exit_prog")
	rdWindow!.setCallback(rdWindow!.ON_RESIZE,"resize_win")

	rdBtnOK!.setCallback(rdBtnOK!.ON_BUTTON_PUSH,"func_accept")
	rdBtnCancel!.setCallback(rdBtnCancel!.ON_BUTTON_PUSH,"exit_prog")

	rdSysGUI!.flushEvents()

	process_events

func_accept:rem --- Update Array With Changes

	rd_password_curr$=rdPwdCurr!.getText()
	rd_password_new$=rdPwdNew!.getText()
	rd_password_conf$=rdPwdConf!.getText()

	if rd_password_curr$<>rd_password_user$
		rd_msg_text$="The current password is incorrect."
		rd_msg_defs$="O;W;O"
		gosub disp_msg
		rdPwdCurr!.focus()
		return
	endif

	if rd_password_new$<>rd_password_conf$
		rd_msg_text$="The new passwords you typed do not match."
		rd_msg_defs$="O;W;O"
		gosub disp_msg
		rdPwdNew!.focus()
		return
	endif

	readrecord(rd_adm_user,key=pad(rd_user_id$,16))rd_adm_user$
		rd_adm_user.password$=rd_password_new$
		rd_adm_user$=field(rd_adm_user$)
	writerecord(rd_adm_user)rd_adm_user$

	goto exit_prog

disp_win:rem --- Display Window

	if pgm(-1)<>pgm(-2) rdBaseWin!=rdSysGUI!.getWindow(rdSysGUI!.getContext())

	dim rd_win_coord[1:4]
	dim rd_ctl_misc$[20]

	rd_form_id$=rd_dir_sys$+"ads_password.arc"

	call rd_dir_pgm$+"rdm_controls.aon",
:		rd_gui_dev,
:		rdSysGUI!,
:		rd_form_id$,
:		rd_win_coord[all],
:		rdWindow!,
:		"DISPLAY",
:		rd_open_chan$[all],
:		rd_ctl_misc$[all]

	rdBtnOK!=rdWindow!.getControl(1)
	rdBtnCancel!=rdWindow!.getControl(2)

	rdPwdCurr!=rdWindow!.getControl(3001)
	rdPwdNew!=rdWindow!.getControl(3002)
	rdPwdConf!=rdWindow!.getControl(3003)

	rd_win_disp$="YES"

	return

open_tables:rem --- Open Tables

	call rd_dir_pgm$+"rdc_open_tables.aon",
:		rd_open_beg,
:		rd_open_end,
:		rd_open_tables$[all],
:		rd_open_opts$[all],
:		rd_open_chans$[all],
:		rd_open_tpls$[all],
:		rd_table_chans$[all],
:		rd_open_batch,
:		rd_open_status$

	if rd_open_status$<>""
:		rd_msg_text$=rd_open_status$,
:		rd_msg_defs$="O  ;F;O";
:		gosub disp_msg;
:		release

	return

disp_msg:rem --- Display Messages

	call rd_dir_pgm$+"rdm_message.aon",
:		rd_gui_dev,
:		rd_msg_dialog_title$,
:		rd_msg_text$,
:		rd_msg_defs$,
:		rd_msg_opt$

	rd_msg_title$=""
	rd_msg_text$=""

	return

error_proc:rem --- Error Processing Routine

	rd_err_text$=""
	if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))

	call stbl("+DIR_PGM")+"adc_error.aon",pgm(-2),str(tcb(5):"00000"),str(err:"000"),rd_err_text$,rd_err_act$

	if pos("EXIT"=rd_err_act$)<>0 goto exit_prog
	if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
	if pos("RETRY"=rd_err_act$)<>0 retry

exit_prog:rem --- Exit Program

	if rd_win_disp$="YES"
		rdWindow!.destroy()
		if pgm(-1)<>pgm(-2)
			rdBaseWin!.focus()
			rdSysGUI!.setContext(rdBaseWin!.getContextID())
		endif
	endif

	if pgm(-1)=pgm(-2) release
	exit
