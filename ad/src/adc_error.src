rem Error Processing
rem Proprietary Software.  AddonSoftware
rem Program ID: adc_error.aon  <Nov 14, 2005>

	seterr release_result

rem --- Enter Arguments

	enter 
:		rd_err_prog$,
:		rd_err_line$,
:		rd_err_num$,
:		rd_err_text$,
:		rd_err_act$

rem --- STBLs

	rd_dir_pgm$=stbl("+DIR_PGM")
	rd_file_set$=stbl("+FILE_SET")

rem --- Functions

	def fnstr_pos(rd_tmp0$,rd_tmp1$,rd_tmp0)=
:		int((pos(rd_tmp0$=rd_tmp1$,rd_tmp0)+rd_tmp0-1)/rd_tmp0)

rem --- Retrieve Config Settings

	rd_attr_desc$=
:		"DEBUG_MODE          "+
:		"MSG_ERC_TEXT        "+
:		"MSG_ERP_TEXT        "

	dim rd_set_data$[len(rd_attr_desc$)/20]
		rd_set_data$[0]=rd_attr_desc$

	call rd_dir_pgm$+"rdm_config.aon",
:		rd_file_set$,
:		"INI",
:		"READ",
:		rd_set_data$[all]

	rd_debug_mode$=rd_set_data$[fnstr_pos("DEBUG_MODE",rd_set_data$[0],20)]
	rd_erc_text$=rd_set_data$[fnstr_pos("MSG_ERC_TEXT",rd_set_data$[0],20)]
	rd_erp_text$=rd_set_data$[fnstr_pos("MSG_ERP_TEXT",rd_set_data$[0],20)]

format_msg:rem --- Format Error Message

	rd_temp_msg$=
:		"This program has performed an illegal operation and will be shut down."+$0a$+
:		"If this problem persists, please contact "+rd_erc_text$+" at"+$0a$+
:		rd_erp_text$+" and supply the following information:"+$0a0a$+
:		"Program: "+rd_err_prog$+$0a$+
:		"Program Line: "+rd_err_line$+$0a$+
:		"Error Number: "+rd_err_num$+" ("+errmes(num(rd_err_num$))+")"

	if rd_err_text$<>""
:		rd_temp_msg$=rd_temp_msg$+$0a$+
:			"Error Text: "+rd_err_text$(1,min(80,len(rd_err_text$)))

	if pos("YES"=rd_debug_mode$+stbl("+DEV_MODE"))<>0
:		rd_temp_msg$=rd_temp_msg$+$0a0a$+
:			"Note: Debug mode is active.  Retry will remove error trapping"+$0a$+
:			"and place the program into console mode."

	wait 0.2
	rd_temp_msg=msgbox(rd_temp_msg$,5+16,"Error Processing")

	on pos(str(rd_temp_msg)="24") goto
:		cancel_result,
:		cancel_result,
:		retry_result

cancel_result:rem --- Route Cancel

	rd_err_act$="EXIT"

	goto exit_prog

retry_result:rem --- Route Retry

	rd_err_act$="RETRY"
	if pos("YES"=rd_debug_mode$+stbl("+DEV_MODE"))<>0
:		rd_err_act$=rd_err_act$+"ESCAPE"

	goto exit_prog

release_result:rem --- Release On Error Processing Error

	release

exit_prog:rem --- Exit Program

	exit
