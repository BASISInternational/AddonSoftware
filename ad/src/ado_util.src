rem --- ado - Resize Window
rem --- Program ado_util.src v8.0.0 03Dec2008
rem --- Copyright 2008, BASIS International, Ltd.
rem 
rem --- This object helps resize Barista windows with customer objects

class public util

	field private static BBjTopLevelWindow Form!
	field private static BBjSysGui SysGUI!
	field public static BBjInt ExtraWidth% = 10
	field public static BBjInt ExtraHeight% = 10
	field public static BBjInt LessWidth% = 40
	field public static BBjInt LessHeight% = 40
	
	method public static void resizeWindow(BBjTopLevelWindow inForm!, BBjSysGui inSysgui!)
	
		#Form! = inForm!
		#SysGUI! = inSysgui!
		controls! = #Form!.getAllControls()
		ScreenSize! = #SysGUI!.getSystemMetrics().getScreenSize()
		screen_width = ScreenSize!.width - #LessWidth%
		screen_height = ScreenSize!.height - #LessHeight%
		group_box = 21
		push_button = 11
		new_width = #Form!.getWidth()
		new_height = #Form!.getHeight()
		no_buttons_yet = 1

		rem --- Roll throught all controls, setting the max width and height
		for i=0 to controls!.size() - 1
			this_ctrl! = controls!.getItem(i)
			type = this_ctrl!.getControlType()

			rem --- Group boxes dimesions can mess up the calculation
			if type = group_box then continue
			
			rem --- Push Buttons (e.g. "OK", "Cancel") need extra room at the bottom
			if type = push_button then
				if no_buttons_yet then
					ExtraHeight = ExtraHeight + this_ctrl!.getHeight() + 5
					no_buttons_yet = 0
				endif
			else
			
				rem --- Most controls go here
				new_width  = max( new_width,  this_ctrl!.getX() + this_ctrl!.getWidth() )
				new_height = max( new_height, this_ctrl!.getY() + this_ctrl!.getHeight() )
			endif
			
		next i

		rem --- Set new size, but not bigger than the screen
		new_width = min( screen_width, new_width + #ExtraWidth% )
		new_height = min( screen_height, new_height + #ExtraHeight% )
		#Form!.setSize(new_width, new_height)
		
		rem --- Will the form still fit on the screen?
		new_position = 0
		form_x = #Form!.getX()
		form_y = #Form!.getY()
		
		if form_x + new_width > screen_width then
			form_x = int( (screen_width - new_width) / 2 )
			new_position = 1
		endif
		
		if form_y + new_height > screen_height then
			form_y = int( (screen_height - new_height) / 2 )
			new_position = 1
		endif
		
		if new_position then
			#Form!.setLocation(form_x, form_y)
		endif
	
	methodend

classend

end
