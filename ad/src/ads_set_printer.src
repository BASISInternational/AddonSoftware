rem Processing Settings - Printer
rem Proprietary Software. AddonSoftware
rem Program ID: ads_set_printer.aon  <Apr 18, 2006>

	if pgm(-1)<>pgm(-2)
		enter rd_gui_dev,rdSysGUI!,rd_proc_prt$,rd_table_chans$[all]
		else
		rd_gui_dev=unt
		open(unt,mode="ANTIALIASED=OFF")"X0"
		rdBBjAPI!=BBjAPI()
		rdSysGUI!=rdBBjAPI!.getSysGui()
		rdSysGUI!.setLookAndFeel("WindowsXPLookAndFeel")
	endif

rem --- Functions

	def fnstr_pos(rd_tmp0$,rd_tmp1$,rd_tmp0)=int((pos(rd_tmp0$=rd_tmp1$,rd_tmp0)+rd_tmp0-1)/rd_tmp0)
	def fnstrip$(rd_tmp0$,rd_tmp1$)=rd_tmp0$(1,pos(fill(50,rd_tmp1$)=rd_tmp0$+fill(50,rd_tmp1$))-1)
	def fngetc_attr$(rd_att0,rd_att0$)=rd_attr_dcol$[rd_att0,fnstr_pos(rd_att0$,rd_attr_def_col$,5)]
	def fngetv_attr$(rd_att0$,rd_att1$)=rd_attr_dcol$[fnstr_pos(rd_att0$,rd_attr_dcol$[0,1],16),fnstr_pos(rd_att1$,rd_attr_def_col$,5)]

rem --- Directory STBLs

	rd_dir_pgm$=stbl("+DIR_PGM")
	rd_dir_img$=stbl("+DIR_IMG")
	rd_dir_sys$=stbl("+DIR_SYS")
	rd_file_usr$=stbl("+FILE_USR")
	rd_file_set$=stbl("+FILE_SET")

rem --- Misc Setup

	rdAPI!=BBjAPI()
    rdColsGUI!=rdAPI!.getSysGui()
    rdSysGUI!=rdAPI!.getSysGui()
	rdPrtsVect!=rdSysGUI!.makeVector()

	call rd_dir_pgm$+"rdm_attr_init.aon",rd_attr_def_tbl$[all],rd_attr_def_col$[all]
		rd_attr_def_tbl$=rd_attr_def_tbl$[0,0]
		rd_attr_def_col$=rd_attr_def_col$[0,0]

	dim rd_alias$:"type:u(1),opens:u(1),alias:c(2*=0),device:c(10*=0),phys:c(10*=0),modes:c(80*=0)"

rem --- Main Process

	gosub get_user_settings
	gosub win_coords
	gosub disp_win

	gosub get_prts_data
	gosub def_grid_prts
	rdPrtsGrid!.setCellText(0,0,rdPrtsVect!)

	if rd_select_row<>-1
		rdPrtsGrid!.setSelectedCell(rd_select_row,1)
		else
		rdPrtsGrid!.setSelectedRow(0)
	endif

	rdPrtsGrid!.focus()
	rdWindow!.setVisible(1)

event_ctl:rem --- Event Control

	rdWindow!.setCallback(rdWindow!.ON_CLOSE,"exit_prog")
	rdWindow!.setCallback(rdWindow!.ON_RESIZE,"resize_win")

	rdBtnOK!.setCallback(rdBtnOK!.ON_BUTTON_PUSH,"func_accept")
	rdBtnCancel!.setCallback(rdBtnCancel!.ON_BUTTON_PUSH,"exit_prog")

	rdSysGUI!.flushEvents()

	process_events

get_prts_data:rem --- Get Printer Data

	rd_alias$=tsk(rd_curr_task,err=*return)
	if rd_alias.type$=$01$
		rdPrtsVect!.addItem(rd_alias.alias$)
		rdPrtsVect!.addItem(rd_alias.device$)
	endif

	rd_curr_task=rd_curr_task+1

	goto get_prts_data

get_user_settings:rem --- Get User Settings

	rd_save_win_desc$=pad("WIN_[SET_DPRT]",20)
	rd_attr_desc$=rd_save_win_desc$

	dim rd_set_data$[len(rd_attr_desc$)/20]
		rd_set_data$[0]=rd_attr_desc$

	call rd_dir_pgm$+"rdm_config.aon",rd_file_usr$,"USR","READ",rd_set_data$[all]

	rd_win_coord$=rd_set_data$[fnstr_pos(rd_save_win_desc$,rd_set_data$[0],20)]

	return

win_coords:rem --- Set Window & Column Coordinates

	if rd_win_coord$="" rd_win_coord$="0,0,0,0"

	dim rd_win_coord[1:4]
		for rd_curr_coord=1 to 4
			rd_temp_spos=pos(","=","+rd_win_coord$+",",1,rd_curr_coord)
			rd_temp_epos=pos(","=","+rd_win_coord$+",",1,rd_curr_coord+1)-1
			rd_win_coord[rd_curr_coord]=num(rd_win_coord$(rd_temp_spos,rd_temp_epos-rd_temp_spos))
		next rd_curr_coord
	
	return

save_win_coords:rem --- Save Windows Coordinates

	if rdWindow!.getWidth()=0 return

	rd_curr_win_coords$=
:		str(rdWindow!.getX())+","+
:		str(rdWindow!.getY())+","+
:		str(rdWindow!.getWidth())+","+
:		str(rdWindow!.getHeight())

	rd_attr_desc$=rd_save_win_desc$

	dim rd_usr_data$[len(rd_attr_desc$)/20]
		rd_usr_data$[0]=rd_attr_desc$

	rd_usr_data$[fnstr_pos(rd_save_win_desc$,rd_usr_data$[0],20)]=rd_curr_win_coords$

	call rd_dir_pgm$+"rdm_config.aon",rd_file_usr$,"USR","WRITE",rd_usr_data$[all]

	return

disp_win:rem --- Display Window

	if pgm(-1)<>pgm(-2) rdBaseWin!=rdSysGUI!.getWindow(rdSysGUI!.getContext())
	dim rd_ctl_misc$[20]

	rd_form_id$=rd_dir_sys$+"ads_set_printer.arc"

	call rd_dir_pgm$+"rdm_controls.aon",
:		rd_gui_dev,
:		rdSysGUI!,
:		rd_form_id$,
:		rd_win_coord[all],
:		rdWindow!,
:		"DISPLAY",
:		rd_open_chan$[all],
:		rd_ctl_misc$[all]

	rdBtnOK!=rdWindow!.getControl(1)
	rdBtnCancel!=rdWindow!.getControl(2)

	rdPrtsGrid!=rdWindow!.getControl(3001)

	rd_win_disp$="YES"

	return

def_grid_prts:rem --- Define Grid - Printers

	rd_def_cols=2
	dim rd_attr_col$[rd_def_cols,len(rd_attr_def_col$)/5]
		rd_attr_col$[1,fnstr_pos("DVAR",rd_attr_def_col$,5)]="PRT_ID"
		rd_attr_col$[1,fnstr_pos("LABS",rd_attr_def_col$,5)]="ID"
		rd_attr_col$[1,fnstr_pos("CTLW",rd_attr_def_col$,5)]="30"

		rd_attr_col$[2,fnstr_pos("DVAR",rd_attr_def_col$,5)]="PRT_DESC"
		rd_attr_col$[2,fnstr_pos("LABS",rd_attr_def_col$,5)]="Name"
		rd_attr_col$[2,fnstr_pos("CTLW",rd_attr_def_col$,5)]="230"

		for rd_curr_attr=1 to rd_def_cols
			rd_attr_col$[0,1]=rd_attr_col$[0,1]+pad(rd_attr_col$[rd_curr_attr,fnstr_pos("DVAR",rd_attr_def_col$,5)],16)
		next rd_curr_attr
		rd_attr_disp_col$=rd_attr_col$[0,1]

	call rd_dir_pgm$+"rdm_grid_init.aon",rd_gui_dev,rdPrtsGrid!,"COLH-LINES-LIGHT-AUTO",rdPrtsVect!.size()/2,rd_attr_def_col$[all],rd_attr_disp_col$,rd_attr_col$[all]
	rdPrtsGrid!.setFitToGrid(rdPrtsGrid!.AUTO_RESIZE_LAST_COLUMN)

	return

resize_win:rem --- Resize Window

	rdBtnOk!.setLocation(rdWindow!.getWidth()-150,rdWindow!.getHeight()-30)
	rdBtnCancel!.setLocation(rdWindow!.getWidth()-75,rdWindow!.getHeight()-30)
	rdDispGrid!.setSize(rdWindow!.getWidth()-rdDispGrid!.getX()*2,rdWindow!.getHeight()-40)

	return

disp_msg:rem --- Display Messages

	call rd_dir_pgm$+"rdm_message.aon",
:		rd_gui_dev,
:		rd_msg_dialog_title$,
:		rd_msg_text$,
:		rd_msg_defs$,
:		rd_msg_opt$

	rd_msg_title$=""
	rd_msg_text$=""

	return

error_proc:rem --- Error Processing Routine

	rd_err_text$=""
	if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))

	call stbl("+DIR_PGM")+"adc_error.aon",pgm(-2),str(tcb(5):"00000"),str(err:"000"),rd_err_text$,rd_err_act$

	if pos("EXIT"=rd_err_act$)<>0 goto exit_prog
	if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
	if pos("RETRY"=rd_err_act$)<>0 retry

exit_prog:rem --- Exit Program

	if rd_win_disp$="YES"
		gosub save_win_coords
		rdWindow!.destroy()
		if pgm(-1)<>pgm(-2)
			rdBaseWin!.focus()
			rdSysGUI!.setContext(rdBaseWin!.getContextID())
		endif
	endif

	exit
