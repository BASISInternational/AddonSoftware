rem ==========================================================================
rem --- Utility to convery a BBj array to an object and back
rem --- Program: adc_array.aon
rem --- Copyright 2009, BASIS International, Ltd.
rem ==========================================================================

rem ==========================================================================
str_array2object: rem --- Turn a string array into an object
rem ==========================================================================

    seterr std_error
    setesc std_error

    enter in$[all], out!, status
    
    dim d$:"dims:i(1),d0elem:i(4),d0base:i(4),d1elem:i(4),d1base:i(4),d2elem:i(4),d2base:i(4)"
    d$ = dims(in$[all])
    
    declare BBjTemplatedString d!
    d! = BBjAPI().makeTemplatedString(fattr(d$))
    d!.setString(d$)
    
    declare Obj4Array out!
    declare BBjVector a!
    
    out! = new Obj4Array(d!)
    a! = BBjAPI().makeVector()
    
    for i=d.d0base% to d.d0elem% + d.d0base% - 1
        if d.dims% >=2 then
            for j=d.d1base% to d.d1elem% + d.d1base% - 1
                if d.dims% = 3 then
                    for k=d.d2base% to d.d2elem% + d.d2base% - 1
                        a!.insertItem(i*d.d1elem%*d.d2elem% + j*d.d2elem% + k, in$[i,j,k])
                    next k
                else
                    a!.insertItem(i*d.d1elem% + j, in$[i,j])
                endif
            next j
        else
            a!.insertItem(i, in$[i])
        endif
    next i
    
    out!.setData(a!)
    
    exit
    
rem ==========================================================================
str_object2array: rem --- Turn an object into a string array
rem ==========================================================================

    seterr std_error
    setesc std_error

    enter in!, out$[all], status
    
    declare Obj4Array in!
    rem declare BBjVector a!
    a! = in!.getData()
    
    switch in!.getDimensions()
        case 1
            dim out$[in!.getD0_base():in!.getD0_elements()-1]
            break
        case 2
            dim out$[in!.getD0_base():in!.getD0_elements()-1, in!.getD1_base():in!.getD1_elements()-1]
            break
        case 3
            dim out$[in!.getD0_base():in!.getD0_elements()-1, in!.getD1_base():in!.getD1_elements()-1, in!.getD2_base():in!.getD2_elements()-1]
            break
        case default
    swend
    
    for i = in!.getD0_base() to in!.getD0_elements() + in!.getD0_base() - 1
        if in!.getDimensions() >=2 then
            for j = in!.getD1_base() to in!.getD1_elements() + in!.getD1_base() - 1
                if in!.getDimensions() = 3 then
                    for k = in!.getD2_base() to in!.getD2_elements() + in!.getD2_base() - 1
                        out$[i,j,k] = str(a!.getItem(i*in!.getD1_elements()*in!.getD2_elements() + j*in!.getD2_elements() + k))
                    next k
                else
                    out$[i,j] = str(a!.getItem(i*in!.getD1_elements() + j))
                endif
            next j
        else
            out$[i] = str(a!.getItem(i))
        endif
    next i
    
    exit

rem ==========================================================================
num_array2object: rem --- Turn a numeric array into an object
rem ==========================================================================

    seterr std_error
    setesc std_error

    enter in[all], out!, status
    
    dim d$:"dims:i(1),d0elem:i(4),d0base:i(4),d1elem:i(4),d1base:i(4),d2elem:i(4),d2base:i(4)"
    d$ = dims(in$[all])
    
    rem declare BBjTemplatedString d!
    d! = BBjAPI().makeTemplatedString(fattr(d$))
    d!.setString(d$)
    
    rem declare Obj4Array out!
    rem declare BBjVector a!
    
    out! = new Obj4Array(d!)
    a! = BBjAPI().makeVector()
    
    for i=d.d0base% to d.d0elem% + d.d0base% - 1
        if d.dims% >=2 then
            for j=d.d1base% to d.d1elem% + d.d1base% - 1
                if d.dims% = 3 then
                    for k=d.d2base% to d.d2elem% + d.d2base% - 1
                        a!.insertItem( i*d.d1elem% + j*d.d2elem% + k, str(in[i,j,k]) )
                    next k
                else
                    a!.insertItem( i*d.d1elem% + j, str(in[i,j]) )
                endif
            next j
        else
            a!.insertItem( i, str(in[i]) )
        endif
    next i
    
    out!.setData(a!)
    
    exit
    
rem ==========================================================================
num_object2array: rem --- Turn an object into a numeric array
rem ==========================================================================

    seterr std_error
    setesc std_error

    enter in!, out[all], status
    
    rem declare Obj4Array in!
    rem declare BBjVector a!
    a! = in!.getData()
    
    switch in!.getDimensions()
        case 1
            dim out[in!.getD0_base():in!.getD0_elements()-1]
            break
        case 2
            dim out[in!.getD0_base():in!.getD0_elements()-1, in!.getD1_base():in!.getD1_elements()-1]
            break
        case 3
            dim out[in!.getD0_base():in!.getD0_elements()-1, in!.getD1_base():in!.getD1_elements()-1, in!.getD2_base():in!.getD2_elements()-1]
            break
        case default
    swend
    
    for i = in!.getD0_base() to in!.getD0_elements() + in!.getD0_base() - 1
        if in!.getDimensions() >=2 then
            for j = in!.getD1_base() to in!.getD1_elements() + in!.getD1_base() - 1
                if in!.getDimensions() = 3 then
                    for k = in!.getD2_base() to in!.getD2_elements() + in!.getD2_base() - 1
                        out[i,j,k] = num(a!.getItem(i*in!.getD1_elements() + j*in!.getD2_elements() + k))
                    next k
                else
                    out[i,j] = num(a!.getItem(i*in!.getD1_elements() + j))
                endif
            next j
        else
            out[i] = num(a!.getItem(i))
        endif
    next i
    
    exit
    
    
rem ==========================================================================
rem --- Class for array object
rem ==========================================================================

    class public Obj4Array
    
        field public BBjInt Dimensions%
        field public BBjInt D0_elements%
        field public BBjInt D0_base%
        field public BBjInt D1_elements%
        field public BBjInt D1_base%
        field public BBjInt D2_elements%
        field public BBjInt D2_base%
        field public BBjVector Data!
        
        method public Obj4Array(BBjTemplatedString d!)
        
            #Dimensions%  = int(d!.getFieldAsNumber("dims"))
            #D0_elements% = int(d!.getFieldAsNumber("d0elem"))
            #D0_base%     = int(d!.getFieldAsNumber("d0base"))
            #D1_elements% = int(d!.getFieldAsNumber("d1elem"))
            #D1_base%     = int(d!.getFieldAsNumber("d1base"))
            #D2_elements% = int(d!.getFieldAsNumber("d2elem"))
            #D2_base%     = int(d!.getFieldAsNumber("d2base"))
            
            #Data! = BBjAPI().makeVector()
            
        methodend
    
    classend

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry

std_error_exit:
    
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
    
std_error_release:

    status=999
    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    exit

rem #endinclude std_exit.src

end
