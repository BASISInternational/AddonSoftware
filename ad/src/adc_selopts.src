rem Saved Selection Options
rem Proprietary Software. AddonSoftware
rem Program ID: ads_selopts.aon  <May 24, 2006>

	if stbl("+USE_SETERR")="YES" seterr error_proc

	enter rd_gui_dev,rd_alias_id$,rd_selopts_id$,rd_table_chans$[all]

rem --- Functions

	def fnstr_pos(rd_tmp0$,rd_tmp1$,rd_tmp0)=int((pos(rd_tmp0$=rd_tmp1$,rd_tmp0)+rd_tmp0-1)/rd_tmp0)
	def fnstrip$(rd_tmp0$,rd_tmp1$)=rd_tmp0$(1,pos(fill(50,rd_tmp1$)=rd_tmp0$+fill(50,rd_tmp1$))-1)

misc_vars:rem --- Misc Variables

	rd_dir_pgm$=stbl("+DIR_PGM")
	rd_dir_img$=stbl("+DIR_IMG")
	rd_dir_sys$=stbl("+DIR_SYS")
	rd_gui_dev$=stbl("+GUI_DEVICE")
	rd_file_usr$=stbl("+FILE_USR"),rd_file_usr$=stbl("+DIR_USR",err=*next)+"aon_"+cvs(stbl("+USER_ID",err=*next),138)+".usr"
	rd_file_set$=stbl("+FILE_SET")

	rd_save_win_desc$=pad("[SELOPTS]",20)
	rd_user_id$=stbl("+USER_ID")

	rd_status_ind$="*"
	rd_status_ind$=chr(num(stbl("+STATUS_IND_CHAR"),err=*next))

rem --- Get Attributes

	call rd_dir_pgm$+"rdm_attr_init.aon",rd_attr_def_tbl$[all],rd_attr_def_col$[all]
		rd_attr_def_col$=rd_attr_def_col$[0,0]
		rd_attr_def_tbl$=rd_attr_def_tbl$[0,0]

rem --- Open Tables

	rd_num_files=2
	dim rd_open_tables$[1:rd_num_files],rd_open_opts$[1:rd_num_files],rd_open_chans$[1:rd_num_files],rd_open_tpls$[1:rd_num_files]

	rd_open_tables$[1]=rd_gui_dev$,rd_open_opts$[1]="O"
	rd_open_tables$[2]="ADS_SELOPTS",rd_open_opts$[2]="OTA"

	gosub open_tables

	rd_gui_dev=num(rd_open_chans$[1])
	rd_ads_selopts=num(rd_open_chans$[2]);dim rd_ads_selopts$:rd_open_tpls$[2]

	bbjAPI!=bbjAPI()
	rdSysGUI!=bbjAPI!.getSysGui()
	rdOptsVect!=rdSysGUI!.makeVector()

prog_setup:rem --- Program Setup

	gosub disp_win
	gosub def_grids
	gosub get_user_settings
	gosub resize_win
	rdWindow!.setVisible(1)
	gosub get_opts

	rdOptsGrid!.focus()
	if rdOptsVect!.size()<>0 rdOptsGrid!.setSelectedRow(0)

event_ctl:rem --- Event Control

	rdWindow!.setCallback(rdWindow!.ON_CLOSE,"exit_prog")
	rdWindow!.setCallback(rdWindow!.ON_RESIZE,"resize_win")

	rdOptsGrid!.setCallback(rdOptsGrid!.ON_GRID_ENTER_KEY,"func_select")
	rdOptsGrid!.setCallback(rdOptsGrid!.ON_GRID_DOUBLE_CLICK,"func_select")

	rdBtnSelect!.setCallback(rdBtnSelect!.ON_BUTTON_PUSH,"func_select")
	rdBtnExit!.setCallback(rdBtnExit!.ON_BUTTON_PUSH,"exit_prog")
	rdBtnEdit!.setCallback(rdBtnEdit!.ON_BUTTON_PUSH,"func_edit")
	rdBtnDelete!.setCallback(rdBtnDelete!.ON_BUTTON_PUSH,"func_delete")

	process_events,err=*same

func_select:rem --- Return Selected Entry

	rd_selopts_id$=rdOptsGrid!.getCellText(rdOptsGrid!.getSelectedRow(),0)

	goto exit_prog

func_edit:rem --- Edit Selected Entry

	rd_selopts_id$=rdOptsGrid!.getCellText(rdOptsGrid!.getSelectedRow(),0)
	call rd_dir_pgm$+"rdm_run_prog.aon","ADS_SELOPTS",rd_user_id$,"MNT",pad(rd_alias_id$,16)+pad(rd_selopts_id$,16),rd_table_chans$[all]
	gosub get_opts
	rd_selopts_id$=""

	rdOptsGrid!.focus()

	return

func_delete:rem --- Return Selected Entry

	rd_selopts_id$=rdOptsGrid!.getCellText(rdOptsGrid!.getSelectedRow(),0)
	remove(rd_ads_selopts,key=pad(rd_alias_id$,16)+pad(rd_selopts_id$,16),dom=*next)
	gosub get_opts
	rd_selopts_id$=""

	rdOptsGrid!.focus()

	return

get_opts:rem --- Get & Display Associated Tables

	rdOptsGrid!.clearGrid()
	rdOptsVect!.clear()

	read(rd_ads_selopts,key=pad(rd_alias_id$,16),dom=*next)

next_opt:rem --- Read Next Table Record

	readrecord(rd_ads_selopts,end=disp_opts_grid)rd_ads_selopts$

	if rd_ads_selopts.dd_table_alias$=pad(rd_alias_id$,16)
		if rd_ads_selopts.private$="N" or cvs(rd_ads_selopts.user_id$,3)=stbl("+USER_ID")
			rdOptsVect!.addItem(cvs(rd_ads_selopts.selection_id$,2))
			rdOptsVect!.addItem(cvs(rd_ads_selopts.selection_desc$,2))
			rdOptsVect!.addItem(cvs(rd_ads_selopts.user_id$,2))
			rd_private$=""
			if rd_ads_selopts.private$="Y" rd_private$=rd_status_ind$
			rdOptsVect!.addItem(rd_private$)
		endif
		goto next_opt
	endif

disp_opts_grid:rem --- Update opts Grid

	rdOptsGrid!.setNumRows(rdOptsVect!.size()/rd_def_cols)
	rdOptsGrid!.setCellText(0,0,rdOptsVect!)
	rdOptsGrid!.sortByColumn(0,rdOptsGrid!.SORT_ASCENDING)
	rdWindow!.setCursor(0)

	return

open_tables:rem -----<Open Tables

	call rd_dir_pgm$+"rdc_open_tables.aon",
:		rd_open_beg,
:		rd_open_end,
:		rd_open_tables$[all],
:		rd_open_opts$[all],
:		rd_open_chans$[all],
:		rd_open_tpls$[all],
:		rd_table_chans$[all],
:		rd_open_batch,
:		rd_open_status$

	return

disp_msg:rem --- Display Message Dialog

	rd_msg_dialog_title$="AddonSoftware"
	
	call rd_dir_pgm$+"rdm_message.aon",
:		rd_gui_dev,
:		rd_msg_dialog_title$,
:		rd_msg_text$,
:		rd_msg_defs$,
:		rd_msg_opt$

	return

get_user_settings:rem --- Get User/Windows Settings

	call rd_dir_pgm$+"rdc_winsize.aon",rd_save_win_desc$,"W","",rdWindow!,"READ",rd_win_coord[all]

	return

save_user_settings:rem --- Save Windows Coordinates

	call rd_dir_pgm$+"rdc_winsize.aon",rd_save_win_desc$,"W","",rdWindow!,"SAVE",rd_win_coord[all]

	return

disp_win:rem --- Call Window Display Public
	
	dim rd_ctl_misc$[20]

	rd_form_id$=rd_dir_sys$+"adc_selopts.arc"

	call rd_dir_pgm$+"rdm_controls.aon",
:		rd_gui_dev,
:		rdSysGUI!,
:		rd_form_id$,
:		rd_win_coord[all],
:		rdWindow!,
:		"DISPLAY",
:		rd_open_chan$[all],
:		rd_ctl_misc$[all]

	rdOptsGrid!=rdWindow!.getControl(5000)
	rdBtnSelect!=rdWindow!.getControl(1)
	rdBtnExit!=rdWindow!.getControl(2)
	rdBtnEdit!=rdWindow!.getControl(3)
	rdBtnDelete!=rdWindow!.getControl(4)

	rd_win_disp$="YES"

	return

def_grids:rem --- Define Grid

	rd_def_cols=4
	dim rd_attr_col$[rd_def_cols,len(rd_attr_def_col$[0,0])/5]
		rd_attr_col$[1,fnstr_pos("DVAR",rd_attr_def_col$[0,0],5)]="SEL_ID"
		rd_attr_col$[1,fnstr_pos("LABS",rd_attr_def_col$[0,0],5)]="Selection ID"
		rd_attr_col$[1,fnstr_pos("CTLW",rd_attr_def_col$[0,0],5)]="140"

		rd_attr_col$[2,fnstr_pos("DVAR",rd_attr_def_col$[0,0],5)]="SEL_DESC"
		rd_attr_col$[2,fnstr_pos("LABS",rd_attr_def_col$[0,0],5)]="Description"
		rd_attr_col$[2,fnstr_pos("CTLW",rd_attr_def_col$[0,0],5)]="250"

		rd_attr_col$[3,fnstr_pos("DVAR",rd_attr_def_col$[0,0],5)]="SEL_USER"
		rd_attr_col$[3,fnstr_pos("LABS",rd_attr_def_col$[0,0],5)]="User ID"
		rd_attr_col$[3,fnstr_pos("CTLW",rd_attr_def_col$[0,0],5)]="100"

		rd_attr_col$[4,fnstr_pos("DVAR",rd_attr_def_col$[0,0],5)]="SEL_PRIV"
		rd_attr_col$[4,fnstr_pos("LABS",rd_attr_def_col$[0,0],5)]="Pri?"
		rd_attr_col$[4,fnstr_pos("MAXL",rd_attr_def_col$[0,0],5)]="1"
		rd_attr_col$[4,fnstr_pos("CTLW",rd_attr_def_col$[0,0],5)]="50"

		for rd_curr_attr=1 to rd_def_cols
			rd_attr_col$[0,1]=rd_attr_col$[0,1]+pad("SELOPTS."+rd_attr_col$[rd_curr_attr,fnstr_pos("DVAR",rd_attr_def_col$[0,0],5)],40)
		next rd_curr_attr
		rd_attr_disp_col$=rd_attr_col$[0,1]

	call rd_dir_pgm$+"rdm_grid_init.aon",rd_gui_dev,rdOptsGrid!,"COLH-AUTO-LINES-LIGHT",rd_num_rows,rd_attr_def_col$[all],rd_attr_disp_col$,rd_attr_col$[all]

	return

resize_win:rem --- Resize Window Event

	rdOptsGrid!.setSize(rdWindow!.getWidth()-85,rdWindow!.getHeight()-(rdOptsGrid!.getY()*2))
	rdBtnEdit!.setLocation(rdWindow!.getWidth()-75,5)
	rdBtnDelete!.setLocation(rdWindow!.getWidth()-75,30)
	rdBtnSelect!.setLocation(rdWindow!.getWidth()-75,rdWindow!.getHeight()-50)
	rdBtnExit!.setLocation(rdWindow!.getWidth()-75,rdWindow!.getHeight()-25)
	rdOptsGrid!.setFitToGrid(rdOptsGrid!.AUTO_RESIZE_LAST_COLUMN)

	return

error_proc:rem --- Error Processing Routine

	rd_err_text$=""
	if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))

	call stbl("+DIR_PGM")+"adc_error.aon",pgm(-2),str(tcb(5):"00000"),str(err:"000"),rd_err_text$,rd_err_act$

	if pos("EXIT"=rd_err_act$)<>0 goto exit_prog
	if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
	if pos("RETRY"=rd_err_act$)<>0 retry

exit_prog:rem --- Exit Program

	if rd_win_disp$="YES"
		gosub save_user_settings
		rdWindow!.destroy()
	endif

	exit
