rem ==========================================================================
rem --- A class to help with creating orders and invoices in O/P
rem --- Program ado_order.src v8.0.0 30Jul2009
rem --- Copyright 2008-2009, BASIS International, Ltd.
rem ==========================================================================

    use ::sys/prog/bao_callpoint.bbj::Callpoint
    use ::ado_util.src::util
    use ::adc_array.aon::ArrayObject
    
    use java.util.HashMap

    class public OrderHelper
    
        field private BBjString className$ = "OrderHelper"
        field private BBjString firm_id$
        field private BBjInt precision% = 2
        field private Callpoint callpoint!
        field private BBjString dtl_tmpl$
        
        field private BBjInt lineCodeDev% = 0
        field private BBjString lineCodeTmpl$ = ""
        field private BBjInt ordDetailDev% = 0
        field private BBjString ordDetailTmpl$ = ""
        field private BBjInt itemMastDev% = 0
        field private BBjString itemMastTmpl$ = ""
        field private BBjInt taxcodeDev% = 0
        field private BBjString taxcodeTmpl$ = ""
        field private BBjString taxcodeData$ = ""
        
        field public BBjVector DtlVect!
        field public BBjString Cust_id$
        field public BBjString Order_no$
        field public BBjString Inv_type$
        field public BBjString TaxCode$ = ""
        
        field private BBjNumber extPrice
        field private BBjNumber taxable
        field private BBjNumber extCost
        
        field public BBjNumber Freight
        field public BBjNumber Discount
        field public BBjNumber TaxAmount
        
        field private BBjNumber START_BLOCK = 1
    
    rem --- New object
    
        method public OrderHelper(BBjString p_firm_id$, ArrayObject pTableChans!, BBjInt p_precision%, Callpoint pCallpoint!, BBjString p_dtl_tmpl$)
            #setOrderHelper(p_firm_id$, pTableChans!, p_precision%, pCallpoint!, p_dtl_tmpl$)
        methodend
        
        method public OrderHelper(BBjString p_firm_id$, BBjInt p_precision%, Callpoint pCallpoint!, BBjString p_dtl_tmpl$)
            #setOrderHelper(p_firm_id$, null(), p_precision%, pCallpoint!, p_dtl_tmpl$)
        methodend
        
        method public OrderHelper(BBjString p_firm_id$, BBjInt p_precision%, Callpoint pCallpoint!)
            #setOrderHelper(p_firm_id$, null(), p_precision%, pCallpoint!, "")
        methodend
        
        method public OrderHelper(BBjString p_firm_id$, Callpoint pCallpoint!)
            #setOrderHelper(p_firm_id$, null(), 0, pCallpoint!, "")
        methodend
        
        method private void setOrderHelper(BBjString p_firm_id$, ArrayObject pTableChans!, BBjInt p_precision%, Callpoint pCallpoint!, BBjString p_dtl_tmpl$)
        
            #firm_id$   = p_firm_id$
            #precision% = p_precision%
            #callpoint! = pCallpoint!
            #dtl_tmpl$  = p_dtl_tmpl$
            
            if !util.isTableChansSet() then
                if pTableChans! = null() then
                    throw #className$+": setOrderHelper(): Pass TableChans! or set table_chans$[all] in util object", 257
                else
                    util.setTableChans(pTableChans!)
                endif
            endif
            
            data_name$ = "OPC_LINECODE"
            if #lineCodeDev% = 0   then #lineCodeDev% = int(util.getDev(data_name$))
            if #lineCodeTmpl$ = "" then #lineCodeTmpl$ = util.getTmpl(data_name$)
            
            data_name$ = "OPE_ORDDET"
            if #ordDetailDev% = 0   then #ordDetailDev% = int(util.getDev(data_name$))
            if #ordDetailTmpl$ = "" then #ordDetailTmpl$ = util.getTmpl(data_name$)
            
            data_name$ = "IVM_ITEMMAST"
            if #itemMastDev% = 0   then #itemMastDev% = int(util.getDev(data_name$))
            if #itemMastTmpl$ = "" then #itemMastTmpl$ = util.getTmpl(data_name$)
            
            data_name$ = "OPC_TAXCODE"
            if #taxcodeDev% = 0   then #taxcodeDev% = int(util.getDev(data_name$))
            if #taxcodeTmpl$ = "" then #taxcodeTmpl$ = util.getTmpl(data_name$)
            
            if #precision% = 0 then
                data_name$ = "IVS_PARAMS"
                dim params_rec$:util.getTmpl(data_name$)
                find record (util.getDev(data_name$), key=#firm_id$+"IV00") params_rec$
                #precision% = int(num(params_rec.precision$))
            endif
            
        methodend
        
    rem --- Total order totals from passed detail vector
    
        method public BBjNumber totalSales(BBjVector pDtlVect!)
        
            if #dtl_tmpl$ = "" then
                throw #className$+": totalSales(): You must pass the detail template to use this method", 258
            endif
            
            if #Inv_type$ = "" then
                throw #className$+": totalSales(): You must setInv_type() (header invoice type) before you use this method", 259
            endif
        
            precision #precision%
            ttl_sales = 0
            
            dim dtl_rec$:#dtl_tmpl$
            dim linecode_rec$:#lineCodeTmpl$
            
            if pDtlVect! <> null() and pDtlVect!.size() then
                for i = 0 to pDtlVect!.size() - 1
                    dtl_rec$ = str(pDtlVect!.getItem(i))
                    
                    rem --- GridRowDeleteStatus() does not return the correct status because the vector it's based on isn't built
                    rem print "---GridRowDeleteStatus row(", i, "): """, #callpoint!.getGridRowDeleteStatus(i), """"
                    
                    if cvs(dtl_rec$, 2) <> "" and #callpoint!.getGridRowDeleteStatus(i) <> "Y" then 
                        read record (#lineCodeDev%, key=#firm_id$+dtl_rec.line_code$, dom=*endif) linecode_rec$
                        
                        if dtl_rec.commit_flag$ = "Y" or #Inv_type$ = "P" then
                            if pos(linecode_rec.line_type$="SPN") then
                                ttl_sales = ttl_sales + (dtl_rec.unit_price * dtl_rec.qty_shipped)
                            else
                                if linecode_rec.line_type$ = "O" then 
                                    ttl_sales = ttl_sales + dtl_rec.ext_price
                                endif
                            endif    
                        endif
                    endif
                next i
            endif
            
            methodret ttl_sales
            
        methodend
        
    rem --- Total sales from set detail vector
        
        method public BBjNumber totalSales()
            if #DtlVect! = null() then
                throw #className$+": totalSales(): Pass in the detail vector or use setDtlVect()", 260
            else
                methodret #totalSales(#DtlVect!)
            endif
        methodend
        
    rem --- Order totals from disk records
    
        method public void totalSalesDisk(BBjString p_cust_id$, BBjString p_order_no$, BBjString p_inv_type$)
            print #className$+": void totalSalesDisk(p_cust_id$, p_order_no$, p_inv_type$)"; rem debug
        
            #Cust_id$  = p_cust_id$
            #Order_no$ = p_order_no$
            #Inv_type$ = p_inv_type$
        
            precision #precision%
            
            ext_price = 0
            taxable   = 0
            ext_cost  = 0
            more      = 1
            
            dim orddet_rec$:#ordDetailTmpl$
            dim linecode_rec$:#lineCodeTmpl$
            dim itemmast_rec$:#itemMastTmpl$
            
        rem --- Begin loop
        
            read (#ordDetailDev%, key=#firm_id$+"  "+p_cust_id$+p_order_no$, dom=*next)
            
            while more
                read record (#ordDetailDev%, end=*break) orddet_rec$
                if orddet_rec.firm_id$     <> #firm_id$ then break
                if orddet_rec.customer_id$ <> p_cust_id$ then break
                if orddet_rec.order_no$    <> p_order_no$ then break
                print "---Line: ", orddet_rec.line_no$; rem debug
                found = 0
                
                if #START_BLOCK then
                    find record (#lineCodeDev%, key=#firm_id$+orddet_rec.line_code$, dom=*endif) linecode_rec$
                    found = 1
                endif
                
                if !found or linecode_rec.line_type$ = "M" then continue
                
            rem --- Can't ship if not committed
                
                if orddet_rec.commit_flag$ <> "Y" and p_inv_type$ <> "P" then 
                    if linecode_rec.line_type$ <> "O" then
                        orddet_rec.qty_backord = 0
                        orddet_rec.qty_shipped = 0
                        orddet_rec.ext_price   = 0
                        orddet_rec.taxable_amt = 0
                    else
                        if orddet_rec.ext_price <> 0 then
                            orddet_rec.unit_price  = orddet_rec.ext_price
                            orddet_rec.ext_price   = 0
                            orddet_rec.taxable_amt = 0
                        endif
                    endif
                endif
                
            rem --- Determine taxable amount
            
                orddet_rec.taxable_amt = 0
                
                if linecode_rec.line_type$ <> "S" and linecode_rec.line_type$ <> "P" then
                    if linecode_rec.taxable_flag$ = "Y" then
                        print "---Line type not ""S"" and not ""P"" and linecode_rec.taxable_flag$ = ""Y"""; rem debug
                        orddet_rec.taxable_amt = orddet_rec.ext_price
                    endif
                else
                    print "---Line type ""S"" or ""P"""; rem debug
                    
                    if #START_BLOCK then
                        find record (#itemMastDev%, key=#firm_id$+orddet_rec.item_id$, dom=*endif) itemmast_rec$
                        print "---Found item"; rem debug
                        
                        if itemmast_rec.taxable_flag$ = "Y" then 
                            print "---itemmast_rec.taxable_flag$ = ""Y"""; rem debug
                            orddet_rec.taxable_amt = orddet_rec.ext_price
                        endif
                    endif
                endif
                
                print "---Taxable amount:", orddet_rec.taxable_amt; rem debug
                
                orddet_rec$ = field(orddet_rec$)
                write record (#ordDetailDev%) orddet_rec$
                
                rem print "--- Extended Price:", orddet_rec.ext_price; rem debug
                ext_price = ext_price + orddet_rec.ext_price
                taxable   = taxable   + orddet_rec.taxable_amt
                ext_cost  = ext_cost  + orddet_rec.unit_cost * orddet_rec.qty_shipped
            
            wend
            
        rem --- Set return values as fields
            
            #extPrice = ext_price
            #taxable  = taxable
            #extCost  = ext_cost 
            
        methodend
        
        method public void totalSalesDisk(BBjString p_cust_id$, BBjString p_order_no$)
        
            if #Inv_type$ <> "" then
                #totalSalesDisk(p_cust_id$, p_order_no$, #Inv_type$)
            else
                data_name$ = "OPE_ORDHDR"
                dim ordhdr_rec$:util.getTmpl(data_name$)
                ordhdr_dev = util.getDev(data_name$)
                found = 0
                
                if #START_BLOCK then
                    find record (ordhdr_dev, key=#firm_id$+"  "+p_cust_id$+p_order_no$, dom=*endif) ordhdr_rec$
                    found = 1
                endif
                
                if found then
                    #totalSalesDisk(p_cust_id$, p_order_no$, ordhdr_rec.invoice_type$)
                else
                    throw #className$+": totalSalesDisk(): Could not find order """+p_order_no$+""" for customer """+p_cust_id$+"""", 261
                endif
            endif
            
        methodend
        
        method public void totalSalesDisk()
        
            if #Cust_id$ <> "" and #Order_no$ <> "" then
                if #Inv_type$ <> "" then
                    #totalSalesDisk(#Cust_id$, #Order_no$, #Inv_type$)
                else
                    #totalSalesDisk(#Cust_id$, #Order_no$)
                endif
            else
                throw #className$+": totalSalesDisk(): You must setCust_id() and setOrder_no() before calling this method", 262
            endif
        
        methodend
        
    rem --- Three accessor methods for the return values of totalSalesDisk()
    
        method public BBjNumber getExtPrice()
            methodret #extPrice
        methodend
        
        method public BBjNumber getTaxable()
            methodret #taxable
        methodend
        
        method public BBjNumber getExtCost()
            methodret #extCost
        methodend
        
    rem --- Clear values for a new order
    
        method public void newOrder()
            #Cust_id$ = ""
            #Order_no$ = ""
            #Inv_type$ = ""
            #extPrice = 0
            #taxable = 0
            #extCost = 0
            #Freight = 0
            #Discount = 0
            #TaxAmount = 0
        methodend
        
    rem --- Calculate Tax Amount
    
        method public BBjNumber calculateTax(BBjNumber p_discount_amt, BBjNumber p_freight_amt)
        
            print #className$+ ": BBjNumber calculateTax(p_discount_amt, p_freight_amt)"; rem debug
            
            if #TaxCode$ = "" then 
                throw #className$ + ": calculateTax(): #TaxCode is null", 263
            endif
            
            print "---taxable amount in:", #getTaxable(); rem debug

            rem if ordHelp!.getTaxable() <> 0 then 
            rem 	taxable_amt = round(ordHelp!.getTaxable() - disc_per_in * ordHelp!.getTaxable() / 100, 2)
            rem else
            rem 	taxable_amt = 0
            rem endif
            
        rem --- Get taxcode record, if necessary
        
            dim taxcode_rec$:#taxcodeTmpl$
            dim taxcode_rec2$:#taxcodeTmpl$
            
            if #taxcodeData$ = "" then
                found = 0
                
                if #START_BLOCK then
                    find record (#taxcodeDev%, key=#firm_id$+#TaxCode$, err=*endif) taxcode_rec$
                    #taxcodeData$ = taxcode_rec$
                    found = 1
                endif
            else
                taxcode_rec$ = #taxcodeData$
            endif
            
            if #taxcodeData$ = "" and !found then
                throw #className$ + ": calculateTax(): tax code """ + #TaxCode$ + """ not found", 11
            endif
            
        rem --- Changes to taxable amount

            taxable_amt = max(#getTaxable() - p_discount_amt, 0)
            print "---taxable amount after discount:", taxable_amt; rem debug

            if taxcode_rec.tax_frt_flag$ = "Y" then 
                taxable_amt = taxable_amt + p_freight_amt
            endif

            rem print "---taxable amount after tax freight:", taxable_amt; rem debug

            tax_amount = 0
            tax_calc = round(taxcode_rec.tax_rate * taxable_amt / 100, 2)

            print "---Top level tax amount:", tax_calc; rem debug

            if taxcode_rec.op_max_limit <> 0 and abs(tax_calc) > taxcode_rec.op_max_limit then
                tax_calc = taxcode_rec.op_max_limit * sgn(tax_calc)
            endif

            rem print "---tax amount after limit:", tax_calc; rem debug

            tax_amount = tax_calc

        rem --- Do multi-level taxes
            
            for i=1 to 10
                tax_code$ = field(taxcode_rec$, "AR_TOT_CODE_" + str(i:"00"))
                if cvs(tax_code$,2) = "" then continue
                find record (#taxcodeDev%, key=firm_id$+tax_code$, dom=*continue) taxcode_rec2$
                tax_calc = round(taxcode_rec2.tax_rate * taxable_amt / 100, 2)

                if taxcode_rec2.op_max_limit <> 0 and abs(tax_calc) > taxcode_rec2.op_max_limit then
                    tax_calc = taxcode_rec2.op_max_limit * sgn(tax_calc)
                endif

                tax_amount = tax_amount + tax_calc
            next i

            print "---tax amount after all levels:", tax_amount; rem debug
            print "out"; rem debug
            
            methodret tax_amount
        methodend
    
    classend
    
rem ==========================================================================    
rem --- Possible Usage
rem ==========================================================================    
    
rem === AFMC

rem --- Inits

	rem use ::ado_util.src::util
    rem use ::sys/prog/bao_callpoint.bbj::Callpoint
	use ::ado_order.src::OrderHelper
	rem use ::adc_array.aon::ArrayObject
    
rem === BSHO

    rem ...
	rem gosub open_tables

rem --- Set table_chans$[all] into util object for getDev() and getTmpl()

	declare ArrayObject tableChans!

	call stbl("+DIR_PGM")+"adc_array.aon::str_array2object", table_chans$[all], tableChans!, status
	if status = 999 then goto std_exit
	util.setTableChans(tableChans!)
    rem ...

rem --- Order Helper object

	declare OrderHelper ordHelp!
    declare Callpoint callpoint!

	ordHelp! = new OrderHelper(firm_id$, int(num(ivs01a.precision$)), callpoint!, dtlg_param$[1,3])
	callpoint!.setDevObject("order_helper_object", ordHelp!)
        
rem === "CUSTOMER_ID:AVAL"

    declare BBjVector GridVect!

    if user_tpl.credit_installed$ = "Y" and user_tpl.display_bal$ = "A" then
        ordHelp! = cast(OrderHelper, callpoint!.getDevObject("order_helper_object"))
        ordHelp!.setDtlVect( cast(BBjVector, GridVect!.getItem(0)) )
        call user_tpl.pgmdir$+"opc_creditmgmnt.aon", cust_id$, ordHelp!, table_chans$[all], callpoint!, status
    endif
        
rem === CRCH

rem --- Credit check?

	cust_id$ = callpoint!.getColumnData("OPE_ORDHDR.CUSTOMER_ID")

	if user_tpl.credit_installed$ = "Y" and user_tpl.display_bal$ <> "N" and cvs(cust_id$, 2) <> "" then
		ordHelp! = cast(OrderHelper, callpoint!.getDevObject("order_helper_object"))
		ordHelp!.setDtlVect( cast(BBjVector, GridVect!.getItem(0)) )
		call user_tpl.pgmdir$+"opc_creditmgmnt.aon", cust_id$, ordHelp!, table_chans$[all], callpoint!, status
	endif

rem === Detail ===

rem === BGDS

rem --- Total detail lines

	cust_id$  = callpoint!.getColumnData("OPE_ORDDET.CUSTOMER_ID")
	order_no$ = callpoint!.getColumnData("OPE_ORDDET.ORDER_NO")
	inv_type$ = callpoint!.getHeaderColumnData("OPE_ORDHDR.INVOICE_TYPE")

	ordHelp! = cast(OrderHelper, callpoint!.getDevObject("order_helper_object"))
	ordHelp!.totalSalesDisk(cust_id$, order_no$, inv_type$)
	
	user_tpl.ext_price = ordHelp!.getExtPrice()
	user_tpl.taxable   = ordHelp!.getTaxable()
	user_tpl.ext_cost  = ordHelp!.getExtCost()
    

rem ==========================================================================
calc_grid_totals: rem --- Roll thru all detail line, totaling ext_price
                  rem     OUT: user_tpl.ord_tot
rem ==========================================================================

	ordHelp! = cast(OrderHelper, callpoint!.getDevObject("order_helper_object"))
	tamt = ordHelp!.totalSales( cast(BBjVector, GridVect!.getItem(0)) )
	user_tpl.ord_tot = tamt

    return
    
rem ...

    std_exit:

    end
