rem adc - Get proper batch from user (Public/Called)
rem Program adc_getbatch v8.0.0 21Mar2008

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 12Apr2008 |
rem  |  Copyright (c) 1981-2008 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- PROC_ID$  : Process id calling program (in)
rem --- PROG_NAME$: Program Name to determine if new batches allowed (in)
rem --- BATCH_ID$ : Returned Batch Number (blank if none)

    seterr std_error
    setesc std_error
    enter proc_id$,prog_name$,batch_id$

rem --- Open/Lock files

    files=2,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="adm-09",ids$[1]="ADM_PROCMASTER"
    files$[2]="adm-19",ids$[2]="ADM_PROCDETAIL"

    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    
    adm09_dev=channels[1]
    adm19_dev=channels[2]

rem --- Dimension string templates

    dim adm09a$:templates$[1],adm19a$:templates$[2]

rem --- Setup default values

    batch_id$=""
    allow_entry$=""

rem --- See if program is in batch file

    read record (adm09_dev,key=pad(proc_id$,16),dom=done)adm09a$
    if adm09a.batch_entry$<>"Y" goto done

rem --- Now see if this program allows batch entry or not

    read record (adm19_dev,key=pad(proc_id$,16),dom=*next)
    while 1
        readrecord(adm19_dev,end=*break)adm19a$
        if cvs(adm19a.program_name$,2)=cvs(prog_name$,2)
            allow_entry$=adm19a.new_batch$
            break
        endif
    wend

rem --- Here's where we call the proper form (allow new or not)

    batch_id$=allow_entry$

done: rem --- All done

    goto std_exit

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
    call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry

std_error_exit:
    
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
    
std_error_release:

    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    close (adm09_dev,err=*next)
    close (adm19_dev,err=*next)
    exit

rem #endinclude std_exit.src

    end