rem --- Upgrade Conversion Utility For Version 25.00
rem --- Program adc_v2500conversion.aon

rem --- AddonSoftware Version 25.00
rem --- Copyright BASIS International Ltd.  All Rights Reserved.


rem --- CONVCTRLFILECHAN : Channel number conversion control file data/adx_conversionCtrl is open on (Input)
rem --- CONVCTRL!        : Property object of the conversion control file data/adx_conversionCtrl (Input)
rem --- INSTALLMAP!      : HashMap object of installed modules, keyed by module ID (Input)
rem --- rdForm!          : Window object to hold progress meter
rem --- Translate!       : Translator object translations
rem --- CONV_STATUS      : Status code/0=No error (Returned)

    use java.io.File
    use java.util.HashMap
    use java.util.GregorianCalendar
    use java.util.Properties

    use ::ado_file.src::FileObject
    use ::ado_SyncRecords.aon::SyncRecords

    setesc std_error
    seterr std_error

    enter convCtrlFileChan,convCtrl!,installMap!,rdForm!,Translate!,conv_status

rem --- Initializations

    conv_status=1
    dir_syp$=stbl("+DIR_SYP")
    pgmdir$=stbl("+DIR_PGM",err=*next)
    dataDir$=FileObject.fixPath(stbl("+DIR_DAT",err=*next), "/")

    Calendar! = new GregorianCalendar()
    
rem --- Open log file in aon/logs directory

    num_files=1
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    open_tables$[1]="DDM_SYSTEMS",open_opts$[1]="OTA"
    gosub open_tables
    ddm_systems=num(open_chans$[1]);dim ddm_systems$:open_tpls$[1]
    
    aonDir$=""
    readrecord(ddm_systems,key=pad("ADDON",16," "),knum="SYSTEM_ID",err=*next)ddm_systems$
    if cvs(ddm_systems.mount_dir$,2)<>"" then
        aonDir$=ddm_systems.mount_dir$
    endif

    location$ = aonDir$
    gosub open_log
    
rem --- Start progress meter

    meter_title$="adc_v2500conversion.aon"
    meter_proc_recs=0
    meter_text$=""
    meter_action$="WIN-LST"
    gosub disp_meter

rem --- For version 24.01, Copy ADC_STATECODE ZZ records for all firms (ER 8362)
    this_module$="AD"
    if installMap!.get(this_module$)="Y" then
        rem --- Increment progress meter
        meter_text$=this_module$+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:           Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$

		rem --- Initialize AD data file changes implemented during minor releases of v24
        task$="2400_AD_v24initMinorRev"
        if convCtrl!.getProperty(task$)<>"Completed" then
            rem --- Execute AP upgrade conversion utility for minor releases of v17
            theFirm$=""
            call pgmdir$+"adc_v24initMinorRev.aon",theFirm$,rdForm!,status
            if status=0 then
                rem --- Conversion completed successfully
                write(convCtrlFileChan)task$+"=Completed"
                
                rem --- Increment progress meter
                meter_text$=Translate!.getTranslation("AON_DATA_CONV","Data conversion")+" "+
:                   Translate!.getTranslation("AON_COMPLETED_FOR","completed for")+": APM_VENDCMTS"
                meter_proc_recs=meter_proc_recs+1
                meter_action$="MTR-LST"
                gosub disp_meter
                print(log_dev)meter_text$
            else
                rem --- Conversion did NOT complete successfully, exit with error message
                err_msg$="***** "+Translate!.getTranslation("AON_ERROR","Error")+": "+
:                   Translate!.getTranslation("AON_DATA_CONV","Data conversion")+" "+Translate!.getTranslation("AON_NOT","Not")+" "+
:                   Translate!.getTranslation("AON_COMPLETED_FOR","completed for")+": ADC_STATECODE"+" *****"
                goto done
            endif
        else
            rem --- Increment progress meter
            meter_text$=Translate!.getTranslation("AON_CONV_PREVIOUSLY","Conversion previously completed for")+" "+task$
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        endif
    else
        rem --- Increment progress meter
        meter_text$=this_module$+" "+Translate!.getTranslation("AON_NOT","Not")+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:           Translate!.getTranslation("AON_SKIP","SKIP")+" "+Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif

rem --- Initialize AR data file changes implemented during minor releases of v25

    this_module$="AR"
    if installMap!.get(this_module$)="Y" then
        task$="2500_AR_arc_labelcode"
        if convCtrl!.getProperty(task$)<>"Completed" then
            rem --- Increment progress meter
            meter_text$=this_module$+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:               Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        
            rem --- Increment progress meter
            meter_text$=Translate!.getTranslation("AON_INITIALIZING","Initializing")+": ARC_LABELCODE"
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
    
            rem --- Execute AR upgrade conversion utility for minor releases of v25
            theFirm$=""
            call pgmdir$+"arc_v25initMinorRev.aon",theFirm$,rdForm!,status
            if status=0 then
                rem --- Conversion completed successfully
                rem --- Increment progress meter
                meter_text$=Translate!.getTranslation("AON_DATA_CONV","Data conversion")+" "+
:                       Translate!.getTranslation("AON_COMPLETED_FOR","completed for")+": ARC_LABELCODE"
                meter_proc_recs=meter_proc_recs+1
                meter_action$="MTR-LST"
                gosub disp_meter
                print(log_dev)meter_text$
                write(convCtrlFileChan)task$+"=Completed"
            else
                rem --- Conversion did NOT complete successfully, exit with error message
                err_msg$="***** "+Translate!.getTranslation("AON_ERROR","Error")+": "+
:                       Translate!.getTranslation("AON_DATA_CONV","Data conversion")+" "+Translate!.getTranslation("AON_NOT","Not")+" "+
:                       Translate!.getTranslation("AON_COMPLETED_FOR","completed for")+": ARC_LABELCODE"+" *****"
                goto done
            endif
        else
            rem --- Increment progress meter
            meter_text$=Translate!.getTranslation("AON_CONV_PREVIOUSLY","Conversion previously completed for")+" "+task$
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        endif
    else
        rem --- Increment progress meter
        meter_text$=this_module$+" "+Translate!.getTranslation("AON_NOT","Not")+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:           Translate!.getTranslation("AON_SKIP","SKIP")+" "+Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif

rem --- Create historical PO-to-SO link for links that are now closed, and update PO_NO in POT_REQHDR_ARC
    this_module$="PO"
    if installMap!.get(this_module$)="Y" then
        rem --- Increment progress meter
        meter_text$=this_module$+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:           Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$

        task$="2500_PO_pot_linked"
        if convCtrl!.getProperty(task$)<>"Completed" then
            rem --- Create historical PO-to-SO link for links that are now closed
            
            rem --- Open File(s)
            num_files=9
            dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
            open_tables$[1]="ADM_FIRMS",  open_opts$[1]="OTA"
            open_tables$[2]="POE_REQDET", open_opts$[2]="OTA"
            open_tables$[3]="POE_PODET",  open_opts$[3]="OTA"
            open_tables$[4]="POE_RECDET", open_opts$[4]="OTA"
            open_tables$[5]="POE_INVDET", open_opts$[5]="OTA"
            open_tables$[6]="POT_RECDET", open_opts$[6]="OTA"
            open_tables$[7]="POT_RECHDR", open_opts$[7]="OTA"
            open_tables$[8]="POE_LINKED", open_opts$[8]="OTA"
            open_tables$[9]="POT_LINKED", open_opts$[9]="OTA"
    
            gosub open_tables
    
            admFirms_dev=num(open_chans$[1]);  dim admFirms$:open_tpls$[1]
            poeReqDet_dev=num(open_chans$[2]); dim poeReqDet$:open_tpls$[2]
            poe12_dev=num(open_chans$[3]);     dim poe12a$:open_tpls$[3]
            poeRecDet_dev=num(open_chans$[4]); dim poeRecDet$:open_tpls$[4]
            poe25_dev=num(open_chans$[5]);     dim poe25a$:open_tpls$[5]
            pot14_dev=num(open_chans$[6]);     dim pot14a$:open_tpls$[6]
            pot04_dev=num(open_chans$[7]);     dim pot04a$:open_tpls$[7]
            poeLinked_dev=num(open_chans$[8]); dim poeLinked$:open_tpls$[8]
            potLinked_dev=num(open_chans$[9]); dim potLinked$:open_tpls$[9]
 
            rem --- Get firm IDs used in this installation
            firms!=BBjAPI().makeVector()
            read(admFirms_dev,key="",dom=*next)
            while 1
                readrecord(admFirms_dev,end=*break)admFirms$
                firms!.addItem(admFirms.company_id$)
            wend

            rem --- For each firm update their PO-to-SO links
            for i=0 to firms!.size()-1
                thisFirm$=firms!.getItem(i)
                if thisFirm$="99" then continue
                gosub update_PO_SO_links
            next i
            
            rem --- Add PO-to-SO historical links for links in PO Receipt History
            read(pot14_dev,key="",dom=*next)
            while 1
                readrecord(pot14_dev,end=*break)pot14a$
                if cvs(pot14a.so_int_seq_ref$,2)<>"" then
                    redim pot04a$
                    readrecord(pot04_dev,key=pot14a.firm_id$+pot14a.po_no$+pot14a.receiver_no$,dom=*next)pot04a$
                    if cvs(pot04a.customer_id$,2)<>"" and cvs(pot04a.order_no$,2)<>"" then
                        redim potLinked$
                        potLinked.firm_id$=pot14a.firm_id$
                        potLinked.po_no$=pot14a.po_no$
                        potLinked.poedet_seq_ref$=pot14a.po_int_seq_ref$
                        potLinked.customer_id$=pot04a.customer_id$
                        potLinked.order_no$=pot04a.order_no$
                        potLinked.opedet_seq_ref$=pot14a.so_int_seq_ref$
                        writerecord(potLinked_dev)potLinked$
                    endif
                endif
            wend
        else
            rem --- Increment progress meter
            meter_text$=Translate!.getTranslation("AON_CONV_PREVIOUSLY","Conversion previously completed for")+" "+task$
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        endif

        task$="2500_PO_pot_reqhdr_arc"
        if convCtrl!.getProperty(task$)<>"Completed" then
            rem --- Update PO_NO in POT_REQHDR_ARC
 
            rem --- Open File(s)
            num_files=2
            dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
            open_tables$[1]="POT_POHDR_ARC",  open_opts$[1]="OTA"
            open_tables$[2]="POT_REQHDR_ARC", open_opts$[2]="OTA"
    
            gosub open_tables
    
            potPoHdrArc_dev=num(open_chans$[1])
            potReqHdrArc_dev=num(open_chans$[2])
            dim potPoHdrArc$:open_tpls$[1]
            dim potReqHdrArc$:open_tpls$[2]
                
            rem --- Use PO_NO from POT_POHDR_ARC to update POT_REQHDR_ARC
            read(potPoHdrArc_dev,key="",dom=*next)
            while 1
                potPoHdrArc_key$=key(potPoHdrArc_dev,end=*break)
                readrecord(potPoHdrArc_dev)potPoHdrArc$
                if cvs(potPoHdrArc.req_no$,2)="" then continue; rem --- not a requisition
                
                call pgmdir$+"adc_copyfile.aon",potPoHdrArc$,potReqHdrArc$,status
                if status=0 then
                    potReqHdrArc.po_no$=potPoHdrArc.po_no$
                    potReqHdrArc$=field(potReqHdrArc$)
                    writerecord(potReqHdrArc_dev)potReqHdrArc$
                endif
            wend
        else
            rem --- Increment progress meter
            meter_text$=Translate!.getTranslation("AON_CONV_PREVIOUSLY","Conversion previously completed for")+" "+task$
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        endif
    else
        rem --- Increment progress meter
        meter_text$=this_module$+" "+Translate!.getTranslation("AON_NOT","Not")+" "+Translate!.getTranslation("AON_INSTALLED","Installed")+", "+
:           Translate!.getTranslation("AON_SKIP","SKIP")+" "+Translate!.getTranslation("AON_DO_DATA_CONV","Doing data conversion for")+" "+this_module$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif

done: rem --- Done, close things up

    rem --- Complete progress meter
    completed_successfully=0
    if err_msg$<>"" then
        meter_text$=err_msg$
    else
        meter_text$=Translate!.getTranslation("AON_CONV_COMPLETED","Conversions completed successfully")
        completed_successfully=1
    endif
    meter_proc_recs=meter_total_recs
    meter_action$="MTR-LST"
    gosub disp_meter
    print(log_dev)meter_text$
    
    rem --- Stop progress meter
    meter_text$=""
    meter_action$="LST-END"
    gosub disp_meter

    rem --- Complete and close log file
    print(log_dev)
    print(log_dev)"v2500conversion log finished: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
    close(log_dev)

    rem --- Close opened files
    if ddm_table_tpls then close(ddm_table_tpls,err=*next)

    if completed_successfully then conv_status=0; rem --- no errors encountered
	goto std_exit

get_path_to_old_file: rem --- Locate data directory in old Addon installation
    rem --- IN: oldStbl$ - STBL in old addon.syn that points to the data directory where the old file is located
    rem --- IN: oldFile$ - name of old file to get path for
    rem --- OUT: oldPathToFile$ - full path to requested file, including the file name
    oldPathToFile$=oldFile$

    rem --- Open old aon/config/addon.syn
    open_success=0
    old_addonsyn_dev=unt
    open(old_addonsyn_dev,isz=-1,err=*next)convCtrl!.getProperty("addon_source")+"/aon/config/addon.syn"; open_success=1
    if !open_success then return

    rem --- Get specified data directory from old addon.syn
    key$="=SET "+oldStbl$+"="
    while 1
        read(old_addonsyn_dev,end=*break)record$
        if(pos(key$=record$)) then
            xpos=pos(key$=record$)
            oldPathToFile$=record$(xpos+len(key$))+oldFile$
            break
        endif
    wend
    close(old_addonsyn_dev)

    return

open_log: rem --- Open log file
           
    rem --- create logs directory at location
    logDir$=FileObject.fixPath(location$ + "/logs", "/")
    FileObject.makeDirs(new File(logDir$))

    rem --- create and open log file
    log$=logDir$+"/v2500conversion_"+DATE(0:"%Yd%Mz%Dz")+"_"+DATE(0:"%Hz%mz")+".log"
    erase log$,err=*next
    string log$
    log_dev=unt
    open(log_dev)log$
            
    rem --- write log header info
    print(log_dev)"v2500conversion log started: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
    print(log_dev)

    return

update_PO_SO_links: rem --- Create historical PO-to-SO link for links that are now closed
    linkedSOs!=new HashMap()

    rem --- Check ope_reqdet for open links to SO detail lines
    read(poeReqDet_dev,key=thisFirm$,dom=*next)
    while 1
        poeReqDet_key$=key(poeReqDet_dev,end=*break)
        if pos(thisFirm$=poeReqDet_key$)<>1 then break
        readrecord(poeReqDet_dev)poeReqDet$
        if cvs(poeReqDet.so_int_seq_ref$,2)<>"" then linkedSOs!.put(poeReqDet.so_int_seq_ref$, poeReqDet_key$)
    wend

    rem --- Check ope_podet for open links to SO detail lines
    read(poe12_dev,key=thisFirm$,dom=*next)
    while 1
        poe12_key$=key(poe12_dev,end=*break)
        if pos(thisFirm$=poe12_key$)<>1 then break
        readrecord(poe12_dev)poe12a$
        if cvs(poe12a.so_int_seq_ref$,2)<>"" then linkedSOs!.put(poe12a.so_int_seq_ref$, poe12_key$)
    wend

    rem --- Check ope_recdet for open links to SO detail lines
    read(poeRecDet_dev,key=thisFirm$,dom=*next)
    while 1
        poeRecDet_key$=key(poeRecDet_dev,end=*break)
        if pos(thisFirm$=poeRecDet_key$)<>1 then break
        readrecord(poeRecDet_dev)poeRecDet$
        if cvs(poeRecDet.so_int_seq_ref$,2)<>"" then linkedSOs!.put(poeRecDet.so_int_seq_ref$, poeRecDet_key$)
    wend

    rem --- Check ope_invdet for open links to SO detail lines
    read(poe25_dev,key=thisFirm$,dom=*next)
    while 1
        poe25_key$=key(poe25_dev,end=*break)
        if pos(thisFirm$=poe25_key$)<>1 then break
        readrecord(poe25_dev)poe25a$
        po_no$=poe25a.po_no$
        receiver_no$=poe25a.po_no$
        
        read(pot14_dev,key=thisFirm$+po_no$+receiver_no$,dom=*next)
        while 1
            pot14_key$=key(pot14_dev,end=*break)
            if pos(thisFirm$=pot14_key$)<>1 then break
            readrecord(pot14_dev)pot14a$
            if cvs(pot14a.so_int_seq_ref$,2)<>"" then linkedSOs!.put(pot14a.so_int_seq_ref$, pot14_key$)
        wend
    wend

    rem --- Move poe_linked record to pot_linked if link NOT found in PO req/po/rec/inv entry files
    read(poeLinked_dev,key=thisFirm$,dom=*next)
    while 1
        poeLinked_key$=key(poeLinked_dev,end=*break)
        if pos(thisFirm$=poeLinked_key$)<>1 then break
        readrecord(poeLinked_dev)poeLinked$
        if linkedSOs!.get(poeLinked.opedet_seq_ref$)=null() then
            writerecord(potLinked_dev)poeLinked$
            remove(poeLinked_dev,key=poeLinked_key$)
        endif
    wend
    
    return
    
disp_meter: rem --- Display Progress Meter

    rem --- sysGUI! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjSysGui sysGUI!
    sysGUI!=BBjAPI().getSysGui()

    rem --- rdForm! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjTopLevelWindow rdForm!

    rem --- meterWin! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjTopLevelWindow meterWin!

    call dir_syp$+"bam_prog_bar.bbj",
:       sysGUI!,
:       rdForm!,
:       meterWin!,
:       meter_title$,
:       meter_total_recs,
:       meter_proc_recs,
:       meter_text$,
:       meter_action$

    return
   
open_tables:rem --- Open Tables

    call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:       open_beg,
:       open_end,
:       open_tables$[all],
:       open_opts$[all],
:       open_chans$[all],
:       open_tpls$[all],
:       rd_table_chans$[all],
:       open_batch,
:       open_status$

    if open_status$<>"" then
        msg_id$="ENTRY_OPEN_ERROR"
        dim msg_tokens$[1]
        msg_tokens$[1]=open_status$
        gosub disp_message
        rdFuncSpace!=bbjAPI().getGroupNamespace().setValue("+build_task","OFF")
        release
    endif
    return

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

    return

rem #include std_error.src

std_error: rem --- Standard error handler (22Feb2013)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src


rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)
    
    exit

rem #endinclude std_exit.src

    end
