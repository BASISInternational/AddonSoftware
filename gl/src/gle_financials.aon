rem gle - Financial Report Printing (Lead Overlay)
rem Program gle_financials v8.0.0 03Jun2006

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- This program is the lead overlay for financial report
rem --- generation and printing. The following sequence of programs
rem --- are run:

rem --- glu_financials: Compiles the data necessary to produce the
rem ---                 reports from the Summary Activity (glm-02)
rem ---                 file. This program is only run if the user
rem ---                 has chosen NOT to reprint existing reports.
rem --- glr_headings  : Assembles the headings for the financial reports
rem ---                 by replacing heading tokens with actual values.
rem ---                 This program is the next overlay when the user has
rem ---                 chosen to reprint existing reports.
rem --- glr_financials: Produces the custom financial reports from 
rem ---                 either existing data (when reprinting) or from
rem ---                 the new data compiled by glu_financials.

rem --- Arguments provided by the input form are

rem --- BEGPER$ : Fiscal period for the reports
rem --- BEGYEAR$: Fiscal year for the reports
rem --- REPRINT$: Reprint flag (Y/N)

rem --- The list of reports to be printed is contained in the 
rem --- Financial Report Selections (gle-04) table.

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Open/Lock files

    files=14,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="ads-01"
    files$[2]="gle-04"
    files$[3]="gle-06"
    files$[4]="gle-07"
    files$[5]="gle-08"
    files$[6]="gle-09"
    files$[7]="glm-01"
    files$[8]="glm-02"
    files$[9]="glm-11"
    files$[10]="glm-12"
    files$[11]="glm-13"
    files$[12]="glm-21"
    files$[13]="glm-22"
    files$[14]="glm-32"
    options$[2]="L"
    options$[3]="L"
    options$[13]="L"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    ads01_dev=channels[1]
    gle04_dev=channels[2]
    gle06_dev=channels[3]
    gle07_dev=channels[4]
    gle08_dev=channels[5]
    gle09_dev=channels[6]
    glm01_dev=channels[7]
    glm02_dev=channels[8]
    glm11_dev=channels[9]
    glm12_dev=channels[10]
    glm13_dev=channels[11]
    glm21_dev=channels[12]
    glm22_dev=channels[13]
    glm32_dev=channels[14]

rem --- Dimension string templates

    dim gle04a$:templates$[2],gle06a$:templates$[3],gle07a$:templates$[4],gle08a$:templates$[5],
:       gle09a$:templates$[6],glm01a$:templates$[7],glm02a$:templates$[8],glm11a$:templates$[9],
:       glm12a$:templates$[10],glm13a$:templates$[11],glm21a$:templates$[12],glm22a$:templates$[13],
:       glm32a$:templates$[14]

rem --- Retrieve miscellaneous templates

    files=2,begfile=1,endfile=files
    dim ids$[files],templates$[files]
    ids$[1]="gls-01A"
    ids$[2]="gls-01C"
    call pgmdir$+"adc_template.aon",begfile,endfile,ids$[all],templates$[all],status
    if status goto std_exit

rem --- Dimension miscellaneous string templates

    dim gls01a$:templates$[1],gls01c$:templates$[2]

rem --- Retrieve parameter records

    gls01a_key$=firm_id$+"GL00"
    find record (ads01_dev,key=gls01a_key$,err=std_missing_params) gls01a$
    total_pers=num(gls01a.total_pers$)
    acctlen=num(gls01a.acct_length$)
    m0$=gls01a.acct_output$
    m1$=gls01a.amount_mask$
    m2$=gls01a.unit_mask$
    m3$=gls01a.percent_mask$
    mp=13

rem --- Assign input values to local variables

    begper$=rd_rec_data$[1,0]
    begyear$=rd_rec_data$[2,0]
    reprint$=rd_rec_data$[3,0]
    if cvs(begper$,2)="" begper$=gls01a.current_per$
    if cvs(begyear$,2)="" begyear$=gls01a.current_year$
    reprint=reprint$="Y"

rem --- Retrieve GL Financial Report Parameter record and either update it
rem --- or create it if necessary

    gls02c.firm_id$=firm_id$
    gls02c.gl$="GL"
    gls02c.sequence_02$="02"
    gls02c.period$=begper$
    gls02c.year$=begyear$
    gls02c.gl_updt_seq_01$="0"
    gls02c.gl_updt_seq_02$="0"
    gls02c.print_flag_01$="0"
    gls02c.print_flag_02$="0"
    gls01c_key$=firm_id$+"GL02"
    find record (ads01_dev,key=gls01c_key$,err=*next) gls01c$
    sequence=num(gls02c.gl_updt_seq_01$)
    sequence$=str(sequence)
    write record (ads01_dev,key=gls01c_key$) gls01c$
    if reprint=0
       sequence=sequence+1
       if sequence>9 sequence=0
       sequence$=str(sequence$)
       gls02c.gl_updt_seq_01$=sequence$
       write record (ads01_dev,key=gls01c_key$) gls01c$
    endif

rem --- Initializations

    overlay=0
    financials=1
    report$="glr_financials.aon"
    overlay$="glu_financials.aon"
    if reprint then overlay$="glr_headings.aon"

rem --- Position file and run next overlay (report or update)

    read (gle04_dev,key=firm_id$,dom=*next)
    run pgmdir$+overlay$,err=std_exit

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$

std_missing_params_exit:
    
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
    
std_missing_params_release:

    status=999
    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run pgmdir$+"ads_process_end.aon",err=*next
    release

rem #endinclude std_end.src

    end
