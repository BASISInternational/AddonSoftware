rem glu - Update Beginning Balances
rem Program glu_beginbalance v8.0.0 12May2006 (glu_ka)
rem Created by adx_progconv.bbx v1.0.3 (05/12/2006 12:44:49)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- This program is run from two different menu options:

rem --- gle_beginbalance - Update Beginning Balances
rem --- gle_yearend      - Year End Update

rem --- The following arguments are supplied by these front-end programs:

rem --- PROGRAM$: Overlay to be run after the beginning balances program
rem ---         : (glu_beginbalance) finishes. Not used in this program.
rem ---         : The Update Beginning Balances task passes ads_process_end.aon
rem ---         : The Year End Update program passes glu_yearend.aon
rem --- PROMPT$ : Y=Update Beginning Balances (prompt for permission to update
rem ---         :   the register).
rem ---         : N=Year End Update (don't prompt for permission to update the
rem ---         :   register, update is automatically performed).
rem --- UPDATE$ : Y=Year End Update (user prompted for permission to close year).
rem ---         : N=Update Beginning Balances (user prompted for approval to
rem ---         :   produce the register).

    setesc std_error
    seterr std_error

rem --- Initializations

    more=1

rem --- Options

    if prompt$="Y" 
       call pgmdir$+"adc_yesno.aon",0,"Are you ready to update Account Beginning Balances",0,v$,v3
       if v$<>"YES" goto std_exit
    endif

rem --- Position File

    call pgmdir$+"adc_progress.aon","N",title$,"","","",m0,glw03_dev,1,meter_num,status
    read (glw03_dev,key=firm_id$,dom=*next)

rem --- Read next glw-03 record

    while more
          k$=key(glw03_dev,end=*break)
          if pos(firm_id$=k$)<>1 break
          read record (glw03_dev,key=k$)glw03a$
          call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status

          rem --- Retrieve glm-02 record

          glm02_key$=firm_id$+glw03a.gl_account$+"4"
          find record (glm02_dev,key=b0$,dom=*next)glm02a$
          glm02a.begin_amt=glw03a.begin_amt
          glm02a.begin_units=glw03a.begin_units
          write record (glm02_dev)glm02a$
    wend

rem --- All done

    run program$,err=std_exit

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run pgmdir$+"ads_process_end.aon",err=*next
    release

rem #endinclude std_end.src

    end
