rem glr - GL Budget Revision Register (Lead Overlay)
rem Program gle_budgetupdate v8.0.0 27May2006 (glr_ra)
rem Created by adx_progconv.bbx v1.0.3 (05/27/2006 05:26:55)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Open/Lock files

    files=3,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="ads-01"
    files$[2]="glm-08"
    files$[3]="glm-18"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    ads01_dev=channels[1]
    glm08_dev=channels[2]
    glm18_dev=channels[3]

rem --- Retrieve miscellaneous templates

    files=1,begfile=1,endfile=files
    dim ids$[files],templates$[files]
    ids$[1]="gls-01A"
    call pgmdir$+"adc_template.aon",begfile,endfile,ids$[all],templates$[all],status
    if status goto std_exit

rem --- Dimension miscellaneous string templates

    dim gls01a$:templates$[1]

rem --- Retrieve parameter records

    gls01a_key$=firm_id$+"GL00"
    find record (ads01_dev,key=gls01a_key$,err=std_missing_params) gls01a$
    acctlen=num(gls01a.acct_length$)
    m0$=gls01a.acct_output$
    headings=3

rem --- Assign input values to local variables

    id$=rd_rec_data$[1,0]
    au$=rd_rec_data$[2,0]
    begacct$=rd_rec_data$[3,0]
    endacct$=rd_rec_data$[4,0]
    wildcard$=rd_rec_data$[5,0]
    rounding$=rd_rec_data$[6,0]
    update_year$=rd_rec_data$[7,0]

rem --- If no record id or type was specified, use first on file for this firm

    read (glm08_dev,key=firm_id$,dom=*next)
    k$=key(glm08_dev,end=std_exit)
    if id$="" id$=k$(3,1)
    if au$="" au$=k$(4,1)

rem --- Initializations

    dim headings$[headings]
    update$="glu_budgetupdate.aon"

rem --- Retrieve budget title

    call pgmdir$+"glc_suminfo.aon",glm08_dev,glm18_dev,firm_id$,id$,au$,description$,x$,status

rem --- Initialize report headings

    headings$[0]=cvs(sysinfo.firm_name$,2)
    headings$[1]=cvs(sysinfo.task_desc$,2)
    if cvs(begacct$,2)="" x$="First Account"
:                    else x$="Account "+fnmask$(begacct$(1,acctlen),m0$)
    if cvs(endacct$,2)="" y$="Last Account"
:                    else y$="Account "+fnmask$(endacct$(1,acctlen),m0$)
    x$="From "+x$+" Through "+y$
    if cvs(wildcard$,2)<>"" then x$=x$+" (Wildcard "+$22$+cvs(wildcard$,2)+$22$+")"
    headings$[2]=x$
    x$="Budget "+$22$+id$+$22$+" "+cvs(description$,2)
    if au$="A" y$="(Amounts)"
:         else y$="(Units)"
    headings$[3]=x$+" "+y$

rem --- Run report overlay

    close (ads01_dev,err=*next)
    close (glm08_dev,err=*next)
    close (glm18_dev,err=*next)
    run pgmdir$+"glr_summary.aon"

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$

std_missing_params_exit:
    
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
    
std_missing_params_release:

    status=999
    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run pgmdir$+"ads_process_end.aon",err=*next
    release

rem #endinclude std_end.src

    end
