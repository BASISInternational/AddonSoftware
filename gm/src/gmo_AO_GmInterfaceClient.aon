rem ==========================================================================
rem --- gmo_AO_GMInterfaceClient.aon 
rem --- AO_GMInterfaceClient class (GMInterfaceClient superclass)
rem --- Interface client to BBj web service on GoldMine server.
rem --- 
rem --- AddonSoftware Version 15.0
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem ==========================================================================

    use ::ado_file.src::FileObject

    use java.io.File
    use java.io.StringReader
    use java.io.StringWriter
    use java.util.Properties
    
    use javax.xml.parsers.DocumentBuilder
    use javax.xml.parsers.DocumentBuilderFactory
    use javax.xml.transform.OutputKeys
    use javax.xml.transform.Transformer
    use javax.xml.transform.TransformerFactory
    use javax.xml.transform.dom.DOMSource
    use javax.xml.transform.stream.StreamResult

    use org.apache.commons.httpclient.HttpClient
    use org.apache.commons.httpclient.HttpStatus
    use org.apache.commons.httpclient.methods.PostMethod

    use org.w3c.dom.Attr
    use org.w3c.dom.Document
    use org.w3c.dom.Element
    use org.w3c.dom.Node
    use org.w3c.dom.NodeList
    
    use org.xml.sax.InputSource

    class public AO_GMInterfaceClient
        field private BBjNumber BLOCKSIZE = 2^20
        field private BBjInt devErrLog% = -1
        field private BBjInt devGmqCustomer% = -1
        field private BBjInt devGmxCustomer% = -1
        field private BBjInt devPushLog% = -1
        field private BBjInt devSQL% = -1
rem wgh ...
        field private BBjString gmMasterPw$ = "wgh1Pw"
rem wgh ...
        field private BBjString gmMasterUser$ = "wgh1"
rem wgh ...
        field private BBjInt gmSessionId% = 49
        field private BBjString sqlDb$ = ""
rem wgh ...
        field private BBjString sqlPassword$ = "wgh2Pw"
        field private BBjString sqlURL$ = ""
rem wgh ...
        field private BBjString sqlUser$ = "wgh2"
        field private BBjString tplGmqCustomer$ = ""
        field private BBjString tplGmxCustomer$ = ""
        field private BBjNumber waitTime = 60
rem wgh ...
        field private BBjString webServiceURL$ = "https://bbjserver.basis.com:8443/servlet/ChileCustomer/json/balance?custnum=10"

        rem /**
        rem  * Constructor.
        rem  *
        rem  */
        method public AO_GMInterfaceClient()
            seterr GMInterfaceClient_error

rem wgh ... Open GMS_PARAMS
rem wgh ... Open ddm_table_tpls
rem wgh ... Get template for GMS_PARAMS
rem wgh ... Read parameters (does NOT depend on firm_id)
rem wgh ... Initialize field variables 
rem wgh ... ... gmMasterPw = GM_MASTER_PW
rem wgh ... ... mMasterUser = GM_MASTER_USER
rem wgh ... ... sqlDb = SQL_DB_ALIAS
rem wgh ... ... sqlPassword = SQL_DB_PASSWORD
rem wgh ... ... sqlURL = SQL_CONNECTION_URL
rem wgh ... ... sqlUser = SQL_DB_USER
rem wgh ... ... webServiceURL = WEBSERVICE_URL
rem wgh ... ... Close GMS_PARAMS
            
            methodret
            
GMInterfaceClient_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Launch interface client to BBj web service on GoldMine server.
        rem  *
        rem  */
        method public void launchClient()
            seterr launchClient_error

            rem --- As needed, open push log file
            if #devPushLog% < 0 then 
                rem --- Open push log file in aon/logs directory, if possible
                aonDir$ = ""
                dataDir$ = FileObject.fixPath(stbl("+DIR_DAT",err=*next), "/")
                xpos = pos("/aon/"=dataDir$,-1)
                if xpos then
                    aonDir$ = dataDir$(1,xpos+4)
                endif
                if aonDir$="" then
                    xpos = pos("/data/"=dataDir$,-1)
                    if xpos then
                        aonDir$ = dataDir$(1,xpos)
                    else
                        aonDir$ = dataDir$
                    endif
                endif

                rem --- Create logs directory
                logDir$ = aonDir$ + "/logs"
                FileObject.makeDirs(new File(logDir$))

                rem --- Open/create push log file
                log$ = logDir$+"/GmInterfaceClient_pushes.log"
                string log$,err=*next
                log_dev = unt
                open(log_dev,err=*next)log$
                #devPushLog% = int(log_dev)
                
                rem --- Locate end of push log file so new messages are appended
                while 1
                    readrecord(#devPushLog%,siz=#BLOCKSIZE,end=*break)
                wend
            endif

            rem --- Lock push log to ensure only one client instance is talking to web service
            lock_successful = 0
            lock(#devPushLog%,err=*next); lock_successful = 1
                
            rem --- Start endless loop if have exclusive access to push log
            if lock_successful then
                while 1
                    #pushCustomers()
                    wait #waitTime
                wend
            endif
            
            methodret
            
launchClient_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Pushes Addon customers from table GMQ_CUSTOMER to GoldMine database using BBj web service.
        rem  *
        rem  */
        method private void pushCustomers()
            seterr pushCustomers_error

            rem --- Make sure the GoldMine XML interface API is loaded and GMQ_CUSTOMER open
            #gmLoadAPI()            
            gmqCustomerDev = #openGmqCustomer()

            if gmSessionId% >= 0 and gmqCustomerDev > 0 then
                rem --- Make sure GMX_CUSTOMER is open
                gmxCustomerDev = #openGmxCustomer()
                if gmxCustomerDev > 0 then
                    dim gmqCustomerTpl$:#tplGmqCustomer$

                    rem --- Send GMQ_CUSTOMER records to BBj web service on GoldMine server
                    read(gmqCustomerDev,key="",dom=*next)
                    while 1
                        gmqCustomerKey$ = key(gmqCustomerDev,end=*break)
                        readrecord(gmqCustomerDev)gmqCustomerTpl$
                        
                        rem --- Map non-blank fields to GoldMine fields
                        aonData! = new Properties()
                        rem --- Skip firm_id
                        rem --- Skip customer_id
                        if cvs(gmqCustomerTpl.gm_accountno$,2) <> "" then
                            aonData!.setProperty("GM_ACCOUNTNO",gmqCustomerTpl.gm_accountno$)
                        endif
                        if cvs(gmqCustomerTpl.gm_recid$,2) <> "" then
                            aonData!.setProperty("GM_RECID",gmqCustomerTpl.gm_recid$)
                        endif
                        if cvs(gmqCustomerTpl.customer_name$,2) <> "" then
                            gmProps! = #mapToGoldMine("customer_name",gmqCustomerTpl.customer_name$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                            aonData!.setProperty(gmProps!.getProperty("field2"),gmProps!.getProperty("value2"))
                        endif
                        if cvs(gmqCustomerTpl.contact_name$,2) <> "" then
                            gmProps! = #mapToGoldMine("contact_name",gmqCustomerTpl.contact_name$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                            aonData!.setProperty(gmProps!.getProperty("field2"),gmProps!.getProperty("value2"))
                        endif
                        if cvs(gmqCustomerTpl.phone_no$,2) <> "" then
                            gmProps! = #mapToGoldMine("phone_no",gmqCustomerTpl.phone_no$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.phone_exten$,2) <> "" then
                            gmProps! = #mapToGoldMine("phone_exten",gmqCustomerTpl.phone_exten$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.fax_no$,2) <> "" then
                            gmProps! = #mapToGoldMine("fax_no",gmqCustomerTpl.fax_no$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.addr_line_1$,2) <> "" then
                            gmProps! = #mapToGoldMine("addr_line_1",gmqCustomerTpl.addr_line_1$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.addr_line_2$,2) <> "" then
                            gmProps! = #mapToGoldMine("addr_line_2",gmqCustomerTpl.addr_line_2$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.addr_line_3$,2) <> "" then
                            gmProps! = #mapToGoldMine("addr_line_3",gmqCustomerTpl.addr_line_3$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.city$,2) <> "" then
                            gmProps! = #mapToGoldMine("city",gmqCustomerTpl.city$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                            aonData!.setProperty(gmProps!.getProperty("field2"),gmProps!.getProperty("value2"))
                        endif
                        if cvs(gmqCustomerTpl.state_code$,2) <> "" then
                            gmProps! = #mapToGoldMine("state_code",gmqCustomerTpl.state_code$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                            aonData!.setProperty(gmProps!.getProperty("field2"),gmProps!.getProperty("value2"))
                        endif
                        if cvs(gmqCustomerTpl.zip_code$,2) <> "" then
                            gmProps! = #mapToGoldMine("zip_code",gmqCustomerTpl.zip_code$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                        endif
                        if cvs(gmqCustomerTpl.cntry_id$,2) <> "" or cvs(gmqCustomerTpl.country$,2) <> "" then
                            gmProps! = #mapToGoldMineCountry(gmqCustomerTpl.country$,gmqCustomerTpl.cntry_id$)
                            aonData!.setProperty(gmProps!.getProperty("field1"),gmProps!.getProperty("value1"))
                            aonData!.setProperty(gmProps!.getProperty("field2"),gmProps!.getProperty("value2"))
                        endif

                        rem --- Write contact data to GoldMine database
                        gmData! = #gmWriteContact(aonData!)
                        if gmData!.containsKey("statusCode") and cvs(gmData!.getProperty("statusCode"),3)="1" then
                            rem --- On success, update GMX_CUSTOMER Addon-GoldMine cross reference file
                            dim gmxCustomerTpl$:#tplGmxCustomer$
                            gmxCustomerTpl.firm_id$ = gmqCustomerTpl.firm_id$
                            gmxCustomerTpl.customer_id$ = gmqCustomerTpl.customer_id$
                            if gmData!.containsKey("AccountNo") then
                                gmxCustomerTpl.gm_accountno$ = gmData!.getProperty("AccountNo")
                            endif
                            if gmData!.containsKey("RecID") then
                                gmxCustomerTpl.gm_recid$ = gmData!.getProperty("RecID")
                            endif
                            writerecord(gmxCustomerDev,dom=*next)gmxCustomerTpl$
                            
                            rem --- On success, remove GMQ_CUSTOMER record
                            remove(gmqCustomerDev,key=gmqCustomerKey$,dom=*next)

                            rem --- On success, log GMQ_CUSTOMER records written
                            msg1$ = gmxCustomerTpl.firm_id$+"::"+gmxCustomerTpl.customer_id$
                            msg2$ = gmxCustomerTpl.gm_accountno$+"::"+gmxCustomerTpl.gm_recid$
                            #logPush(msg1$,msg2$)
                        else
                            rem --- Skip to next GMQ_CUSTOMER record
                        endif
                    wend
                endif
            endif

            methodret
            
pushCustomers_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Load the GoldMine XML interface API.
        rem  *
        rem  */
        method private void gmLoadAPI()
            seterr gmLoadAPI_error

            rem --- Load API as necessary
            if #gmSessionId% < 0 then
                aonData! = new Properties()
                aonData!.setProperty("User",#gmMasterUser$)
                aonData!.setProperty("Password",#gmMasterPw$)
rem wgh ... ... SysDir =
rem wgh ... ... GoldDir =
rem wgh ... ... ComDir =
                aonData!.setProperty("SQLUser",#sqlUser$)
                aonData!.setProperty("SQLPassword",#sqlPassword$)
                
                xmlRequest$ = #buildXMLRequest("LoadAPI", aonData!)
                xmlResponse$ = #postRequest(xmlRequest$)
                props!=#parseXMLResponse(xmlResponse$)

                rem --- Log error
                if props!.containsKey("statusCode") and cvs(props!.getProperty("statusCode"),3)<>"1" then
                    #logError("gmLoadAPI(): statusCode="+props!.getProperty("statusCode"),props!.getProperty("statusText"))
                endif
            endif

            methodret
            
gmLoadAPI_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Write contact data via GoldMine XML interface API.
        rem  *
        rem  * @param Properties aonData!
        rem  *
        rem  * @return Properties gmData!
        rem  */
        method private Properties gmWriteContact(Properties aonData!)
            seterr gmWriteContact_error

            xmlRequest$ = #buildXMLRequest("WriteContact", aonData!)
            xmlResponse$ = #postRequest(xmlRequest$)
            gmData!=#parseXMLResponse(xmlResponse$)

            rem --- Log error
            if gmData!.containsKey("statusCode") and cvs(gmData!.getProperty("statusCode"),3)<>"1" then
                #logError("gmWriteContact(): statusCode="+gmData!.getProperty("statusCode"),gmData!.getProperty("statusText"))
            endif

            methodret gmData!
            
gmWriteContact_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Logout of the GoldMine XML interface API.
        rem  *
        rem  */
        method private void gmLogout()
            seterr gmLogout_error

            aonData! = new Properties()
            xmlRequest$ = #buildXMLRequest("Logout", aonData!)
            xmlResponse$ = #postRequest(xmlRequest$)
            props!=#parseXMLResponse(xmlResponse$)

            rem --- Log error
            if props!.containsKey("statusCode") and cvs(props!.getProperty("statusCode"),3)<>"1" then
                #logError("gmLogout(): statusCode="+props!.getProperty("statusCode"),props!.getProperty("statusText"))
            endif

            methodret
            
gmLogout_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Unload the GoldMine XML interface API.
        rem  *
        rem  */
        method private void gmUnloadAPI()
            seterr gmUnloadAPI_error

            aonData! = new Properties()
            xmlRequest$ = #buildXMLRequest("UnloadAPI", aonData!)
            xmlResponse$ = #postRequest(xmlRequest$)
            props!=#parseXMLResponse(xmlResponse$)

            rem --- Log error
            if props!.containsKey("statusCode") and cvs(props!.getProperty("statusCode"),3)<>"1" then
                #logError("gmUnloadAPI(): statusCode="+props!.getProperty("statusCode"),props!.getProperty("statusText"))
            endif

            methodret
            
gmUnloadAPI_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Build XML request to post to the GoldMine XML interface API web service.
        rem  *
        rem  * @param BBjString apiFunction$
        rem  * @param Properties aonData!
        rem  *
        rem  * @return BBjString xmlString$
        rem  */
        method private BBjString buildXMLRequest(BBjString apiFunction$,Properties aonData!)
            seterr buildXMLRequest_error
            xmlString$ = ""

            rem --- Instantiate document
            docFactory! = DocumentBuilderFactory.newInstance()
            docBuilder! = docFactory!.newDocumentBuilder()
            doc! = docBuilder!.newDocument()


            rem --- Add GMAPI element
            gmapi! = doc!.createElement("GMAPI")
            doc!.appendChild(gmapi!)

            rem --- Set attribute(s) to GMAPI element
            attr! = doc!.createAttribute("call")
            attr!.setValue(apiFunction$)
            gmapi!.setAttributeNode(attr!)
            if apiFunction$ <> "LoadAPI" then
                attr! = doc!.createAttribute("SessionID")
                attr!.setValue(str(#gmSessionId%))
                gmapi!.setAttributeNode(attr!)
            endif


            rem --- Loop over aonData properties
            properties! = aonData!.propertyNames() 
            while properties!.hasMoreElements()
                propertyName$ = properties!.nextElement()
                propertyValue$ = aonData!.getProperty(propertyName$)

                rem --- Add data element to GMAPI
                data! = doc!.createElement("data")
                gmapi!.appendChild(data!)
 
                rem --- Set attribute and text to data element
                attr! = doc!.createAttribute("name")
                attr!.setValue(propertyName$)
                data!.setAttributeNode(attr!)
                data!.appendChild(doc!.createTextNode(propertyValue$))
            wend

            rem --- Transform XML Document object into XML string
            transformerFactory! = TransformerFactory.newInstance()
            transformer! = transformerFactory!.newTransformer()
            transformer!.setOutputProperty(OutputKeys.OMIT_XML_DECLARATION, "yes")
            transformer!.setOutputProperty(OutputKeys.INDENT, "yes")
            sw! = new StringWriter()
            result! = new StreamResult(sw!)
            source! = new DOMSource(doc!)
            transformer!.transform(source!, result!)
            xmlString$ = sw!.toString()

            methodret xmlString$
            
buildXMLRequest_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * POST XML request to the GoldMine XML interface API web service.
        rem  *
        rem  * @param BBjString xmlRequest$
        rem  *
        rem  * @return BBjString xmlResponse$
        rem  */
        method private BBjString postRequest(BBjString xmlRequest$)
            seterr postRequest_error
            xmlResponse$ = ""

            rem --- Create instance of HttpClient
            client! = new HttpClient()

            rem --- Create instance of PostMethod
            method! = new PostMethod(#webServiceURL$)
            method!.addParameter("XML", xmlRequest$)

            rem --- Tell HttpClient to execute the PostMethod
            sc% = client!.executeMethod(method!)
            if sc% <> HttpStatus.SC_OK then
                #logError("postRequest(): "+method!.getStatusText(),xmlRequest$)
                xmlResponse$ = ""
            else
                rem --- Read response
                xmlResponse$ = method!.getResponseBodyAsString()
            endif

            rem --- Release connection
            method!.releaseConnection()

            methodret xmlResponse$
            
postRequest_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Parse XML response from the GoldMine XML interface API web service.
        rem  *
        rem  * @param BBjString xmlResponse$
        rem  *
        rem  * @return Properties props!
        rem  */
        method private Properties parseXMLResponse(BBjString xmlResponse$)
            seterr parseXMLResponse_error
            props! = new Properties()

            if cvs(xmlResponse$,2) <> "" then
                rem --- Transform XML string into XML Document object
                docFactory! = DocumentBuilderFactory.newInstance()
                docBuilder! = docFactory!.newDocumentBuilder()

                is! = new InputSource()
                is!.setCharacterStream(new StringReader(xmlResponse$))

                doc! = null()
                doc! = docBuilder!.parse(is!,err=*next)
                if doc! <> null() then
                    rem --- Get GMAPI element attribute(s)
                    gmapi! = doc!.getDocumentElement()
                    sessionId$ = gmapi!.getAttribute("SessionID")
                    function$ = gmapi!.getAttribute("call")
                    if function$ = "LoadAPI" then
                        #gmSessionId% = int(num(sessionID$))
                    endif
    
                    rem --- Verify GoldMine session ID
                    if int(num(sessionId$)) <> #gmSessionId% then
                        props!.setProperty("statusCode","-100")
                        props!.setProperty("statusText","Wrong session Id: "+sessionId$)
                    else
    
                        rem --- Get status element attribute and text
                        statusList! = doc!.getElementsByTagName("status")
                        if statusList!.getLength() > 0 then
                            status! = cast(Element,statusList!.item(0))
                            props!.setProperty("statusCode",status!.getAttribute("code"))
                            props!.setProperty("statusText",status!.getTextContent())
                        else
                            props!.setProperty("statusCode","")
                            props!.setProperty("statusText","Status code missing.")
                        endif
    
                        rem --- Loop over data elements
                        dataList! = doc!.getElementsByTagName("data")
                        if dataList!.getLength() > 0 then
                            for i=0 to dataList!.getLength()-1
                                rem --- Get data element attribute and text
                                data! = cast(Element,dataList!.item(i))
                                props!.setProperty(data!.getAttribute("name"),data!.getTextContent())
                            next i
                        endif
                    endif
                else
                    rem --- Log error
                    #logError("parseXMLResponse(): "+errmes(-1),xmlResponse$)
                endif
            endif

            methodret props!
            
parseXMLResponse_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Open gmq_customer. Do NOT create it if it doesn't exist.
        rem  *
        rem  * @return BBjInt openChannel%
        rem  */
        method private BBjInt openGmqCustomer()
            seterr openGmqCustomer_error
            openChannel% = #devGmqCustomer%

            if openChannel% < 0 then
                rem --- Open gmq_customer.
                filename$ = "gmq_customer"
                fileChan = unt
                success = 0
                open(fileChan,err=*next)stbl("+GMDATA")+filename$; success = 1
                if success then
                    rem --- Get record template for gmq_customer
                    tplsChan = unt
                    success = 0
                    open(tplsChan,err=*next)stbl("+DIR_BRD")+"ddm_table_tpls.dat"; success = 1
                    if success then
                        read(tplsChan,key=cvs(pad(filename$,16," "),4),dom=*next)*,*,table_tpl$
                        if table_tpl$<>"" then
                            rem --- Hold on to channel and record template for gmq_customer
                            openChannel% = fileChan
                            #devGmqCustomer% = openChannel%
                            #tplGmqCustomer$ = table_tpl$
                        else
                            #logError("openGmqCustomer(): Failed to get gmq_customer record template","Error "+str(err)+": "+errmes(-1))
                        endif
                    endif
                else
                    rem --- Log error
                    #logError("openGmqCustomer(): Failed to open gmq_customer","Error "+str(err)+": "+errmes(-1))
                endif
            endif

            methodret openChannel%
            
openGmqCustomer_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Open gmx_customer. Need to create it if it doesn't exist.
        rem  *
        rem  * @return BBjInt openChannel%
        rem  */
        method private BBjInt openGmxCustomer()
            seterr openGmxCustomer_error
            openChannel% = #devGmxCustomer%

            if openChannel% < 0 then
                rem --- Open gmx_customer and get record template. Create file if necessary.
                num_files=1
                dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
                open_tables$[1]="GMX_CUSTOMER",open_opts$[1]="OTA"

                call stbl("+DIR_SYP")+"bac_open_tables.bbj",open_beg,open_end,open_tables$[all],open_opts$[all],open_chans$[all],open_tpls$[all],rd_table_chans$[all],open_batch,open_status$

                if open_status$ = "" then
                    rem --- Hold on to channel and record template for gmx_customer
                    openChannel% = num(open_chans$[1])
                    #devGmxCustomer% = num(open_chans$[1])
                    #tplGmxCustomer$ = open_tpls$[1]
                else
                    rem --- Log error
                    #logError("openGmxCustomer(): Failed to open gmx_customer",open_status$)
                endif
            endif

            methodret openChannel%
            
openGmxCustomer_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Returns GoldMine field and value mapped to Addon field.
        rem  *
        rem  * @param BBjString aonField$
        rem  * @param BBjString aonValue$
        rem  *
        rem  * @return Properties gmProps!
        rem  */
        method public Properties mapToGoldMine(BBjString aonField$, BBjString aonValue$)
            seterr mapToGoldMine_error
            gmProps! = new Properties()

            switch (BBjAPI().TRUE)
                case cvs(aonField$,10) = "customer_name"
                    gmProps!.setProperty("field1","COMPANY")
                    gmProps!.setProperty("value1",pad(aonValue$,40))
                    gmProps!.setProperty("field2","U_COMPANY")
                    gmProps!.setProperty("value2",pad(cvs(aonValue$,4),40))
                    break
                case cvs(aonField$,10) = "contact_name"
                    gmProps!.setProperty("field1","CONTACT")
                    gmProps!.setProperty("value1",pad(aonValue$,40))
                    gmProps!.setProperty("field2","U_CONTACT")
                    gmProps!.setProperty("value2",pad(cvs(aonValue$,4),40))
                    break
                case cvs(aonField$,10) = "phone_no"
                    gmProps!.setProperty("field1","PHONE1")
                    call stbl("+DIR_SYP")+"bac_getmask.bbj","T",cvs(aonValue$,2),"",phone_mask$
                    phone$=cvs(aonValue$,2)
                    phone$=str(phone$:phone_mask$,err=*next)
                    gmProps!.setProperty("value1",pad(phone$,25))
                    break
                case cvs(aonField$,10) = "phone_exten"
                    gmProps!.setProperty("field1","EXT1")
                    gmProps!.setProperty("value1",pad(aonValue$,6))
                    break
                case cvs(aonField$,10) = "fax_no"
                    gmProps!.setProperty("field1","FAX")
                    call stbl("+DIR_SYP")+"bac_getmask.bbj","T",cvs(aonValue$,2),"",phone_mask$
                    fax$=cvs(aonValue$,2)
                    fax$=str(fax$:phone_mask$,err=*next)
                    gmProps!.setProperty("value1",pad(fax$,25))
                    break
                case cvs(aonField$,10) = "addr_line_1"
                    gmProps!.setProperty("field1","ADDRESS1")
                    gmProps!.setProperty("value1",pad(aonValue$,40))
                    break
                case cvs(aonField$,10) = "addr_line_2"
                    gmProps!.setProperty("field1","ADDRESS2")
                    gmProps!.setProperty("value1",pad(aonValue$,40))
                    break
                case cvs(aonField$,10) = "addr_line_3"
                    gmProps!.setProperty("field1","ADDRESS3")
                    gmProps!.setProperty("value1",pad(aonValue$,40))
                    break
                case cvs(aonField$,10) = "city"
                    gmProps!.setProperty("field1","CITY")
                    gmProps!.setProperty("value1",pad(aonValue$,30))
                    gmProps!.setProperty("field2","U_CITY")
                    gmProps!.setProperty("value2",pad(cvs(aonValue$,4),30))
                    break
                case cvs(aonField$,10) = "state_code"
                    gmProps!.setProperty("field1","STATE")
                    gmProps!.setProperty("value1",pad(aonValue$,20))
                    gmProps!.setProperty("field2","U_STATE")
                    gmProps!.setProperty("value2",pad(cvs(aonValue$,4),20))
                    break
                case cvs(aonField$,10) = "zip_code"
                    gmProps!.setProperty("field1","ZIP")
                    call stbl("+DIR_SYP")+"bac_getmask.bbj","P",cvs(aonValue$,2),"",postal_mask$
                    postal$=cvs(aonValue$,2)
                    postal$=str(postal$:postal_mask$,err=*next)
                    gmProps!.setProperty("value1",pad(postal$,10))
                    break
                case default
                    rem --- No mapping available/needed
                    gmProps!.setProperty("field1",aonField$)
                    gmProps!.setProperty("value2",aonValue$)
                    break
            swend

            methodret gmProps!
            
mapToGoldMine_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Returns GoldMine COUNTRY field and value mapped to Addon country and cntry_id fields.
        rem  * Uses Addon's cntry_id if not blank, else uses Addon's country.
        rem  *
        rem  * @param BBjString aonCountry$
        rem  * @param BBjString aonCntry_id$
        rem  *
        rem  * @return Properties gmProps!
        rem  */
        method public Properties mapToGoldMineCountry(BBjString aonCountry$, BBjString aonCntry_id$)
            seterr mapToGoldMineCountry_error
            gmProps! = new Properties()

            if cvs(aonCntry_id$,2) <> "" then
                gmProps!.setProperty("field1","COUNTRY")
                gmProps!.setProperty("value1",pad(aonCntry_id$,20))
                gmProps!.setProperty("field2","U_COUNTRY")
                gmProps!.setProperty("value2",pad(cvs(aonCntry_id$,4),20))
            else
                gmProps!.setProperty("field1","COUNTRY")
                gmProps!.setProperty("value1",pad(aonCountry$,20))
                gmProps!.setProperty("field2","U_COUNTRY")
                gmProps!.setProperty("value2",pad(cvs(aonCountry$,4),20))
            endif

            methodret gmProps!
            
mapToGoldMineCountry_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Starts EM scheduled task for running the interface client to BBj web service on GoldMine server.
        rem  * Allows starting the GoldMine interface with a single client (not multi-threaded).
        rem  *
        rem  */
        method public void startClient()
            seterr startClient_error

rem wgh ... per Jeff
rem wgh ... From BBjAdminBase you can call getTaskQueue() to get started:
rem wgh ... tq! = api!.getTaskQueue()
rem wgh ... tg! = tq!.getTaskGroup("MyGroup")
rem wgh ... tasks! = tg!.getTasks()
rem wgh ... Then you can iterate over the tasks and call the execute() method on each task.  
            
            methodret
            
startClient_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Log successful pushes of Addon customers to GoldMine database.
        rem  *
        rem  * @param BBjString msg1$
        rem  * @param BBjString msg2$
        rem  */
        method private void logPush(BBjString msg1$,BBjString msg2$)
            seterr logPush_error

            rem --- Write messages to push log
            write(#devPushLog%)"["+DATE(0:"%Yd%Mz%Dz")+"  "+DATE(0:"%Hz%mz")+"]"
            write(#devPushLog%)msg1$
            if cvs(msg2$,2) <> "" then write(#devPushLog%)msg2$

            methodret
            
logPush_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Write 2-line error messages to log file.
        rem  *
        rem  * @param BBjString errMsg1$
        rem  * @param BBjString errMsg2$
        rem  */
        method private void logError(BBjString errMsg1$,BBjString errMsg2$)
            seterr logError_error

            rem --- As needed, open error log file
            if #devErrLog% < 0 then 
                rem --- Open error log file in aon/logs directory, if possible
                aonDir$ = ""
                dataDir$ = FileObject.fixPath(stbl("+DIR_DAT",err=*next), "/")
                xpos = pos("/aon/"=dataDir$,-1)
                if xpos then
                    aonDir$ = dataDir$(1,xpos+4)
                endif
                if aonDir$="" then
                    xpos = pos("/data/"=dataDir$,-1)
                    if xpos then
                        aonDir$ = dataDir$(1,xpos)
                    else
                        aonDir$ = dataDir$
                    endif
                endif

                rem --- Create logs directory
                logDir$ = aonDir$ + "/logs"
                FileObject.makeDirs(new File(logDir$))

                rem --- Open/create error log file
                log$ = logDir$+"/GmInterfaceClient_errors_"+DATE(0:"%Yd%Mz%Dz")+".log"
                string log$,err=*next
                log_dev = unt
                open(log_dev,err=*next)log$
                #devErrLog% = int(log_dev)
                
                rem --- Locate end of error log file so new messages are appended
                while 1
                    readrecord(#devErrLog%,siz=#BLOCKSIZE,end=*break)
                wend
            endif

            rem --- Write error messages to error log
            write(#devErrLog%)"["+DATE(0:"%Yd%Mz%Dz")+"  "+DATE(0:"%Hz%mz")+"]"
            write(#devErrLog%)errMsg1$
            if cvs(errMsg2$,2) <> "" then write(#devErrLog%)errMsg2$

            methodret
            
logError_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Cleanup on object's destruction and garbage collection.
        rem  *
        rem  */
        method protected void finalize()
            seterr finalize_error

            if #gmSessionId% >= 0 then
                #gmLogout()
                #gmUnloadAPI()
            endif
            if devErrLog% >= 0 then close(devErrLog%,err=*next)
            if devGmqCustomer% >= 0 then close(devGmqCustomer%,err=*next)
            if devGmxCustomer% >= 0 then close(devGmxCustomer%,err=*next)
            if devSQL% >= 0 then close(devSQL%,err=*next)

            methodret
            
finalize_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

    classend
