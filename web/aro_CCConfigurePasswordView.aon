REM /**
REM  * aro_CCConfigurePasswordView.aon
REM  * @author shaun
REM  * Copyright BASIS International Ltd.  All Rights Reserved.
REM  */

use ::CCView.aon::CCView

REM Test program to show window.
declare CCConfigurePasswordView cpv!
cpv!=new CCConfigurePasswordView()
cpv!.show()
process_events

class public CCConfigurePasswordView extends CCView
    field protected BBjStaticText PasswordST!
    field protected BBjStaticText Password2ST!
    field protected BBjEditBox PasswordEB!
    field protected BBjEditBox Password2EB!   
    field protected BBjButton OKBtn!
    field protected BBjButton CancelBtn!
    field protected BBjStaticText header1!
    field protected BBjStaticText header2!
	field protected BBjNumber SqlChan
	
	method public CCConfigurePasswordView()
		#super!()
		#getWin().setTitle("Change Password")
		#SqlChan=sqlunt
		sqlopen (#SqlChan) stbl("+DBNAME")
		
		sqlprep(#SqlChan) "select FIRM_NAME from ADM_FIRMS where COMPANY_ID = '" + stbl("+FIRM_ID") + "'"
		sqlexec(#SqlChan)
		dim result$:sqltmpl(#SqlChan)
		result$=sqlfetch(#SqlChan)

		#header1!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,25,640,60,result$)
		#header2!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,85,640,50,"Customer Portal - Change Password")

		REM This is your new password + new password again dialog.
		#PasswordST!=#getWin().addStaticText(#getWin().getAvailableControlID(),5,172,150,24,"New Password",$8000$)
		#Password2ST!=#getWin().addStaticText(#getWin().getAvailableControlID(),5,222,150,24,"Repeat Password",$8000$)
		#PasswordEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),160,165,350,34,"",$0400$)
		#Password2EB!=#getWin().addEditBox(#getWin().getAvailableControlID(),160,215,350,34,"",$0400$)
		#OKBtn!=#getWin().addButton(#getWin().getAvailableControlID(),225,263,110,50,"OK")
		#CancelBtn!=#getWin().addButton(#getWin().getAvailableControlID(),340,263,110,50,"Cancel")
		
		#header1!.addStyle("cc-header1")
		#header2!.addStyle("cc-header2")
		#PasswordST!.addStyle("cc-statictext")
		#Password2ST!.addStyle("cc-statictext")
		#PasswordEB!.addStyle("cc-editbox")
		#Password2EB!.addStyle("cc-editbox")
		#OKBtn!.addStyle("cc-button")
		#CancelBtn!.addStyle("cc-button")
		
	methodend
    
    method public BBjString getPassword()
        methodret #PasswordEB!.getText()
    methodend 
    
    method public BBjNumber passwordsMatch()
		if cvs(#PasswordEB!.getText(),3)="" or cvs(#Password2EB!.getText(),3)="" methodret 0
        methodret #PasswordEB!.getText()=#Password2EB!.getText() 
    methodend 
    
    method public void registerOKBtn_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
        #OKBtn!.setCallback(BBjEditBox.ON_BUTTON_PUSH,handler!,callback!)
    methodend 
    
    method public void registerCancelBtn_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
        #CancelBtn!.setCallback(BBjEditBox.ON_BUTTON_PUSH,handler!,callback!)
    methodend 


classend 