REM /**
REM  * aro_CCConfigurePasswordView.aon
REM  * @author shaun
REM  *
REM  */
use ::CCView.aon::CCView

REM Test program to show window. 
declare CCForgotPasswordView cfpv!
cfpv!=new CCForgotPasswordView()
cfpv!.show()
process_events 

class public CCForgotPasswordView extends CCView

    REM This dialog just has the user provide their email address
    field protected BBjStaticText header1!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,25,640,60,"")
    field protected BBjStaticText header2!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,85,640,50,"Customer Portal")
    field protected BBjStaticText EmailST!=#getWin().addStaticText(#getWin().getAvailableControlID(),50,172,120,26,"Email Address",$8000$)
    field protected BBjEditBox EmailEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),180,165,400,34,"")
    
    field protected BBjButton OKBtn!=#getWin().addButton(#getWin().getAvailableControlID(),205,213,110,50,"OK")
    field protected BBjButton CancelBtn!=#getWin().addButton(#getWin().getAvailableControlID(),325,213,110,50,"Cancel")
    
    method public CCForgotPasswordView() 
        #getWin().setTitle("Request Password Reset")
        #header1!.addStyle("cc-header1")
        #header2!.addStyle("cc-header2")
        #EmailST!.addStyle("cc-statictext")
        #EmailEB!.addStyle("cc-editbox")
        #OKBtn!.addStyle("cc-button")
        #CancelBtn!.addStyle("cc-button")
    methodend 
    
    method public void setHeader1Text(BBjString headerText!)
       #header1!.setText(headerText!)
    methodend 
    
    method public void setHeader2Text(BBjString headerText!)
       #header2!.setText(headerText!)
    methodend 
    
    method public BBjString getEmail()
        methodret #EmailEB!.getText()
    methodend 
        
    method public void registerOKBtn_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
        #OKBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
    methodend 
    
    method public void registerCancelBtn_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
        #CancelBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
    methodend 

classend 