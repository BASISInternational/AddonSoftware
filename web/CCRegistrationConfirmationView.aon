REM /**
REM  * CCRegistrationConfirmationView.aon
REM  * @author shaun
REM  * Copyright BASIS International Ltd.  All Rights Reserved.
REM  */

use ::CCView.aon::CCView

class public CCRegistrationConfirmationView extends CCView

	field protected BBjStaticText Header1!
	field protected BBjStaticText Header2!
	field protected BBjStaticText DesiredUserNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,172,190,26,"User Name",$8000$)
	field protected BBjStaticText FirstNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,222,190,26,"First Name",$8000$)
	field protected BBjStaticText LastNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,272,190,26,"Last Name",$8000$)
	field protected BBjStaticText UserEmailST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,322,190,26,"User Email",$8000$)
	field protected BBjStaticText CustomerIDST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,372,190,26,"Customer ID",$8000$)
	field protected BBjNumber SqlChan
	
	field public BBjEditBox DesiredUserNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,165,400,34,"",$0200$)
	field public BBjEditBox FirstNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,215,400,34,"",$0200$)
	field public BBjEditBox LastNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,265,400,34,"",$0200$)
	field public BBjEditBox UserEmailEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,315,400,34,"",$0200$)
	field public BBjEditBox CustomerIDEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,365,150,34,"",$0200$)
	field protected BBjStaticText CustomerNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),380,372,400,34,"",$2000$)
	
	field public BBjButton RegisterBtn!=#getWin().addButton(#getWin().getAvailableControlID(),300,420,110,50,"Confirm")
	field public BBjButton CancelBtn!=#getWin().addButton(#getWin().getAvailableControlID(),420,420,110,50,"Cancel")


	method public CCRegistrationConfirmationView()
		#super!()
		#getWin().setTitle("Customer Registration Confirmation")
		#getWin().setSize(#getWin().getWidth()+100,520)
		
		REM Belongs in model
		#SqlChan=sqlunt
		sqlopen (#SqlChan) stbl("+DBNAME")
		
		sqlprep(#SqlChan) "select FIRM_NAME from ADM_FIRMS where COMPANY_ID = '" + stbl("+FIRM_ID") + "'"
		sqlexec(#SqlChan)
		dim result$:sqltmpl(#SqlChan)
		result$=sqlfetch(#SqlChan)
		REM /Belongs in model 
		
		#Header1!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,25,720,60,result$)
		#Header2!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,85,720,50,"Customer Portal - Confirm Registration")

		#Header1!.addStyle("cc-header1")
		#Header2!.addStyle("cc-header2")
		#DesiredUserNameST!.addStyle("cc-statictext")
		#FirstNameST!.addStyle("cc-statictext")
		#LastNameST!.addStyle("cc-statictext")
		#UserEmailST!.addStyle("cc-statictext")
		#CustomerIDST!.addStyle("cc-statictext")
		#CustomerNameST!.addStyle("cc-statictext-left")
		#DesiredUserNameEB!.addStyle("cc-editbox")
		#FirstNameEB!.addStyle("cc-editbox")
		#LastNameEB!.addStyle("cc-editbox")
		#UserEmailEB!.addStyle("cc-editbox")
		#CustomerIDEB!.addStyle("cc-editbox")
		#RegisterBtn!.setName("cc-button")
		#CancelBtn!.setName("cc-button")

	methodend

	method public void registerYesButton_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
		#RegisterBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
	methodend

	method public void registerNoButton_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
		#CancelBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
	methodend

	method public void updateCustomerNameText(BBjString customerName$)
		#CustomerNameST!.setText(customerName$)
	methodend
	
	method public void showAcceptanceMessage()
		#displayMessage("User registered and account created.  Welcome to credit card payments!")
	methodend 
	
	method public void showRejectionMessage()
		#displayMessage("Registration cancelled. If this was not intentional, please have user re-register.")
    methodend 
classend
