REM /**
REM  * CCRegistrationView.aon
REM  * @author shaun
REM  * Copyright BASIS International Ltd.  All Rights Reserved.
REM  */

use ::CCView.aon::CCView
use ::CCRegistrationModel.aon::CCCustomerFirmInfo

class public CCRegistrationView extends CCView

	field protected BBjStaticText DesiredUserNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,172,190,26,"User Name",$8000$)
	field protected BBjStaticText FirstNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,222,190,26,"First Name",$8000$)
	field protected BBjStaticText LastNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,272,190,26,"Last Name",$8000$)
	field protected BBjStaticText UserEmailST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,322,190,26,"User Email",$8000$)
	field protected BBjStaticText CustomerIDST!=#getWin().addStaticText(#getWin().getAvailableControlID(),25,372,190,26,"Customer ID",$8000$)
	field protected BBjStaticText header1!
    field protected BBjStaticText header2!

	field protected BBjEditBox DesiredUserNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,165,400,34,"")
	field protected BBjEditBox FirstNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,215,400,34,"")
	field protected BBjEditBox LastNameEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,265,400,34,"")
	field protected BBjEditBox UserEmailEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,315,400,34,"") 
	field protected BBjEditBox CustomerIDEB!=#getWin().addEditBox(#getWin().getAvailableControlID(),225,365,150,34,"")
	field protected BBjStaticText CustomerNameST!=#getWin().addStaticText(#getWin().getAvailableControlID(),380,372,400,34,"")

	field protected BBjButton RegisterBtn!=#getWin().addButton(#getWin().getAvailableControlID(),300,420,110,50,"Register")
	field protected BBjButton CancelBtn!=#getWin().addButton(#getWin().getAvailableControlID(),420,420,110,50,"Cancel")

	field protected BBjNumber SqlChan

	method public CCRegistrationView()
	
		#super!()
		#getWin().setTitle("New Customer Registration")		
		REM Adjust the height of our window
		#getWin().setSize(#getWin().getWidth()+150,520)

		#SqlChan=sqlunt
		sqlopen (#SqlChan) stbl("+DBNAME")
		
		sqlprep(#SqlChan) "select FIRM_NAME from ADM_FIRMS where COMPANY_ID = '" + stbl("+FIRM_ID") + "'"
		sqlexec(#SqlChan)
		dim result$:sqltmpl(#SqlChan)
		result$=sqlfetch(#SqlChan)

		#header1!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,25,800,60,result$)
		#header2!=#getWin().addStaticText(#getWin().getAvailableControlID(),0,85,800,50,"Customer Portal - New User Registration")
		
		#header1!.addStyle("cc-header1")
		#header2!.addStyle("cc-header2")		 
		#DesiredUserNameST!.addStyle("cc-statictext")
		#FirstNameST!.addStyle("cc-statictext")
		#LastNameST!.addStyle("cc-statictext")
		#UserEmailST!.addStyle("cc-statictext")
		#CustomerIDST!.addStyle("cc-statictext")
		#CustomerNameST!.addStyle("cc-statictext-left")
		#DesiredUserNameEB!.addStyle("cc-editbox")
		#FirstNameEB!.addStyle("cc-editbox")
		#LastNameEB!.addStyle("cc-editbox")
		#UserEmailEB!.addStyle("cc-editbox")
		#CustomerIDEB!.addStyle("cc-editbox")
		#RegisterBtn!.addStyle("cc-button")
		#CancelBtn!.addStyle("cc-button")		

		#RegisterBtn!.setEnabled(0)

	methodend

	method public void registerDesiredUserNameEB_OnFocusLostCallback(CustomObject handler!, BBjString callback!)
	    #DesiredUserNameEB!.setCallback(BBjEditBox.ON_LOST_FOCUS,handler!,callback!)
	methodend 
	
	method public void registerFirstNameEB_OnFocusLostCallback(CustomObject handler!, BBjString callback!)
        #FirstNameEB!.setCallback(BBjEditBox.ON_LOST_FOCUS,handler!,callback!)
    methodend 
    
    method public void registerLastNameEB_OnFocusLostCallback(CustomObject handler!, BBjString callback!)
        #LastNameEB!.setCallback(BBjEditBox.ON_LOST_FOCUS,handler!,callback!)
    methodend 
    
    method public void registerUserEmailEB_OnFocusLostCallback(CustomObject handler!, BBjString callback!)
        #UserEmailEB!.setCallback(BBjEditBox.ON_LOST_FOCUS,handler!,callback!)
    methodend 

	method public void registerCustomerIDEB_OnFocusLostCallback(CustomObject handler!, BBjString callback!)
        #CustomerIDEB!.setCallback(BBjEditBox.ON_LOST_FOCUS,handler!,callback!)
    methodend

    method public void registerRegisterButton_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
		#RegisterBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
	methodend

	method public void registerCancelButton_OnButtonPushCallback(CustomObject handler!, BBjString callback!)
		#CancelBtn!.setCallback(BBjButton.ON_BUTTON_PUSH,handler!,callback!)
	methodend
	
	method public BBjString getUserName()
	    methodret #DesiredUserNameEB!.getText()
	methodend 
	
	method public BBjString getFirstName()
	    methodret #FirstNameEB!.getText() 
	methodend 
	
	method public BBjString getLastName()
	    methodret #LastNameEB!.getText()
	methodend 
	
	method public BBjString getCustomerID() 
	    methodret #CustomerIDEB!.getText()
	methodend 
	
	method public BBjString getUserEmail()
	    methodret #UserEmailEB!.getText()
	methodend 
	
	method public void updateCustomerNameText(BBjString customerName$)
		#CustomerNameST!.setText(customerName$)
	methodend

	method public void clearCustomerNameText()
		#CustomerNameST!.setText("")
	methodend
	
	method public void showRegistrationMessage() 
	    #displayMessage("A registration email has been sent to your company. Your account will be ready when the email is approved.")
	methodend  
	
	method public void setRegistrationEnabled(BBjNumber enabled)
	  #RegisterBtn!.setEnabled(enabled)
	methodend 
	
	method public BBjNumber allFieldsHaveValues()
	    methodret #notEmpty(#DesiredUserNameEB!) and #notEmpty(#CustomerNameST!) and #notEmpty(#FirstNameEB!) and #notEmpty(#LastNameEB!) and #notEmpty(#CustomerIDEB!) and #notEmpty(#UserEmailEB!)
	methodend 

	method protected BBjNumber notEmpty(BBjControl c!)
	    methodret c!.getText().trim().length()>0
	methodend 


classend
