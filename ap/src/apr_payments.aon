rem --- Payment Selection Report
rem --- Program apr_payments v8.0.0 28Jan2007 (apr_da)
rem --- Created by adx_codeport.bbx v1.1.5 (01/28/2007 01:24:45)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    use java.util.HashMap

    setesc std_error
    seterr std_error

rem --- Document initializations

    declare BBjVector OutVect!
    declare BBjVector HdrVect!
    declare BBjVector BrkVect!
    declare BBjVector TotVect!

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()
    
    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    rem --- If BUI, initialize Barista processes
    if info(3,6)="5" then
        use ::ado_util.src::util
        Translate!=util.initBUIProcess()
        if Translate!=null() then goto std_exit
    endif

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$

rem --- set rd_alias_id$, if needed, for DocOut Settings

    if rd_alias_id$=""
        rd_alias_id$=pgm(-2)
        rd_alias_id$=rd_alias_id$(max(pos("/"="/"+rd_alias_id$,-1),pos("\"="\"+rd_alias_id$,-1)))
        rd_alias_id$=rd_alias_id$(1,pos("."=rd_alias_id$+".")-1)
    endif
	rd_alias_id$=cvs(rd_alias_id$,4)

rem --- Open/Lock files

    files=9,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="aps_params",ids$[1]="APS_PARAMS";rem "aps-01
    files$[2]="apt-01",ids$[2]="APT_INVOICEHDR"
    files$[3]="ape-04",ids$[3]="APE_CHECKS";rem "ape-04
    files$[4]="ape-11",ids$[4]="APE_INVOICEDET";rem ape-11
    files$[5]="ape-16",ids$[5]="APE_SELECTIONDET";rem "ape-16
    files$[6]="apm-01",ids$[6]="APM_VENDMAST";rem "apm-01
    files$[7]="apc_typecode",ids$[7]="APC_TYPECODE";rem "apm-10A
    files$[8]="apc_distribution",ids$[8]="APC_DISTRIBUTION"
    files$[9]="adc_bankacctcode",ids$[9]="ADC_BANKACCTCODE"
    
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:       ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    
    aps01_dev=channels[1]
    apt01_dev=channels[2]
    ape04_dev=channels[3]
    ape11_dev=channels[4]
    ape16_dev=channels[5]
    apm01_dev=channels[6]
    apm10_dev=channels[7]
    apcDist_dev=channels[8]
    adcBnkAcct_dev=channels[9]

rem --- Dimension string templates

    dim aps01a$:templates$[1],apt01a$:templates$[2],ape04a$:templates$[3]
    dim ape11a$:templates$[4],ape16a$:templates$[5],apm01a$:templates$[6]
    dim apm10a$:templates$[7],apcDist$:templates$[8],adcBnkAcct$:templates$[9]

rem --- Retrieve parameter records

    aps01a_key$=firm_id$+"AP00"
    find record (aps01_dev,key=aps01a_key$,err=std_missing_params) aps01a$
    call stbl("+DIR_PGM")+"adc_getmask.aon","VENDOR_ID","","","",m0$,0,vendor_size
    call stbl("+DIR_PGM")+"adc_getmask.aon","","AP","A","",m1$,0,0
    multitypes=aps01a.multi_types$="Y"
    retention=aps01a.ret_flag$="Y"
    m0=len(m0$)
    m1=len(m1$)
    maxheadings=9
    
rem --- If using GL, open Bank Account Master file
    if aps01a.post_to_gl$="Y" then
        files=2,begfile=1,endfile=files
        dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
        files$[1]="glm-01",ids$[1]="GLM_ACCT"
        files$[2]="glm-05",ids$[2]="GLM_BANKMASTER"
    
        call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:           ids$[all],templates$[all],channels[all],batch,status
        if status goto std_exit
    
        glm01_dev=channels[1];dim glm01$:templates$[1]
        glm05_dev=channels[2];dim glm05$:templates$[2]

        call stbl("+DIR_PGM")+"adc_getmask.aon","GL_ACCOUNT","","","",gl_acct_mask$,0,gl_acct_length
    endif

rem --- Initializations

    dim headings$[maxheadings],underline$((2)*m1,"-")
    dim h[9],o[9],detail[3],totals[3],at[3],rt[3],vt[3],achtot[3],checktot[3]
    vm=max(m0,8)
    done=0
    page=0
    width=132
    maxlines=59
    lines=maxlines+1
    printed=0
    more=1
    headings=1
    negative_checks=0
    when$=sysinfo.system_date$
    clock$=""
    nf$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$
    aptype$=""
    vendor$=""
    name$=""
    thisvendor$=""
    thisname$=""
    if multitypes headings$[2]="[HEADER]"
    recs=0
    use_underline$="Y"
    acctInvMap!=new HashMap()
    badBankAccts!=BBjAPI().makeVector()
    missingBankRecs!=BBjAPI().makeVector()

rem --- Prompt user for permission to continue

    msg_id$="AP_CHECK_SELECT"
    dim msg_tokens$[1]
    gosub disp_message
    if msg_opt$="N" goto std_exit_no_report

rem --- Headings and print positions

    if retention
        dim columns$[10,10]
        columns = 10
    else
        dim columns$[9,10]
        columns = 9
    endif

    columns$[0,0]=Translate!.getTranslation("AON_VENDOR"),           columns$[0,1]="C",columns$[0,2]="6"
    columns$[1,0]=Translate!.getTranslation("AON_NAME"),             columns$[1,1]="C",columns$[1,2]="25"
    columns$[2,0]=Translate!.getTranslation("AON_ACH"),              columns$[2,1]="C",columns$[2,2]="4"
    columns$[3,0]=Translate!.getTranslation("AON_INVOICE"),          columns$[3,1]="C",columns$[3,2]="10"
    columns$[4,0]=Translate!.getTranslation("AON_INVOICE_DT"),       columns$[4,1]="C",columns$[4,2]="10"
    columns$[5,0]=Translate!.getTranslation("AON_DUE_DT"),           columns$[5,1]="C",columns$[5,2]="10"
    columns$[6,0]=Translate!.getTranslation("AON_DISCOUNT_DT"),      columns$[6,1]="C",columns$[6,2]="10"
    columns$[7,0]=Translate!.getTranslation("AON_INVOICE_AMT"),      columns$[7,1]="N",columns$[7,2]="10",columns$[7,3]=m1$,columns$[7,10]="T"
    columns$[8,0]=Translate!.getTranslation("AON_DISCOUNT_AMT"),     columns$[8,1]="N",columns$[8,2]="10",columns$[8,3]=m1$,columns$[8,10]="T"
    columns$[9,0]=Translate!.getTranslation("AON_PAYMENT_AMT"),      columns$[9,1]="N",columns$[9,2]="10",columns$[9,3]=m1$,columns$[9,10]="T"

    if retention
        columns$[10,0]=Translate!.getTranslation("AON_RETENTION_AMT"),   columns$[10,1]="N",columns$[10,2]="10",columns$[10,3]=m1$,columns$[10,10]="T"
    endif

    call stbl("+DIR_PGM",err=*next)+"adc_docout_adj.aon",columns$[all]

rem --- Define print positions

    o[9]=width-1
    retention_hdr1$=""
    retention_hdr2$="",n=3
    
    if retention
        o[9]=width-m1
        retention_hdr1$=Translate!.getTranslation("AON_RETENTION")
        retention_hdr2$=Translate!.getTranslation("AON____AMOUNT")
    endif
    
    o[8]=o[9]-m1
    o[7]=o[8]-m1
    o[6]=o[7]-m1
    o[5]=o[6]-10
    o[4]=o[5]-9
    o[3]=o[4]-9
    o[2]=o[3]-11
    o[0]=0
    o[1]=vm
    w=min(30,o[2]-o[1]-1)
    
    for x=0 to 9
        h[x]=o[x]
    next x
    
    h[6]=h[6]+m1-8
    h[7]=h[7]+m1-9
    h[8]=h[8]+m1-8
    if retention h[9]=h[9]+m1-10

rem --- Open printer, start the progress meter and position payment selections file

    call pgmdir$+"adc_progress.aon","NC","","","","",0,ape04_dev,1,meter_num,status
    read (ape16_dev,key=firm_id$,dom=*next)

rem --- Position the check detail file

    read (ape04_dev,key=firm_id$,dom=*next)

rem --- Read next Check Detail (ape-04) record

    while more

        k$=key(ape04_dev,end=*break)
        if pos(firm_id$=k$)<>1 break
        recs=recs+1
        read record (ape04_dev) ape04a$
        call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status

    rem --- Level breaks

        if ape04a.ap_type$<>lasttype$ and multitypes then gosub type_break
        if ape04a.vendor_id$<>lastvendor$ gosub vendor_break

    rem --- Format detail line fields

        retention$=""
        if retention retention$=str(ape04a.retention)

        OutVect!.addItem(vendor$)
        OutVect!.addItem(name$)
        OutVect!.addItem(ach$)
        OutVect!.addItem(ape04a.ap_inv_no$)
        OutVect!.addItem(fndate$(ape04a.invoice_date$))
        OutVect!.addItem(fndate$(ape04a.inv_due_date$))
        OutVect!.addItem(fndate$(ape04a.disc_date$))
        OutVect!.addItem(str(ape04a.invoice_amt))
        OutVect!.addItem(str(ape04a.discount_amt))
        OutVect!.addItem(str(ape04a.payment_amt)); rem payment

        if retention
            OutVect!.addItem(retention$)
        endif

        lines=lines+1
        printed=printed+1
        vendor$=""
        name$=""
        ach$=""

    rem --- Accumulate totals

        detail[0]=ape04a.invoice_amt
        detail[1]=ape04a.discount_amt
        detail[2]=ape04a.retention
		detail[3]=ape04a.payment_amt
        
        for x=0 to 3
            vt[x]=vt[x]+detail[x]
            at[x]=at[x]+detail[x]
            rt[x]=rt[x]+detail[x]
            if accum_ach$="Y"
                achtot[x]=achtot[x]+detail[x]
            else
                checktot[x]=checktot[x]+detail[x]
            endif
        next x

        rem --- Build HashMap holding Vector of all invoices being paid for each checking account
        if aps01a.post_to_gl$="Y" then
            redim apt01a$
            apt01_key$=firm_id$+ape04a.ap_type$+ape04a.vendor_id$+ape04a.ap_inv_no$
            readrecord(apt01_dev,key=apt01_key$,dom=*next)apt01a$
            if cvs(apt01a.ap_dist_code$,2)<>"" then
                redim apcDist$
                readrecord(apcDist_dev,key=firm_id$+"B"+apt01a.ap_dist_code$,dom=*next)apcDist$
                if cvs(apcDist.gl_cash_acct$,2)<>"" then
                    redim glm05$
                    readrecord(glm05_dev,key=firm_id$+apcDist.gl_cash_acct$,dom=*next)glm05$
                    if cvs(glm05.bnk_acct_cd$,2)<>"" then
                        rem --- Warn if Bank Account is not a checking account
                        redim adcBnkAcct$
                        readrecord(adcBnkAcct_dev,key=firm_id$+glm05.bnk_acct_cd$,dom=*next)adcBnkAcct$
                        if adcBnkAcct.bnk_acct_type$<>"C" then
                            badBankAccts!.addItem(adcBnkAcct.acct_desc$+" ("+glm05.bnk_acct_cd$+")")
                        endif

                        if acctInvMap!.containsKey(adcBnkAcct.acct_desc$+" ("+glm05.bnk_acct_cd$+")")
                            invVect!=acctInvMap!.get(adcBnkAcct.acct_desc$+" ("+glm05.bnk_acct_cd$+")")
                        else
                            invVect!=BBjAPI().makeVector()
                        endif
                        invVect!.addItem(ape04a.ap_inv_no$+" ("+fnmask$(ape04a.vendor_id$(1,vendor_size),m0$)+")")
                        acctInvMap!.put(adcBnkAcct.acct_desc$+" ("+glm05.bnk_acct_cd$+")",invVect!)
                    else
                        rem --- Warn if Cash Account isn't in Bank Reconciliation
                        redim glm01$
                        readrecord(glm01_dev,key=firm_id$+apcDist.gl_cash_acct$,dom=*next)glm01$
                        missingBankRecs!.addItem(fnmask$(apcDist.gl_cash_acct$(1,gl_acct_length),gl_acct_mask$)+" "+glm01$.gl_acct_desc$)
                    endif
                endif
            endif
        endif

        rem --- Loop back for next check detail record
    wend

rem --- All done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    done=1
    gosub type_break
  
    total$=Translate!.getTranslation("AON_ACH_PAYMENTS")
    for x=0 to 3
        totals[x]=achtot[x]
    next x
    use_underline$="N"
    gosub print_total


    total$=Translate!.getTranslation("AON_CHECK_PAYMENTS")    
    for x=0 to 3
        totals[x]=checktot[x]
    next x
    use_underline$="N"
    gosub print_total

    total$=Translate!.getTranslation("AON_TOTALS_FOR_REPORT")
    for x=0 to 3
        totals[x]=rt[x]
    next x
    use_underline$="Y"
    gosub print_total

    if negative_checks then
        xwk=fnblank(10)
        if retention OutVect!.addItem("")

        warning$=Translate!.getTranslation("AON_WARNING:_THIS_PAYMENT_SELECTION_REPORT_CONTAINS_NEGATIVE_CHECKS!")
        xwk = fnblank(1)
        OutVect!.addItem(warning$)
            if retention wblanks=9 else wblanks=8
            xwk=fnblank(wblanks)

        msg_id$="NEG_CHECKS_ON_RPT"
        gosub disp_message
    endif

    rem --- Warn multiple checking accounts (Bank Accounts) being used
    if acctInvMap!.size()>1 then
        xwk=fnblank(10)
        if retention OutVect!.addItem("")

        warning$=Translate!.getTranslation("AON_MULTI_CHK_ACCTS")
        OutVect!.addItem(warning$)
        if retention wblanks=10 else wblanks=9
        xwk=fnblank(wblanks)

        acctIter!=acctInvMap!.keySet().iterator()
        while acctIter!.hasNext()
            acct$=acctIter!.next()
            
            xwk = fnblank(1)
            OutVect!.addItem(acct$)
            if retention wblanks=9 else wblanks=8
            xwk=fnblank(wblanks)
            
            invVect!=acctInvMap!.get(acct$)
            for i=0 to invVect!.size()-1
                xwk = fnblank(1)
                OutVect!.addItem("        "+invVect!.getItem(i))
                if retention wblanks=9 else wblanks=8
                xwk=fnblank(wblanks)
            next i
        wend

        msg_id$="AP_MULTI_CHK_ACCTS"
        gosub disp_message
    endif

    rem --- Warn Cash Account isn't in Bank Reconciliation
    if missingBankRecs!.size()>0 then
        xwk=fnblank(10)
        if retention OutVect!.addItem("")

        warning$=Translate!.getTranslation("AON_NOT_BANK_REC")
        OutVect!.addItem(warning$)
        if retention wblanks=10 else wblanks=9
        xwk=fnblank(wblanks)

        call stbl("+DIR_PGM")+"adc_getmask.aon","GL_ACCOUNT","","","",gl_acct_mask$,0,gl_acct_length
        for i=0 to missingBankRecs!.size()-1
            bankRec$=missingBankRecs!.getItem(i)
        
            xwk = fnblank(1)
            OutVect!.addItem(bankRec$)
            if retention wblanks=9 else wblanks=8
            xwk=fnblank(wblanks)

            if i>0 then missingBankRecs$=missingBankRecs$+"; "
            missingBankRecs$=missingBankRecs$+bankRec$
        next i

        msg_id$="AP_NOT_BANK_REC"
        dim msg_tokens$[1]
        msg_tokens$[1]=missingBankRecs$
        gosub disp_message
    endif

    rem --- Warn Bank Account is not a checking account
    if badBankAccts!.size()>0 then
        xwk=fnblank(10)
        if retention OutVect!.addItem("")

        warning$=Translate!.getTranslation("AON_NOT_CHECK_ACCT")
        OutVect!.addItem(warning$)
        if retention wblanks=10 else wblanks=9
        xwk=fnblank(wblanks)

        for i=0 to badBankAccts!.size()-1
            xwk = fnblank(1)
            OutVect!.addItem(badBankAccts!.getItem(i))
            if retention wblanks=9 else wblanks=8
            xwk=fnblank(wblanks)

            if i>0 then badBankAccts$=badBankAccts$+"; "
            badBankAccts$=badBankAccts$+badBankAccts!.getItem(i)
        next i

        msg_id$="AP_NOT_CHECK_ACCT"
        dim msg_tokens$[1]
        msg_tokens$[1]=badBankAccts$
        gosub disp_message
    endif

    rem --- Close files now, otherwise they remain open until exit DocOut report viewer
    close(aps01_dev,err=*next)
    close(apt01_dev,err=*next)
    close(ape04_dev,err=*next)
    close(ape11_dev,err=*next)
    close(ape16_dev,err=*next)
    close(apm01_dev,err=*next)
    close(apm10_dev,err=*next)
    close(apcDist_dev,err=*next)
    close(adcBnkAcct_dev,err=*next)
    if aps01a.post_to_gl$="Y" then close(glm05_dev,err=*next)

    goto std_exit

print_total: rem --- Print totals

    retention$=""
    if retention retention$=str(totals[2])
    payment=totals[3]

    if warning$<>""
        xwk = fnblank(1)
        OutVect!.addItem(warning$)
        if retention wblanks=9 else wblanks=8
        xwk=fnblank(wblanks)
    endif

    if use_underline$="Y"
        TotVect!.addItem(str(OutVect!.size()/(columns+1)))
    endif

    xwk = fnblank(1) 
    OutVect!.addItem(total$)
    xwk=fnblank(5)
    OutVect!.addItem(str(totals[0]))
    OutVect!.addItem(str(totals[1]))
    OutVect!.addItem(str(payment))

    if retention
        OutVect!.addItem(retention$)
    endif

    lines=lines+2
    return

type_break: rem --- AP type break

    gosub vendor_break
    if multitypes=0 return

    while lasttype$<>""
        total$=Translate!.getTranslation("AON_TOTALS_FOR_")+aptype$
        
        for x=0 to 3
            totals[x]=at[x]
        next x
        
        gosub print_total
        xwk=fnblank(10)
        if retention OutVect!.addItem("")
        break
    wend

    if done return
    
    dim apm10a$:fattr(apm10a$),at[3]
    apm10a.firm_id$=ape04a.firm_id$
    apm10a.record_id_a$="A"
    apm10a.ap_type$=ape04a.ap_type$
    apm10a.code_desc$=nf$
    k$=apm10a.firm_id$+apm10a.record_id_a$+apm10a.ap_type$
    find record (apm10_dev,key=k$,dom=*next) apm10a$
    lasttype$=apm10a.ap_type$
    aptype$=Translate!.getTranslation("AON_AP_TYPE_")+apm10a.ap_type$+" "+cvs(apm10a.code_desc$,2)
    BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
    HdrVect!.addItem(aptype$)
    lines=maxlines+1
    
    return

vendor_break: rem --- Vendor break

    while lastvendor$<>""
        if pos("TEMP"=lastvendor$)=1 then 
            x$=lastvendor$(1,4)
        else 
            x$=fnmask$(lastvendor$(1,vendor_size),m0$)
        endif
        
        total$=Translate!.getTranslation("AON_TOTALS_FOR_VENDOR_")+x$
        
        for x=0 to 3
            totals[x]=vt[x]
        next
        
        warning$=""
        payment=totals[3]
       
        if payment<0 
            negative_checks=1
            warning$=Translate!.getTranslation("AON_WARNING:_NEGATIVE_CHECK_AMOUNT")
        endif
        
        if printed>=1 
            gosub print_total
            xwk=fnblank(10)
            if retention OutVect!.addItem("")
            break
        endif
        
        if warning$="" break

        xwk = fnblank(1)

        OutVect!.addItem(warning$)
        xwk = fnblank(8)
        
        if retention
            xwk = fnblank(1)
        endif
        
        lines=lines+1
        xwk=fnblank(10)
        if retention OutVect!.addItem("")
        break
    wend

    if done return
    
    dim vt[3]
    lastvendor$=ape04a.vendor_id$
    if cvs(lastvendor$,2)="" return
    dim apm01a$:fattr(apm01a$)
    apm01a.firm_id$=ape04a.firm_id$
    apm01a.vendor_id$=ape04a.vendor_id$
    apm01a.vendor_name$=nf$
    k$=apm01a.firm_id$+apm01a.vendor_id$
    find record (apm01_dev,key=k$,dom=*proceed) apm01a$
    vendor$=fnmask$(apm01a.vendor_id$(1,vendor_size),m0$)
    name$=apm01a.vendor_name$
    thisvendor$=vendor$
    thisname$=name$
    ach$=iff(apm01a.payment_type$="A","Y","")
    accum_ach$=ach$
    
    return

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem --- Functions

rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if cvs(q1$,2)="" return ""
        if q2$="" q2$=fill(len(q1$),"0")
        if pos("E"=cvs(q1$,4)) goto alpha_mask
:      else return str(-num(q1$,err=alpha_mask):q2$,err=alpha_mask)
alpha_mask:
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),
:       str(err),rd_err_text$,rd_err_act$
std_missing_params_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    release

rem #endinclude std_end.src

    end
