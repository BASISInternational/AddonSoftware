rem --- Purchase Journal (Report Overlay)
rem --- Program apr_purchases v8.0.0 18Jan2007 (apr_ob)
rem --- Created by adx_codeport.bbx v1.1.5 (01/18/2007 08:11:06)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Open/Lock files

    files=8,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="ads-01"
    files$[2]="apm-01"
    files$[3]="apm-03"
    files$[4]="apm-10"
    files$[5]="apt-02"
    files$[6]="apt-05"
    files$[7]="apw-04"
    files$[8]="apw-14"
    options$[7]="C"
    options$[8]="C"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    ads01_dev=channels[1]
    apm01_dev=channels[2]
    apm03_dev=channels[3]
    apm10_dev=channels[4]
    apt02_dev=channels[5]
    apt05_dev=channels[6]
    apw04_dev=channels[7]
    apw14_dev=channels[8]

rem --- Dimension string templates

    dim apm01a$:templates$[2],apm03a$:templates$[3],apm10a$:templates$[4],
:       apt02a$:templates$[5],apt05a$:templates$[6],apw04a$:templates$[7],
:       apw14a$:templates$[8]

rem --- Retrieve miscellaneous templates

    files=2,begfile=1,endfile=files
    dim ids$[files],templates$[files]
    ids$[1]="aps-01A"
    ids$[2]="gls-01A"
    call pgmdir$+"adc_template.aon",begfile,endfile,ids$[all],templates$[all],status
    if status goto std_exit

rem --- Dimension miscellaneous string templates

    dim aps01a$:templates$[1],gls01a$:templates$[2]

rem --- Assign form input values to local variables

    table$=fnget_table$("")
    period$=fnget_fld_data$(table$,"period_year")
    sequence$=fnget_fld_data$(table$,"report_sequence")
    year=num(period$(3,4))
    period=num(period$(1,2))
    call pgmdir$+"adc_perioddates.aon",ads01_dev,period,year,begdate$,enddate$,status

rem --- Retrieve parameter records

    aps01a_key$=firm_id$+"AP00"
    find record (ads01_dev,key=aps01a_key$,err=std_missing_params) aps01a$
    gls01a_key$=firm_id$+"GL00"
    find record (ads01_dev,key=gls01a_key$,err=std_missing_params) gls01a$
    vendor_size=num(aps01a.vendor_size$)
    multitypes=aps01a.multi_types$="Y"
    m0$=aps01a.vendor_output$
    m1$=aps01a.amount_mask$
    m0=len(m0$)
    m1=len(m1$)
    acct_length=num(gls01a.acct_length$)
    g0$=gls01a.acct_output$
    g0=len(g0$)
    headings=3
    if multitypes headings=4

rem --- Is General Ledger installed?

    gl$="N"
    application$="AP"
    call pgmdir$+"adc_application.aon",application$,info$[all]
    gl$=info$[9]
    if gl$="Y"
       application$="GL"
       call pgmdir$+"adc_application.aon",application$,info$[all]
       gl$=info$[20]
    endif
    if gl$="Y"
       m1$=gls01a.amount_mask$
       m1=len(m1$)
    endif

rem --- Initializations

    dim headings$[headings],underline$(m1,"-"),o[6],h[11]
    vm=max(m0,8)
    gm=max(g0,9)
    done=0
    page=0
    width=132
    report=0
    maxlines=59
    lines=maxlines+1
    more=1
    when$=sysinfo.system_date$
    clock$=""
    nf$="(Not on File)"
    lasttype$=""
    lastacct$=""
    lastvendor$=""
    lastdetail$=""
    altseq$=""
    vendor$=""
    name$=""
    account$=""
    description$=""
    ah$="Account"
    dh$="Description"
    if gl$<>"Y"
       ah$=""
       dh$=""
       gm=0
       g0=0
       width=80
    endif
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$
    headings$[2]="For Period "+str(period)+" From "+fndate$(begdate$)+" Through "+fndate$(enddate$)
    if sequence$="D"
       channel=apw04_dev 
       headings$[1]=headings$[1]+" By Distribution Account"
    endif
    if sequence$="V" 
       channel=apt02_dev
       headings$[1]=headings$[1]+" By Vendor"
    endif
    if sequence$="A"
       channel=apw14_dev
       headings$[1]=headings$[1]+" By Alternate Sequence"
    endif
    sorting=pos(sequence$="AD")
    alternate=1
    distribution=2
    vendor=3

rem --- Initialize print positions

    o[0]=0
    o[6]=width-m1
    o[1]=o[0]+gm+1
    o[2]=o[1]+36
    o[3]=o[2]+vm
    w=gm+3
    o[5]=o[6]-10
    o[4]=o[5]-14
    dw=min(30,o[4]-o[3]-1)
    if sequence$="V"
       o[1]=o[0]+vm
       o[2]=o[1]+31
       o[3]=o[2]+14
       w=max(3+vm,gm)
       o[4]=o[3]+9
       o[5]=o[4]+gm+1
       dw=min(35,o[6]-o[5]-1)
    endif
    if sequence$="A"
       o[1]=11
       o[2]=o[1]+vm+31
       o[3]=o[2]+14
       w=13
       o[4]=o[3]+9
       o[5]=o[4]+gm+1
       dw=min(35,o[6]-o[5]-1)
    endif

rem --- Additional file opens

    files=2,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    if gl$="Y"
       files$[1]="glm-01"
       files$[2]="glw-04"
       options$[2]="CL"
    endif
    if gl$="Y" call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                              ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    glm01_dev=channels[1]
    glw04_dev=channels[2]

rem --- Dimension string templates

    if gl$="Y" dim glm01a$:templates$[1],glw04a$:templates$[2]

rem --- Open printer

    call pgmdir$+"adc_printer.aon",printer_dev,1,"","",status
    if status goto std_exit

rem --- Sort Invoice History (apt-02) by distribution account or alternate sequence

    while sorting
          call pgmdir$+"adc_progress.aon","NC","","","","",0,apt02_dev,1,meter_num,status
          read (apt02_dev,key=firm_id$,dom=*next)

          rem --- Read next history record

          while more
                k$=key(apt02_dev,end=*break)
                if pos(firm_id$=k$)<>1 break
                read record (apt02_dev) apt02a$
                call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
                if apt02a.accting_date$<begdate$ continue
                if apt02a.accting_date$>enddate$ continue

                rem --- Initialize and write sort record

                switch sorting
                       case alternate
                            dim apm01a$:fattr(apm01a$),apw14a$:fattr(apw14a$)
                            apm01a.firm_id$=apt02a.firm_id$
                            apm01a.vendor_id$=apt02a.vendor_id$
                            k$=apm01a.firm_id$+apm01a.vendor_id$
                            find record (apm01_dev,key=k$,dom=*break) apm01a$
                            apw14a.firm_id$=apt02a.firm_id$
                            apw14a.ap_type$=apt02a.ap_type$
                            apw14a.alt_sequence$=apm01a.alt_sequence$
                            apw14a.vendor_id$=apt02a.vendor_id$
                            apw14a.invoice_type$=apt02a.invoice_type$
                            apw14a.reference$=apt02a.reference$
                            apw14a.gl_account$=apt02a.gl_account$
                            apw14a.accting_date$=apt02a.accting_date$
                            apw14a$=field(apw14a$)
                            k$=apw14a.firm_id$+apw14a.ap_type$+apw14a.alt_sequence$+
:                              apw14a.vendor_id$+apw14a.invoice_type$+apw14a.reference$+
:                              apw14a.gl_account$+apw14a.accting_date$
                            write record (apw14_dev,key=k$) apw14a$
                       break
                       case distribution
                            dim apw04a$:fattr(apw04a$)
                            apw04a.firm_id$=apt02a.firm_id$
                            apw04a.ap_type$=apt02a.ap_type$
                            apw04a.gl_account$=apt02a.gl_account$
                            apw04a.vendor_id$=apt02a.vendor_id$
                            apw04a.invoice_type$=apt02a.invoice_type$
                            apw04a.reference$=apt02a.reference$
                            apw04a.accting_date$=apt02a.accting_date$
                            apw04a$=field(apw04a$)
                            k$=apw04a.firm_id$+apw04a.ap_type$+apw04a.gl_account$+
:                              apw04a.vendor_id$+apw04a.invoice_type$+apw04a.reference$+
:                              apw04a.accting_date$
                            write record (apw04_dev,key=k$) apw04a$
                       break
                       case default
                       break
                swend

                rem --- Loop back for next invoice record

          wend

          rem --- Sorting completed

          call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status
          sorting=0
          break
    wend  

rem --- Start progress meter and position the driver file

    sequence=pos(sequence$="ADV")
    call pgmdir$+"adc_progress.aon","NC","","","","",0,channel,1,meter_num,status
    read (apw04_dev,key=firm_id$,dom=*next)
    read (apw14_dev,key=firm_id$,dom=*next)
    read (apt02_dev,key=firm_id$,dom=*next)

rem --- Read next invoice history or sort record

    while more
          k$=key(channel,end=*break)
          if pos(firm_id$=k$)<>1 break
          if sequence$="V" read record (apt02_dev,key=k$) apt02a$
          if sequence$="A"
             read record (apw14_dev,key=k$) apw14a$
             apt02a.firm_id$=apw14a.firm_id$
             apt02a.ap_type$=apw14a.ap_type$
             apt02a.vendor_id$=apw14a.vendor_id$
             apt02a.invoice_type$=apw14a.invoice_type$
             apt02a.reference$=apw14a.reference$
             apt02a.gl_account$=apw14a.gl_account$
             apt02a.accting_date$=apw14a.accting_date$
          endif
          if sequence$="D"
             read record (apw04_dev,key=k$) apw04a$
             apt02a.firm_id$=apw04a.firm_id$
             apt02a.ap_type$=apw04a.ap_type$
             apt02a.vendor_id$=apw04a.vendor_id$
             apt02a.invoice_type$=apw04a.invoice_type$
             apt02a.reference$=apw04a.reference$
             apt02a.gl_account$=apw04a.gl_account$
             apt02a.accting_date$=apw04a.accting_date$
          endif
          call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
          if pos(sequence$="AD")
              k$=apt02a.firm_id$+apt02a.ap_type$+apt02a.vendor_id$+apt02a.invoice_type$+
:                apt02a.reference$+apt02a.gl_account$+apt02a.accting_date$
              find record (apt02_dev,key=k$,dom=*continue) apt02a$
          endif

          rem --- Is the record in the requested period?

          if apt02a.accting_date$<begdate$ continue
          if apt02a.accting_date$>enddate$ continue

          rem --- Level breaks

          detail$=apt02a.invoice_type$+apt02a.reference$
          if apt02a.ap_type$<>lasttype$ gosub type_break
          if apt02a.gl_account$<>lastaccount$ gosub account_break
          if apt02a.vendor_id$<>lastvendor$ gosub vendor_break
          if detail$<>lastdetail$ gosub detail_break

          rem --- Retrieve GL account description if necessary

          if sequence$<>"D"
:            if gl$<>"Y" account$="",
:                        description$=""
:                   else gosub gl_account

          rem --- Print detail line

          amount=apt02a.gl_post_amt
          if lines+2>maxlines gosub report_heading
          switch sequence
                 case alternate
                      if vendor$<>"" 
                         print (printer_dev) ""
                         lines=lines+1
                      endif
                      if description$<>"" description$=description$(1,dw)
                      name$=vendor$+" "+name$
                      vendor$=altseq$
                      if lines+1>maxlines gosub report_heading
                      print (printer_dev) @(o[0]),vendor$,@(o[1]),name$,@(o[2]),invoice$,@(o[3]),invdate$,
:                                         @(o[4]),account$,@(o[5]),description$,@(o[6]),amount:m1$
                 break
                 case distribution
                      if account$<>"" or vendor$<>"" 
                         print (printer_dev) ""
                         lines=lines+1
                         if name$<>"" name$=name$(1,dw)
                         if lines+1>maxlines gosub report_heading
                         print (printer_dev) @(o[0]),account$,@(o[1]),description$,@(o[2]),vendor$,
:                                            @(o[3]),name$,@(o[4]),invoice$,@(o[5]),invdate$,
:                                            @(o[6]),amount:m1$
                      endif
                 break
                 case vendor
                      if vendor$<>"" 
                         print (printer_dev) ""
                         lines=lines+1
                      endif
                      if description$<>"" description$=description$(1,dw)
                      if lines+1>maxlines gosub report_heading
                      print (printer_dev) @(o[0]),vendor$,@(o[1]),name$,@(o[2]),invoice$,@(o[3]),invdate$,
:                                         @(o[4]),account$,@(o[5]),description$,@(o[6]),amount:m1$
                 break
                 case default
                 break
          swend  
          lines=lines+1

          rem --- Generate GL Recap

          if gl$="Y"
             account$=glm01a.gl_account$
             gosub update_gl
          endif

          rem --- Accumulate totals

          atotal=atotal+amount
          gtotal=gtotal+amount
          vtotal=vtotal+amount
          rtotal=rtotal+amount

          rem --- Initialize fields for the next record

          altseq$=""
          vendor$=""
          name$=""
          account$=""
          description$=""

          rem --- Loop back for next record

    wend

rem --- All done

    done=1
    gosub type_break
    total$="Total For Report"
    total=rtotal
    gosub print_total

rem --- Run next overlay

    if gl$<>"Y" goto std_exit
    run pgmdir$+"glr_accttotals.aon",err=*next
    goto std_exit

report_heading: rem --- Report heading

    lines=headings+1
    call pgmdir$+"adc_rpthead.aon",printer_dev,headings$[all],headings,page,width,when$,clock$,status
    if status exitto std_exit
    if sequence$="D" print (printer_dev) @(o[0]),ah$,@(o[1]),dh$,@(o[2]),"Vendor",@(o[3]),"Name",
:                                        @(o[4]),"Invoice/Check",@(o[5]+2),"Date",@(o[6]+m1-7),"Amount"
    if sequence$="V" print (printer_dev) @(o[0]),"Vendor",@(o[2]),"Invoice/Check",@(o[3]),"Date",
:                                        @(o[4]),ah$,@(o[5]),dh$,@(o[6]+m1-7),"Amount"
    if sequence$="A" print (printer_dev) @(o[0]),"Alt Seq",@(o[1]),"Vendor",@(o[2]),"Invoice/Check",
:                                        @(o[3]+2),"Date",@(o[4]),ah$,@(o[5]),dh$,@(o[6]+m1-7),"Amount"
    lines=lines+1
    return

print_total: rem --- Print subtotal/total

    if lines+2>maxlines gosub report_heading
    print (printer_dev) @(o[6]),underline$
    print (printer_dev) @(o[6]-len(total$)),total$,@(o[6]),total:m1$
    lines=lines+2
    return

type_break: rem --- AP type break

    while lasttype$<>""
          if sequence$="D" gosub account_break
:                     else gosub vendor_break
          if multitypes
             total$="Total for "+aptype$
             total=atotal
             gosub print_total
          endif
          break
    wend
    if multitypes=0 return
    if done return
    dim apm10a$:fattr(apm10a$)
    apm10a.firm_id$=apt02a.firm_id$
    apm10a.record_id_a$="A"
    apm10a.ap_type$=apt02a.ap_type$
    apm10a.code_desc$=nf$
    k$=apm10a.firm_id$+apm10a.record_id_a$+apm10a.ap_type$
    find record (apm10_dev,key=k$,dom=*next) apm10a$
    aptype$="AP Type "+apm10a.ap_type$+" "+cvs(apm10a.code_desc$,2)
    headings$[3]=aptype$
    lines=maxlines+1
    atotal=0
    lasttype$=apm10a.ap_type$
    return

account_break: rem --- GL account break

    if sequence$<>"D" return
    if gl$<>"Y" return
    while lastaccount$<>""
          gosub vendor_break
          total$="Total For Account "+fnmask$(lastaccount$(1,acct_length),g0$)
          total=gtotal
          gosub print_total
          break
    wend
    if done return
    gosub gl_account
    gtotal=0
    return

vendor_break: rem --- Vendor break

    while lastvendor$<>""
          gosub detail_break
          x$=lastvendor$(1,4)
          if x$<>"TEMP" x$=fnmask$(lastvendor$(1,vendor_size),m0$)
          total$="Total for Vendor "+x$
          total=vtotal
          gosub print_total
          break
    wend
    if done return
    dim apm01a$:fattr(apm01a$)
    apm01a.firm_id$=apt02a.firm_id$
    apm01a.vendor_id$=apt02a.vendor_id$
    apm01a.vendor_name$=nf$
    k$=apm01a.firm_id$+apm01a.vendor_id$
    find record (apm01_dev,key=k$,dom=*next) apm01a$
    vendor$=fnmask$(apm01a.vendor_id$(1,vendor_size),m0$)
    name$=apm01a.vendor_name$
    altseq$=apm01a.alt_sequence$
    vtotal=0
    lastvendor$=apm01a.vendor_id$
    return

detail_break: rem --- Invoice type/reference (Invoice/Check) break

    if done return
    invoice$=apt02a.reference$
    invdate$=fndate$(apt02a.accting_date$)
    lastdetail$=apt02a.invoice_type$+apt02a.reference$
    if apt02a.invoice_type$="I" goto temp_vendor

    rem --- Read the Check History (apt-05) record

    invoice$="MC "+apt02a.reference$(1,7)
    if pos("TEMP"=apt02a.vendor_id$)<>1 return
    dim apt05a$:fattr(apt05a$)
    apt05a.firm_id$=apt02a.firm_id$
    apt05a.ap_type$=apt02a.ap_type$
    apt05a.check_no$=apt02a.reference$(1,7)
    apt05a.vendor_id$=apt02a.vendor_id$
    apt05a.vendor_name$=nf$
    apt05_key$=apt05a.firm_id$+apt05a.ap_type$+apt05a.check_no$+apt05a.vendor_id$
    read (apt05_dev,key=apt05_key$,dom=*next)
    k$=key(apt05_dev,end=l6680)
    if pos(apt05_key$=k$)=1 read record (apt05_dev) apt05a$
    vendor$=apt05a.vendor_id$
    name$=apt05a.vendor_name$
    return

temp_vendor: rem --- Retrieve Temporary Vendor (apm-03) record

    if pos("TEMP"=apt02a.vendor_id$)<>1 return
    dim apm03a$:fattr(apm03a$)
    apm03a.firm_id$=apt02a.firm_id$
    apm03a.ap_type$=apt02a.ap_type$
    apm03a.ap_inv_no$=apt02a.reference$
    apm03a.vendor_name$=nf$
    k$=apm03a.firm_id$+apm03a.ap_type$+apm03a.ap_inv_no$
    find record (apm03_dev,key=k$,dom=*next) apm03a$
    vendor$=apt02a.vendor_id$
    name$=apm03a.vendor_name$
    return

gl_account: rem --- Retrieve the GL account master

    dim glm01a$:fattr(glm01a$)
    glm01a.firm_id$=apt02a.firm_id$
    glm01a.gl_account$=apt02a.gl_account$
    glm01a.gl_acct_desc$=nf$
    k$=glm01a.firm_id$+glm01a.gl_account$
    find record (glm01_dev,key=k$,dom=*next) glm01a$
    account$=fnmask$(glm01a.gl_account$(1,acct_length),g0$)
    description$=glm01a.gl_acct_desc$
    lastaccount$=glm01a.gl_account$
    return

update_gl: rem --- Update GL summary

    if gl$<>"Y" return
    if amount=0 return
    dim glw04a$:fattr(glw04a$)
    glw04a.firm_id$=apt02a.firm_id$
    glw04a.gl_account$=account$
    k$=glw04a.firm_id$+glw04a.gl_account$
    find record (glw04_dev,key=k$,dom=*next) glw04a$
    glw04a.gl_post_amt=glw04a.gl_post_amt+amount
    glw04a$=field(glw04a$)
    write record (glw04_dev,key=k$) glw04a$
    return

rem --- Functions

rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Date/time handling functions

    def fndate$(q$)=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2))))

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run pgmdir$+"ads_process_end.aon",err=*next
    release

rem #endinclude std_end.src

    end
