rem DataPort Data File Conversion Utility (Template creation program)
rem Program create_template.bbj v8.0.0 28Oct07
rem  
rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Feb2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+
rem  
rem --- This overlay creates templates for version 6/7 data files using the 
rem --- Addon dictionary files ddm-01,ddm-03 and ddm-04 and then copied to Addon 8 
rem --- data files after performing date format changes.
rem 
rem
rem --- Templates of dictionary files
dim ddm_03$:"file_name:c(6),record_id:c(1*),description:c(30),reserved_str:c(33*),reserved_num:n(3*)"
dim ddm_04$:"file_name:c(6),record_id:c(1),layout_seq:c(3*),data_name:c(12),"+
:           "reserved_str1:c(41),fld_sep:c(1),reserved_str2:c(68*),reserved_num:n(1*),fld_repeats:n(3*),fld_occurs:n(3)"
dim ddm_01$:"data_name:c(12*),description:c(30),lstrev:c(6),data_type:c(1),reserved_str:c(68*),fld_length:n(3*),display_len:n(3*),reserved_num:n(1*)"

rem --- Reads the source folder
let adata_chan=unt
open(adata_chan)source_folder$
read_datafile:
    readrecord(adata_chan,end=end_read_datafile)datafile$
    gosub check_portable_files
    print datafile$
    print (log_dev)datafile$
    if datafile$="IVM-03" print "   ivm-03 will not be ported";goto read_datafile
rem --- Reads the dictionary file ddm-03 to get data file info
let ddm_03_chan=unt
open(ddm_03_chan)source_folder$+"\DDM-03"
rem extractrecord(ddm_03_chan,key="arm-01A")
read_ddm_03:
    rd_gen_table_tpl$=""
    data_seq_str$=""
    count=0
    readrecord(ddm_03_chan,end=end_read)ddm_03$
    if ddm_03.file_name$=datafile$ then
        gosub get_version8_template
        print "   Creating template of "+ddm_03.file_name$+ddm_03.record_id$+"  ("+cvs(table_alias$,2)+")"
        print (temp_dev)datafile$
        print (log_dev)"   Creating template of "+ddm_03.file_name$+ddm_03.record_id$
        let ddm_04_chan=unt
        open (ddm_04_chan)source_folder$+"\DDM-04"
        read_data:
            readrecord(ddm_04_chan,end=end_loop)ddm_04$
            if ddm_04.file_name$=ddm_03.file_name$ and ddm_04.record_id$=ddm_03.record_id$ then
                let temp_chan=unt
                open(temp_chan)source_folder$+"\DDM-01"
                findrecord(temp_chan,key=ddm_04.data_name$,dom=read_data)ddm_01$
                close(temp_chan)
                rd_col_length=ddm_01.fld_length
                if ddm_04.fld_repeats>1 rd_col_length=ddm_01.fld_length*ddm_04.fld_repeats

                rd_col_occurs_sfx$=""
                rd_col_occurs$=""
                rd_col_occurs=1
                if ddm_04.fld_occurs>1
                    rd_col_occurs$="YES"
                    rd_col_occurs=ddm_04.fld_occurs
                endif

                data_seq_pos=pos(pad(ddm_04.data_name$,16)=data_seq_str$,20)
                if data_seq_pos<>0 
                    rd_occur_adj=num(data_seq_str$(data_seq_pos+17,2))
                else
                    rd_occur_adj=0
                    data_seq_str$=data_seq_str$+pad(ddm_04.data_name$,16)+"-00;"
                endif

                for rd_curr_occur=1 to rd_col_occurs
                    data_seq_pos=pos(pad(ddm_04.data_name$,16)=data_seq_str$,20)
                    data_seq_str$(data_seq_pos+17,2)=str(num(data_seq_str$(data_seq_pos+17,2))+1:"00")

                    if rd_col_occurs$="YES" or rd_occur_adj>0 rd_col_occurs_sfx$="_"+str(rd_curr_occur+rd_occur_adj:"00")
                    p=1,data_name$=cvs(ddm_04.data_name$,3)
                    while p
                        p=pos("/"=data_name$)
                        if p<>0 data_name$(p,1)="_"
                    wend
                    p=1
                    while p
                        p=pos("&"=data_name$)
                        if p<>0 data_name$(p,1)="_"
                    wend
                    p=1
                    while p
                        p=pos(" "=data_name$)
                        if p<> 0 data_name$(p,1)="_"   
                    wend
                    rd_temp_seg$=cvs(data_name$,3)+rd_col_occurs_sfx$+":"
                    if ddm_01.data_type$="N" then datatype$="N" else datatype$="C"
                    rd_temp_seg$=rd_temp_seg$+datatype$+"("
                    rd_temp_seg$=rd_temp_seg$+str(rd_col_length)
                    if ddm_04.fld_sep$="Y"
                            gosub create_delim
                    endif
                    if ddm_04.fld_sep$="X" 
                            gosub create_delim
                    endif
                    rd_temp_seg$=rd_temp_seg$+")"
                    count=count+1
                    if rd_gen_table_tpl$<>"" rd_gen_table_tpl$=rd_gen_table_tpl$+","
                    rd_gen_table_tpl$=rd_gen_table_tpl$+rd_temp_seg$

                next rd_curr_occur
            endif 
        goto read_data
    endif

end_loop:
if rd_gen_table_tpl$<>"" then 
    dim old_tmpl$:rd_gen_table_tpl$
    fields$="fname["+str(count)+"]:c(20*)"
    dim fieldlist$:fields$
    fieldlist$= FATTR(old_tmpl$,"")
    
    if version8_template$<>rd_gen_table_tpl$ then print(temp_dev)rd_gen_table_tpl$+$0A$+version8_template$+$0A$+$0A$
    let datafile_chan=unt
    open(datafile_chan)source_folder$+"\"+datafile$
    print "   Copying data from  "+ddm_03.file_name$+ddm_03.record_id$
    print(log_dev)"   Copying data from    "+ddm_03.file_name$+ddm_03.record_id$
    read_date:
        if pos("RECORD_ID_"=fieldlist.fname$[2]) then 
                readrecord(datafile_chan,end=end_ddm_03)old_tmpl$
                if field(old_tmpl$,fieldlist.fname$[2])=ddm_03.record_id$ goto print_fields
                goto read_date
         else
             readrecord(datafile_chan,end=end_ddm_03)old_tmpl$
         endif
        print_fields:
        
        for i=1 to count
            if source_version=6 then gosub address_field_change
            if pos("_DATE"=fieldlist.fname$[i]) or pos("_DT"=fieldlist.fname$[i]) then
                x$=field(old_tmpl$,fieldlist.fname$[i])
                if len(x$)=6 then goto convert_A4Date
                if cvs(x$,3)="" or len(x$)<>3 then continue
                x$=fnb$(x$)
                x$=fngetdate$(x$)
                if pos("00"=x$(5)) then gosub error_log
                let date_flag=1
                goto copy_data
                convert_A4Date:
                    gosub add_centuary_prefix
                    let date_flag=1
            endif
            if pos("YEAR"=fieldlist.fname$[i])then
               x$=field(old_tmpl$,fieldlist.fname$[i])
               if len(x$)<>2 then continue
               gosub add_centuary_prefix
            endif
            copy_data:
            old_fieldname$=fieldlist.fname$[i]
            gosub check_fieldname
            if new_fieldname$="" then continue
            if date_flag=1 then 
                field new_tmpl$,new_fieldname$=x$
            else
                field new_tmpl$,new_fieldname$=field(old_tmpl$,fieldlist.fname$[i],err=std_err)
            endif
            let date_flag=0,x$=""
        next i
        new_tmpl$=field(new_tmpl$)
        writerecord(version8_chan,err=std_err)new_tmpl$
        goto read_date
endif

end_ddm_03:
    if datafile_chan<>0 close(datafile_chan)
    if version8_chan<>0 close(version8_chan)
    if ddm_04_chan<>0 close(ddm_04_chan)
    goto read_ddm_03
end_read:

if unt>100 escape
rem escape

if ddm_03_chan<>0 close(ddm_03_chan)
goto read_datafile
end_read_datafile:
close(adata_chan)
run pgmdir$+"normalization.bbj"
end
rem release

create_delim:rem --- Create Delimiter
    rd_temp_seg$=rd_temp_seg$+"*"
return

error_log:
    print (log_dev)"  Invalid date occured in "+datafile$+" "+fieldlist.fname$[i]+" field"
return

add_centuary_prefix:
    if pos("A"=x$) then 
        x$="200"+x$(2)
    else
        x$="19"+x$
    endif  
return

get_version8_template:
    new_datafile$=cvs(datafile$,8)
    search_string$=ddm_03.file_name$+ddm_03.record_id$
    let xref_chan=unt
    open(xref_chan)filedir$+"file_xref"
    read_xrefs:
    read(xref_chan,end=get_template)tempname$
    if cvs(datafile$,8)=cvs(tempname$(1,12),2+8) then 
        search_string$=cvs(tempname$(14),2)+search_string$(len(search_string$))
        new_datafile$=cvs(tempname$(14),2)
        print "   "+datafile$+" is renamed to "+new_datafile$
        print (log_dev)datafile$+" is renamed to "+new_datafile$
        goto get_template
    endif
    if cvs(search_string$,8)=cvs(tempname$(1,12),2+8) then
        search_string$=cvs(tempname$(14),2)
        new_datafile$=cvs(tempname$(14,len(tempname$(14))-1),2)
        print "   "+datafile$+ddm_03.record_id$+" is copied to "+new_datafile$
        print (log_dev)datafile$+" is renamed to "+new_datafile$
        goto get_template
    endif
    goto read_xrefs  

    get_template:
        close(xref_chan)
        gosub get_alias_name
        close(temp_chan)
        dim template$:"table_name:c(16*),file_name:c(30*),tmpl:c(10230*)"
        let temp_chan=unt
        open(temp_chan)dictfile$+"ddm_table_tpls.dat"
        findrecord(temp_chan,key=pad(table_alias$,16),dom=check_for_definition)template$
        version8_template$=template.tmpl$
        close(temp_chan)
        assign_template:
        dim new_tmpl$:version8_template$
        newfieldlist$= FATTR(new_tmpl$,"")
        dim name$[100]
        tempfieldlist$=newfieldlist$,n=1,p=1
        while p>0
            p=pos($0A$=tempfieldlist$)
            name$[n]=tempfieldlist$(1,p-1)
            n=n+1
            tempfieldlist$=tempfieldlist$(p+1)
        wend
        let version8_chan=unt
        open(version8_chan,err=check_for_definition)destin_folder$+"\"+new_datafile$
        a=msgbox(new_datafile$+" already exists in destination folder. Do you want to overwrite data?",3+32+0,"DataPort")
        if a=6 return
        if a=7 goto end_ddm_03
        if a=2 goto end_dataport
return

check_fieldname:
    new_fieldname$=""
    for x=1 to n-1
        if old_fieldname$=name$[x] then new_fieldname$=old_fieldname$
    next x
    if new_fieldname$<>"" then return
    let channel=unt
    open(channel)filedir$+"field_name_xref"
    read_xref:
        read(channel,end=goback)fieldname$
        if cvs(fieldname$(1,16),2+8)=cvs(old_fieldname$,2+8) then new_fieldname$=cvs(fieldname$(18),2+8);goto check_fieldlist
        new_fieldname$=""
    goto read_xref
    check_fieldlist:
        if new_fieldname$="*" then 
            if cvs(datafile$(1,2),8)="ap" then new_fieldname$="ap_"+cvs(old_fieldname$,2+8) else new_fieldname$="ar_"+cvs(old_fieldname$,2+8)
        endif
        found=0
        for x=1 to n-1
            if cvs(new_fieldname$,2+8)=cvs(name$[x],2+8) then found=1;break
        next x
        if found=0 then new_fieldname$=""
        goback:
        close(channel)
return

check_for_definition:
    let temp_chan=unt
    open(temp_chan)dictfile$+"ddm_tables.dat"
    read_definition:
    findrecord(temp_chan,key=pad(table_alias$,16),dom=no_8data_file)table_name$
    close(temp_chan)
    gosub create_newfile
return

no_8data_file: 
    print "   Data File "+search_string$+" will not be ported"
    goto end_ddm_03
return

create_newfile:
    call dictpgm$+"bac_create_table.bbj",table_alias$,destin_folder$+"/"+new_datafile$,rd_table_chans$[all],"CREATE",status$
    if status$<>"" then goto file_creation_err
    print "   New data file "+new_datafile$+" is created"
    if version8_chan close(version8_chan)
    open(version8_chan)destin_folder$+"\"+new_datafile$
return

get_alias_name:
    table_alias$=""
    let temp_chan=unt
    open(temp_chan)filedir$+"file_alias"
    read_alias:
    read(temp_chan,end=*return)alias_name$
    if cvs(alias_name$(1,25),2+8)=cvs(search_string$,8) then table_alias$=alias_name$(27,16);return
    goto read_alias
return

address_field_change:
    let channel=unt
    open(channel)filedir$+"file_xref_addr_chng"
    read_addr_xref:
        read(channel,end=goback_check_addr)address$
        if cvs(address$(1,13),2+8)=cvs(search_string$,2+8) then 
            if cvs(fieldlist.fname$[i],2+8)<>cvs(address$(15),2+8) then return
            address_field$=cvs(address$(15),2)
            if field(old_tmpl$,address_field$)<>"" then new_tmpl.city$=field(old_tmpl$,address_field$)(1,22);new_tmpl.state_code$=field(old_tmpl$,address_field$)(23,2)
        endif
    goto read_addr_xref
    goback_check_addr:
    close(channel)
return

check_portable_files:
    if pos("."=cvs(datafile$,8)) goto read_datafile
    if pos("_"=cvs(datafile$,8)) goto read_datafile
    if pos("z"=cvs(datafile$,8))  goto read_datafile
    if pos("dd"=cvs(datafile$,8)) goto read_datafile
    if pos("mp"=cvs(datafile$,8)) goto read_datafile
    if pos("w-"=cvs(datafile$,8)) goto read_datafile
    if pos("sh"=cvs(datafile$,8)) goto read_datafile
    let temp_chan=unt
    open(temp_chan)filedir$+"file_normalization"
    read_normalized:
    read(temp_chan,end=end_normalized)dat$
    if dat$(1,6)=cvs(datafile$,8) then close(temp_chan);goto read_datafile
    goto read_normalized
    end_normalized:
    close(temp_chan)
return

rem --- Function for unpacking 3 character date fields to get 8 character date

DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)

def fngetdate$(q1$)
    year$=q1$(len(q1$)-1)
    if q1$(len(q1$)-1)>date(0:"%Yz") then q1$=q1$(1,len(q1$)-2)+"19"+year$ else q1$=q1$(1,len(q1$)-2)+"20"+year$
    q1$=q1$(len(q1$)-3)+q1$(1,2)+q1$(4,2)
    return q1$
fnend

std_err:
    a=msgbox("Error occured while porting data file "+datafile$+"."+$0A$+"Please make sure that your data file matches to dictionary information",0+64,"DataPort")
    print(log_dev)"Error "+errmes(-1)+" occured while porting data file "+datafile$
    goto end_ddm_03
file_creation_err:
    a=msgbox("Error in creating file "+new_datafile$)
    print(log_dev)"Error in creating file "+new_datafile$
    goto end_ddm_03
end_dataport:
a=msgbox("Do you want to quit DataPort?",4+32+256,"DataPort")
if a=6 release
if a=7 goto end_ddm_03