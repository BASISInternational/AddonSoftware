rem --- v7.00 Administrator Upgrade (Lead Overlay)
rem --- Program syx70a_bbj v8.0.0 25Oct2007 (syx70a)
rem --- Created by adx_codeport.bbj v1.1.5 (14/d/y 12:14:59)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved


initializations: rem --- Initializations

    sf=0
    

rem --- Need to lowercase the names of existing UNIX/Linux files

    print "-------------------------------------------------------------------------------"
    print "-                                   DATAPORT UTLITY                           -"
    print "-                                                                             -"
    print "-            This utility will convert Addon version 6.0 data files to        -"
    print "-                                     version 8.0                             -"
    print "-                                                                             -"
    print "-------------------------------------------------------------------------------"
    print ""
    
    wait 2

    print "Renaming data files..."

rem --- Rename all uppercase files

    sym03_dev=unt
    open (sym03_dev,err=chn_error1)dir$+"SYM-03"
    read (sym03_dev,key="",dom=*next)

next_sym03: rem --- Read next SYM-03 record

    k$=key(sym03_dev,end=rename_sym03)
    read record (sym03_dev,key=k$) sym03a$
    if k$="SYM-03" goto next_sym03

rem --- Does it exist?

    channel=unt
    filename$=cvs(k$,2)
    open (channel,err=next_sym03)dir$+filename$
    close (channel)

rem --- Rename the file

    gosub display_filename
    gosub rename_file

    goto next_sym03

rename_sym03: rem --- Rename SYM-03 to ddm-02

    k$="SYM-03"
    find record (sym03_dev,key=k$) sym03a$
    close (sym03_dev)
    gosub display_filename
    filename$=k$
    gosub rename_sym03_ddm02

rem --- Rename any uppercase batched files (xxx-nn.nnn)

    print "Done"
    print "Renaming Batched AddonSoftware files..."
    sf=1
    sym09_dev=unt
    open (sym09_dev,err=chn_error2)dir$+"sym-09"
    sym39_dev=unt
    open (sym39_dev,err=chn_error3)dir$+"sym-39"
    sym59_dev=unt
    open (sym59_dev,err=chn_error4)dir$+"sym-59"
    read (sym09_dev,key="",dom=*next)

read_sym09: rem --- Read next sym-09 Process Definition

    k$=key(sym09_dev,end=done)
    read record (sym09_dev) sym09a$
    read (sym59_dev,key=sym09a$(1,10),dom=*next)

read_sym59: rem --- Read next sym-59 Process/Batch Xref

    k$=key(sym59_dev,end=next_sym09)
    if pos(sym09a$(1,10)=k$)<>1 goto next_sym09
    read record (sym59_dev) sym59a$
    read (sym39_dev,key=sym09a$(1,10),dom=*next)

read_sym39: rem --- Read next sym-39 Process Batch File

    k$=key(sym39_dev,end=next_sym59)
    if pos(sym09a$=k$)<>1 goto next_sym59
    read record (sym39_dev) sym39a$
    filename$=sym39a$(11,6)+"."+sym59a$(13,3)

    source_dev=unt
    open (source_dev,err=next_sym39)dir$+filename$
    close (source_dev)
    gosub display_batch
    gosub rename_file

next_sym39:
    goto read_sym39

next_sym59: rem --- Loop back for next sym-59 record

    goto read_sym59

next_sym09: rem --- Loop back for next sym-09 record

    goto read_sym09

done: 

rem --- Run next overlay (or entry file verification program)

    wait 1
    run "syx70b.bbj"

display_filename: rem --- Display file name

    print sym03a$(1,30)
    return

display_batch: rem --- Display batch file name

    print filename$
    return

rename_file: rem --- Rename FILENAME$ to lowercase

    number=1
    dim source$[number],target$[number],erase$[number]
    source$[1]=dir$+filename$
    target$[1]=dir$+cvs(filename$,8)
    if sf 
        if filename$(1,2)="WO" target$[1]="sf"+cvs(filename$(3),8)
        msg=msgbox(str(sf)+" "+target$[1])
    endif
    rename source$[1],target$[1]+"_temp",err=rename_err1
    rename target$[1]+"_temp",target$[1],err=rename_err2
    return

rename_sym03_ddm02: rem --- Rename SYM-03 to lowercase ddm-02

    number=1
    dim source$[number],target$[number],erase$[number]
    source$[1]=dir$+filename$
    target$[1]=dir$+"ddm-02"
    rename source$[1],target$[1],err=rename_err2
    return

rename_err1: rem --- Rename problem

    dim msg$[1]
    msg$[0]="Unable to rename "+source$[1]+" to "+target$[1]+"_temp"
    msg$[1]="Press <Enter> To Continue"
    msg=msgbox(msg$[0]+"          "+msg$[1])
    return

rename_err2: rem --- Rename problem

    dim msg$[1]
    msg$[0]="Unable to rename "+source$[1]+" to "+target$[1]
    msg$[1]="Press <Enter> To Continue"
    msg=msgbox(msg$[0]+"          "+msg$[1])
    return

chn_error1:
    a=msgbox( "File not found - sym-03")
    goto std_exit
chn_error2:
    a=msgbox("File not found - sym-09")
    goto std_exit
chn_error3:
    a=msgbox( "File not found - sym-39")
    goto std_exit
chn_error4:
    a=msgbox( "File not found - sym-59")
    goto std_exit

std_exit: rem --- Standard program end (01Mar2006)

    release
    end