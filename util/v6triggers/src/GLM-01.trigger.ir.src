rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr throw_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	ky$=td!.getKey()

	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	gldir$ = myns!.getValue("gldata")

rem --- Open Barista G/L Master file and get the template

	glm01_dev=unt
	open (glm01_dev,err=throw_error) gldir$+"glm-01"
	tpl_dev=unt
	open (tpl_dev,err=throw_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("GLM_ACCT",16),dom=throw_error) *,*,glm_acct$

	dim glm_acct$:glm_acct$

	read record (glm01_dev,key=ky$,dom=throw_error) glm_acct$
	
	glm01$="firm_id:c(2),gl_acct:c(10*=10),acct_desc:c(35),acct_type:c(1),detail:c(1),reserved_1:c(3*=10)"
	dim glm01$:glm01$
	glm01.firm_id$ = glm_acct.firm_id$
	glm01.gl_acct$ = glm_acct.gl_account$
	glm01.acct_desc$ = glm_acct.gl_acct_desc$
	glm01.acct_type$ = glm_acct.gl_acct_type$
	glm01.detail$ = glm_acct.detail_flag$
	
	td!.setReadBuffer(glm01$)
		
	goto std_exit

throw_error:

	throw lst(pgm(tcb(5))) , err
	goto std_exit
	
std_exit: