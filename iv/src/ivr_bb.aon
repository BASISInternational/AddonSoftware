rem --- Inventory Stock Status Report (Report Overlay)
rem --- Program ivr_bb v8.0.0 16Jul2007 (ivr_bb)
rem --- Created by adx_codeport.bbx v1.1.5 (07/16/2007 12:54:33)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)
   
rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$
    more=1

rem --- Print positions
    gosub Position
   
rem --- Position files

    call pgmdir$+"adc_printer.aon",printer_dev,1,"","",status
    if status goto std_exit
    call pgmdir$+"adc_progress.aon","N","","","","",0,ivm03_dev,1,meter_num,status

    first$=firm_id$+"A"
    dim ivm03a_key$:ivm03_key_tpl$
    ivm03a_key$=first$+begprod$
    p=0
    if level$="W" first$=firm_id$+"C",ivm03a_key$=first$+begwhse$,p=2
    read (ivm03_dev,key=ivm03a_key$,dom=*next)

rem ----  Loop Starting

    While More

        ivm03a_key$=key(ivm03_dev,end=*break)
        if pos(first$=ivm03a_key$)<>1 break
        whse$=ivm03a_key.warehouse_id$
        product$=ivm03a_key.product_type$
        item$=ivm03a_key.item_id$
        if level$<>"W" then

rem --- By Item: Valid Product/Item?

            if endprod$<>"" if product$>endprod$  break
            if begitem$<>"" then
                if item$<begitem$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$+
:                   ivm03a_key.product_type$+begitem$,dir=0,dom=*Continue)
                    Continue
                endif
            endif
            if enditem$<>"" then
                if item$>enditem$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$+
:                   ivm03a_key.product_type$+$FF$,dom=*Continue)
                    Continue
                endif
            endif
            x$=product$+" "+item$(1,p[0])
            
        else   
rem --- By Warehouse: Valid Warehouse/Product/Item?

            if endwhse$<>"" if whse$>endwhse$ break
            if begprod$<>"" then
                if product$<begprod$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$+
:                   ivm03a_key.warehouse_id$+begprod$,dom=*Continue)
                    Continue
                endif
            endif
            if endprod$<>"" then
                if product$>endprod$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$+
:                   ivm03a_key.warehouse_id$+$FF$,dom=*Continue)
                    Continue
                endif
            endif
            if begitem$<>"" then
                if item$<begitem$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$+
:                   ivm03a_key.warehouse_id$+ivm03a_key.prod_type$+begitem$,dom=*Continue)
                    Continue
                endif
            endif
            if enditem$<>"" then 
                if item$>enditem$ then
                    read (ivm03_dev,key=ivm03a_key.firm_id$+ivm03a_key.record_id_c$ +
:                   ivm03a_key.warehouse_id$+ivm03a_key.prod_type$+$FF$,dom=*Continue)
                    Continue
                endif
            endif
            x$=whse$+" "+product$+" "+item$(1,p[0])
            
        endif 

rem --- Level breaks?

        call pgmdir$+"adc_progress.aon","S","","","",x$,0,0,1,meter_num,status
        if level$="W" if whse$<>t0$ gosub Whse
        if product$<>t1$ gosub Product
        read (ivm03_dev)
        dim item$[2],whse$[2],w[20]
        ivm01a_key$=firm_id$+item$
        find record (ivm01a_dev,key=ivm01a_key$,dom=*Continue) ivm01a$
        if type$<>"" then
            if ivm01a.Item_type$<>type$ then
                Continue
            endif
        endif
        action$="A"
        item$[0]=firm_id$
        item$[2]=item$
        if level$="W" item$[1]=whse$,action$=""
        call pgmdir$+"ivc_wa.bbx",action$,files[all],item$[all],whse$[all],w[all],value,status

rem --- Print detail line

        x$=""
        location$=""
        cost$=""
        available=w[0]-w[2]
        needed=1+split
        if h9$<>"" needed=needed+2
        if level$="W" location$=whse$[1](1,10),cost$=str(w[11]:m3$)
        if split=0 x$=fnitem$(ivm01a.Item_Desc$,p[3],p[4],p[5])
        if l+needed>l9 gosub report_heading
        if h9$<>"" gosub ProdHeading
        print (printer_dev)@(o[0]),ivm01a_key$(3),@(o[1]),x$,@(o[2]),location$,@(o[3]),ivm01a.unit_of_sale$,@(o[4]),w[0]:m2$,@(o[5]),w[2]:m2$,@(o[6]),available:m2$,@(o[7]),w[1]:m2$,@(o[8]),cost$,@(o[9]),fnmask$(value,m1$)
        l=l+1+split
        if split<>0 then
            x$=fnitem$(ivm01a.Item_Desc$,p[3],p[4],p[5])
            print (printer_dev)@(o[0]+3),x$
        endif
rem --- Accumulate totals

        product=product+value
        warehouse=warehouse+value
        report=report+value
    wend

rem ----  Loop Ending

done: rem --- All done

    done=1
    if level$="W" gosub Whse
    if level$="I" gosub Product
    total$="Report"
    total=report
    gosub PrintTotalLine
    goto std_exit

report_heading: rem --- Report Heading

    l=headings+1
    call pgmdir$+"adc_rpthead.aon",printer_dev,headings$[all],headings,page,width,when$,clock$,status
    if status exitto std_exit
    return

ProdHeading: rem --- Product subheading

    if l+2>l9 gosub report_heading
    print (printer_dev)h9$
    print (printer_dev)""
    l=l+2
    h9$=""
    return

InitHeading: rem --- Initialize HEADINGS$

    dim h1$(width),h2$(width),h6$(width)
    if level$="I" headings$[1]=headings$[1]+" By Item"
    if level$="W" headings$[1]=headings$[1]+" By Warehouse"
    x1$="First"
    x2$="First"
    x2=5
    if begitem$<>"" x1$=begitem$
    if begprod$<>"" x2$=begprod$,x2=3
    if endprod$="" if x2=3 x2=4
    h1$(1)="From Item: "+x1$
    h1$(width-x2-13)="From Product: "+x2$
    x1$="Last"
    x2$="Last"
    x3$="All Item Types"
    if enditem$<>"" x1$=enditem$
    if endprod$<>"" x2$=endprod$
    if type$<>"" x3$="Item Type "+type$
    h2$(1)="  To Item: "+x1$
    h2$(fncenter(x3$,width))=x3$
    h2$(width-x2-13)="  To Product: "+x2$
    h6$(o[0]+1)="Item"
    h6$(o[1]+1)="Description"
    if level$="W" h6$(o[2]+1)="Location"
    h6$(o[3]+1)="UM"
    h6$(o[4]+m2-7)="On Hand"
    h6$(o[5]+m2-9)="Committed"
    h6$(o[6]+m2-9)="Available"
    h6$(o[7]+m2-8)="On Order"
    if level$="W" h6$(o[8]+m3-9)="Unit Cost"
    h6$(o[9]+m1-11)="Total Value"
    headings$[3]=h1$
    headings$[4]=h2$
    headings$[6]=h6$
    return

Whse: rem --- Warehouse Break

    if t0$<>"" then
        gosub Product
        total$="Warehouse "+t0$
        total=warehouse
        gosub PrintTotalLine
    endif
    if done  return
    t0$=whse$
    ivm10c.short_name$="(Not on File)"
    l=l9+1
    warehouse=0
    find record (ivm10c_dev,key=firm_id$+"C"+t0$,dom=*next) ivm10c$
    x$="Warehouse "+t0$+" "+cvs(ivm10c.short_name$,2)
    headings$[3]=h1$ 
    headings$[3](fncenter(x$,width),len(x$))=x$
    return 

Product: rem --- Product break

    if t1$<>"" then
    let total$="Product "+t1$,total=product
    gosub PrintTotalLine
    if done return
    let t1$=product$,ivm10a.code_desc$="(Not on File)",product=0
    find record (ivm10a_dev,key=firm_id$+"A"+t1$,dom=*next) ivm10a$
    let h9$="Product Type "+t1$+" "+ivm10a.code_desc$
    return

PrintTotalLine: rem --- Print total line

    if l+2>l9 gosub report_heading
    let l=l+2,total$="Total For "+cvs(total$,2)
    print (printer_dev)""
    print (printer_dev)@(o[9]-len(total$)),total$,@(o[9]),fnmask$(total,m1$)
    return

Position: rem --- Print positions

    m1$=ivs01a.amount_mask$
    m2$=ivs01a.unit_mask$
    m3$=ivs01a.cost_mask$
    call pgmdir$+"adc_sizemask.aon",m2$,m2,8,12
    call pgmdir$+"adc_sizemask.aon",m3$,m3,8,12
    o[1]=p[0]+1
    o[9]=width-m1
    o[8]=o[9]
    if level$="W" o[8]=o[9]-m3
    o[7]=o[8]-m2
    o[6]=o[7]-m2
    o[5]=o[6]-m2
    o[4]=o[5]-m2
    o[3]=o[4]-3
    o[2]=o[3]
    split=0
    if level$="W" o[2]=o[3]-11
    if p[0]+p[3]+p[4]+p[5]>o[2] split=1
    gosub InitHeading
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Date/time handling functions

    def fndate$(q$)=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2))))
    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem #endinclude std_functions.src

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call pgmdir$+"adc_error.aon",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run pgmdir$+"ads_process_end.aon",err=*next
    release
rem #endinclude std_end.src

    end
