rem --- Physical Inventory Load Cycle Code Utility
rem --- Program ivx_loadcyclecode.aon v8.0.0 02Feb2009

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2009 AddonSoftware
rem --- All Rights Reserved

    seterr std_error
    setesc std_error
	
rem --- Retrieve the program path

	pgmdir$=""
	pgmdir$=stbl("+DIR_PGM",err=*next)
	sypdir$=""
	sypdir$=stbl("+DIR_SYP",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL")
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO")
    firm_id$=sysinfo.firm_id$

rem --- Inform user of what's about to happen

	msg_id$ = "AON_UPDT_CYCLE_CODE"
	gosub disp_message
	if msg_opt$<>"Y" then goto std_exit

rem --- Open all files

	num_files = 2
	open_beg  = 1
	open_end  = num_files
	
	dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
	
	open_tables$[1]="IVS_PARAMS",   open_opts$[1]="OTA"
	open_tables$[2]="IVC_PHYSCODE", open_opts$[2]="OTA"
	
	gosub open_tables
	
	params_dev   = num( open_chans$[1] )
	physcode_dev = num( open_chans$[2] )
	
	dim params_rec$:open_tpls$[1]
	dim physcode_rec$:open_tpls$[2]

rem --- Parameters
	
	find record (params_dev, key=firm_id$+"IV00", dom=std_missing_params) params_rec$

rem --- Initializations
	
	precision num( params_rec.precision$ )
	more = 1
	call pgmdir$+"adc_progress.aon","N","","","","",0,itemwhse_dev,1,meter_num,ignore_status

rem ---  SQL to get unique warehouse and cycle code

	prep$ = ""
	prep$ = prep$ + "SELECT distinct warehouse_id, pi_cyclecode "
	prep$ = prep$ + "  FROM ivm_itemwhse "
	prep$ = prep$ + " WHERE pi_cyclecode > '  ' AND firm_id = '" + firm_id$ + "' "
	prep$ = prep$ + " ORDER BY warehouse_id, pi_cyclecode"
	
	sql_dev = sqlunt
	sqlopen (sql_dev) stbl("+DBNAME")
	sqlprep (sql_dev) prep$
	dim sql_rec$:sqltmpl(sql_dev)
	sqlexec (sql_dev)

rem --- Get next sort record (firm/warehouse/cycle)
	
	while more

		sql_rec$ = sqlfetch(sql_dev, end=*break)
		call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,ignore_status

rem --- Does Cycle Code exists?

		find (physcode_dev, key=firm_id$+sql_rec.warehouse_id$+sql_rec.pi_cyclecode$, dom=build_rec)
		break

build_rec: rem --- If not, build and write record

		dim physcode_rec$:fattr(physcode_rec$)
		physcode_rec.firm_id$        = firm_id$
		physcode_rec.warehouse_id$   = sql_rec.warehouse_id$
		physcode_rec.pi_cyclecode$   = sql_rec.pi_cyclecode$
		physcode_rec.phys_inv_sts$   = "0"
		physcode_rec.pending_action$ = "0"
		write record (physcode_dev) physcode_rec$

	wend
	
	sqlclose(sql_dev)

rem --- All done

	call pgmdir$+"adc_progress.aon","D","","","","",0,0,1,0,ignore_status
	goto std_exit

open_tables: rem --- Open tables

	call sypdir$+"bac_open_tables.bbj",
:		open_beg,
:		open_end,
:		open_tables$[all],
:		open_opts$[all],
:		open_chans$[all],
:		open_tpls$[all],
:		table_chans$[all],
:		open_batch,
:		open_status$

	if open_status$<>"" then
		msg_id$="ENTRY_OPEN_ERROR"
		dim msg_tokens$[1]
		msg_tokens$[1]=open_status$
		gosub disp_message
		status=912
		goto std_exit
	endif

	return
	
disp_message: rem --- Display Message Dialog

	call sypdir$+"bac_message.bbj",
:		msg_id$,
:		msg_tokens$[all],
:		msg_opt$,
:		table_chans$[all]

	return
	
rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
    call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:   str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
	
rem #endinclude std_end.src

    end
